SUBROUTINE READ_OSISAF_SIC_NC(CFILE,MAXOBS,NTOBS,ZDATA,&
          & ZLLON,ZLLAT,ZERR)

USE SET_KND
USE RUN
USE NETCDF
USE IOUNITS


IMPLICIT NONE

CHARACTER(LEN=*), INTENT(IN) :: CFILE
INTEGER(I4), INTENT(IN) :: MAXOBS
INTEGER(I4), INTENT(OUT) :: NTOBS
REAL   (R8), DIMENSION(MAXOBS), INTENT(OUT) :: ZDATA,&
          & ZLLON,ZLLAT,ZERR
INTEGER(I4) :: NX, NY,NT
REAL(R4), ALLOCATABLE ,  DIMENSION(:,:)  :: ZOLON,ZOLAT
REAL(R4), ALLOCATABLE ,  DIMENSION(:,:,:)  :: ZOSIC,ZOERR
INTEGER , ALLOCATABLE ,  DIMENSION(:,:,:) :: ZOFLAG

INTEGER(I4) :: STAT,IDIM,IDVAR,NCID,JX,JY,JT
REAL(R8) :: SCALE_FACTORSIC,SCALE_FACTORERR,MISSVALERR,MISSVALSIC

WRITE(IOUNLOG,*) ' :: OPENING OSISAF SIC FILE '//TRIM(CFILE)



STAT=NF90_OPEN(CFILE,NF90_NOWRITE, NCID)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

!WRITE(IOUNLOG,*) 'xc '
STAT = NF90_INQ_DIMID (NCID, 'xc', IDIM)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_INQUIRE_DIMENSION (NCID, IDIM, LEN = NX)

!WRITE(IOUNLOG,*) 'yc '
STAT = NF90_INQ_DIMID (NCID, 'yc', IDIM)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_INQUIRE_DIMENSION (NCID, IDIM, LEN = NY)

!WRITE(IOUNLOG,*) 'time '
STAT = NF90_INQ_DIMID (NCID, 'time', IDIM)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_INQUIRE_DIMENSION (NCID, IDIM, LEN = NT)

ALLOCATE(ZOLON(NX,NY),ZOLAT(NX,NY))
ALLOCATE(ZOSIC(NX,NY,NT),ZOERR(NX,NY,NT),ZOFLAG(NX,NY,NT))

!WRITE(IOUNLOG,*) 'lon '
STAT = NF90_INQ_VARID (NCID, 'lon', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (NCID,IDVAR,ZOLON)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

!WRITE(IOUNLOG,*) 'lat '

STAT = NF90_INQ_VARID (NCID, 'lat', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (NCID,IDVAR,ZOLAT)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

!WRITE(IOUNLOG,*) 'ice_conc '

STAT = NF90_INQ_VARID (NCID, 'ice_conc', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (NCID,IDVAR,ZOSIC)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_ATT (NCID, IDVAR, 'scale_factor',SCALE_FACTORSIC) ! after check with missval
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_GET_ATT(NCID, IDVAR, '_FillValue',MISSVALSIC)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

WHERE(ZOSIC /=MISSVALSIC ) ZOSIC=ZOSIC*SCALE_FACTORSIC/100.


!WRITE(IOUNLOG,*) 'total_standard_error '

STAT = NF90_INQ_VARID (NCID, 'total_standard_error', IDVAR)
IF (STAT /= NF90_NOERR) THEN
        STAT = NF90_INQ_VARID (NCID, 'total_standard_uncertainty', IDVAR)
        IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
ENDIF

STAT = NF90_GET_VAR (NCID,IDVAR,ZOERR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_ATT (NCID, IDVAR, 'scale_factor',SCALE_FACTORERR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_ATT(NCID, IDVAR, '_FillValue',MISSVALERR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

WHERE(ZOERR /=MISSVALERR ) ZOERR=ZOERR*SCALE_FACTORERR/100.

!WHERE(ZOERR .LT. 0.05 .AND. ZOERR /=MISSVALERR ) ZOERR=0.05


WRITE(IOUNLOG,*) ' :: REDUCING FROM [0-100] to [0-1]  '

STAT = NF90_INQ_VARID (NCID, 'status_flag', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (NCID,IDVAR,ZOFLAG)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
!no check on the time
!STAT = NF90_INQ_VARID (NCID, 'time', IDVAR)
!IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
!STAT = NF90_GET_VAR (NCID,IDVAR,ZOTIM)
!IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
!!seconds from 19780101
!CALL YMDS2JU(1978,01,01,0._DP,ZJUL1978)

STAT=NF90_CLOSE(NCID)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

NTOBS=0

DO JT=1,NT
 DO JY=1,NY
  DO JX=1,NX
        IF(ZOSIC(JX,JY,JT).GE. 0. .AND. ZOSIC(JX,JY,JT).LE. 1. .AND. &
        ZOSIC(JX,JY,JT) /= MISSVALSIC .AND. ZOERR(JX,JY,JT) /=MISSVALERR .AND. &
         ( ZOFLAG(JX,JY,JT) == 0 .OR. ZOFLAG(JX,JY,JT) == 4 ) ) THEN
          NTOBS=NTOBS+1
          IF(NTOBS.GT.MAXOBS) &
          & CALL ABOR1('READ_OSISAF_SIC_NC   : MAXOBS TO INCREASE')
          ZDATA(NTOBS) = REAL(ZOSIC(JX,JY,JT),KIND=R8)
          ZLLON(NTOBS) = REAL(ZOLON(JX,JY),KIND=R8)
          ZLLAT(NTOBS) = REAL(ZOLAT(JX,JY),KIND=R8)
          ZERR(NTOBS)  = REAL(ZOERR(JX,JY,JT),KIND=R8)
        ENDIF
  ENDDO
 ENDDO
ENDDO

WRITE(IOUNLOG,*) ' --> FOUND ',NTOBS,' OBSERVATIONS'
DEALLOCATE(ZOSIC,ZOLAT,ZOERR,ZOLON,ZOFLAG)

END SUBROUTINE READ_OSISAF_SIC_NC
