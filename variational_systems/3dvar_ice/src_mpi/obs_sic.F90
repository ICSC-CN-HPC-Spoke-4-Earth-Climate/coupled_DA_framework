#undef SHARED_MEMORY
SUBROUTINE OBS_SIC

!-----------------------------------------------------------------------
!                                                                      !
! APPLY OBSERVATIONAL OPERATOR FOR SIC DATA                            !
!                                                                      !
!-----------------------------------------------------------------------


 USE SET_KND
 USE GRD_STR
 USE OBSDEF
 USE OBS_STR
 USE READFG
 USE CTL_STR
 USE IOUNITS , ONLY : IOUNERR,IOUNLOG,IOUNOUT
 USE RUN, ONLY : RTIMES1950,NTSTEPS
 USE READFG
 USE MYFRTPROF, ONLY : MYFRTPROF_WALL

 IMPLICIT NONE

 INTEGER(I4)   ::  I, J, D, KK,JTSTEP,KSTEP,JP,FIRSTTS
 INTEGER(I4)   ::  KNUM

CALL MYFRTPROF_WALL('OBS_SIC: COMPUTE SIC BACKGROUND VALUES',0)

 WRITE(IOUNOUT,*) ' ENTERING OBS_SIC'

FIRST : DO JTSTEP=1,NTSTEPS
  IF( LL_FGASSIM(JTSTEP) ) THEN
      FIRSTTS=JTSTEP
      EXIT FIRST
  ENDIF
ENDDO FIRST

#ifdef SHARED_MEMORY
!$OMP PARALLEL DEFAULT(SHARED),PRIVATE(KK,KSTEP,JTSTEP,I,J,JP)
!$OMP DO SCHEDULE(DYNAMIC,1)
#endif
 CYOBS : DO KK = 1,SIC%NO

  IF(SIC%FLC(KK).NE.1 ) CYCLE CYOBS

  KSTEP = MINLOC( ABS(RTIMES1950(1:NTSTEPS)-SIC%TIM(KK) ), DIM=1 ) - (FIRSTTS-1)
  IF(KSTEP .LT. 1 .OR. KSTEP .GT. NFGTSTEPS) THEN
     WRITE(IOUNOUT,*) ' OBS_SIC: FOUND KSTEP EQUAL TO ',KSTEP
     WRITE(IOUNOUT,*) ' OBS_SIC: TIME EQUAL TO ',SIC%TIM(KK)
     WRITE(IOUNOUT,*) ' LL_FGASSIM : ',LL_FGASSIM
     WRITE(IOUNOUT,*) ' RTIMES1950 : ',RTIMES1950(1:NTSTEPS)
     CALL ABOR1('OBS_SIC: KSTEP OUT OF RANGE')
  ENDIF

  SIC%BAC(KK) = 0._R8
  DO JP=1,NPQ
       SIC%BAC(KK) = SIC%BAC(KK) + SIC%PQ(KK,JP)*GRD%SICB(SIC%IB(KK,JP),SIC%JB(KK,JP),KSTEP)
  ENDDO

  SIC%RES(KK) = SIC%VAL(KK) - SIC%BAC(KK)

 ENDDO CYOBS
#ifdef SHARED_MEMORY
!$OMP END DO
!$OMP END PARALLEL
#endif

 WRITE(IOUNLOG,*)
 WRITE(IOUNLOG,*) ' *** SIC OBSERVATIONS OBSOP COMPLETED'

CALL MYFRTPROF_WALL('OBS_SIC: COMPUTE SIC BACKGROUND VALUES',1)
END SUBROUTINE OBS_SIC
