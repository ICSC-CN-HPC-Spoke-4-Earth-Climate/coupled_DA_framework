#undef SHARED_MEMORY
SUBROUTINE THINOUT_SIT(ITER,ZIN1,ZIN2)

!.. THINNING OF SIT OBSERVATIONS
!..
!.. FOR EACH SATELLITE DEFINE 2D THINNING
!.. BOXES, WITHIN WHICH ONLY THE OBS CLOSER TO
!.. ANALYSIS TIME IS RETAINED.
!..

USE SET_KND
USE OBSDEF
USE OBS_STR, ONLY : SIT, NPQ
USE GRD_STR
USE IOUNITS, ONLY : IOUNLOG,IOUNOUT
USE MYFRTPROF

IMPLICIT NONE

 INTEGER(I4),INTENT(IN)   ::  ITER
 REAL(R8),   INTENT(IN)   ::  ZIN1, ZIN2
 REAL(R8) :: XRES,YRES,ZAUX,ZM
 INTEGER(I4)   ::  K,JOBS,IIND,JIND,KOKTH,KKOTH,NX,NY,MAXOBS,OIND
 INTEGER(I4)   ::  JX,JY,JSAT,KEVE_HTHN,PKN
 REAL(DP), ALLOCATABLE :: ZDIST(:,:)
 INTEGER(I4), ALLOCATABLE :: KCOUNTTH(:,:),KGOOD(:,:),KOBS(:,:,:)

#include "obs_events.h"

 CALL MYFRTPROF_WALL('THINOUT_SIT: SIT DATA THINNING',0)

  IF(SIT%NO.LE.0) THEN
    CALL MYFRTPROF_WALL('THINOUT_SIT: SIT DATA THINNING',1)
    RETURN
  ENDIF


  WRITE(IOUNOUT,*) ' *** SIT THINNING - ITERATION ',ITER
  WRITE(IOUNLOG,*) ' *** SIT THINNING - ITERATION ',ITER

  KOKTH=0
  KKOTH=0
  XRES=ZIN1
  YRES=ZIN2

  IF(ITER.EQ.1) THEN
     PKN=1
     KEVE_HTHN = KEVE_HTH1
  ELSEIF(ITER.EQ.2) THEN
     PKN=2
     KEVE_HTHN = KEVE_HTH2
  ELSE
     CALL ABOR1('THINOUT: UNSUPPORTED NUMBER OF ITERATION')
  ENDIF


  NX=INT( GRD%IM / XRES ) + 1
  NY=INT( GRD%JM / YRES ) + 1

  MAXOBS = INT(SIT%NO / 200.)

  WRITE(IOUNLOG,*) '*** SIT THINNING PRE-ALLOCATION'
  WRITE(IOUNLOG,*) '    ZIN1, ZIN2  == ', ZIN1 ,ZIN2
  WRITE(IOUNLOG,*) '    PKN         == ', PKN
  WRITE(IOUNLOG,*) '    XRES, YRES  == ', XRES ,YRES
  WRITE(IOUNLOG,*) '    NX, NY      == ', NX, NY
  CALL FLUSH(IOUNLOG)

  ALLOCATE(KCOUNTTH(NX,NY),  &
         & ZDIST(NX,NY),  &
         & KGOOD(NX,NY) )


 SITSAT : DO JSAT=1,NOSITSATS

  IF(SIT%ISITOBS(JSAT) .LE. 0 ) CYCLE SITSAT

  WRITE(IOUNOUT,*) 'THINNING FOR SATELLITE '//TRIM(CSITSATID(JSAT))
  WRITE(IOUNLOG,*) 'THINNING FOR SATELLITE '//TRIM(CSITSATID(JSAT)),&
  & ', FOUND',SIT%ISITOBS(JSAT),' OBS'

  MAXOBS = COUNT(SIT%FLC(1:SIT%NO).EQ.1 .AND. SIT%KSAT(1:SIT%NO) .EQ. JSAT)
  WRITE(IOUNLOG,*) '    SAT ', JSAT, ' MAXOBS = ',MAXOBS
  MAXOBS = MAX( NINT(MAXOBS/100._R8), 100)
  WRITE(IOUNLOG,*) '    ALLOCATION = ',MAXOBS
  ALLOCATE( KOBS(NX,NY,MAXOBS) )

  KCOUNTTH=0
  ZDIST = 1000000._DP
  KGOOD=0

#ifdef SHARED_MEMORY
!$OMP PARALLEL DEFAULT(SHARED), PRIVATE(K,IIND,JIND,....)
!$OMP DO SCHEDULE(STATIC,1)
#endif
  CYOBS5: DO K=1,SIT%NO

       IF(SIT%FLC(K).EQ.0 .OR. SIT%KSAT(K) .NE. JSAT) CYCLE CYOBS5

       !... COORDINATES ON PROJECTED PLAN
       IIND = INT((REAL(SIT%IB(K,PKN),KIND=R8))/XRES) + 1
       JIND = INT((REAL(SIT%JB(K,PKN),KIND=R8))/YRES) + 1

       KCOUNTTH(IIND,JIND) = KCOUNTTH(IIND,JIND) + 1
       OIND = KCOUNTTH(IIND,JIND)
       IF(OIND.GT. MAXOBS) &
       & CALL ABOR1('THINOUT: MAXOBS MUST BE INCREASED')
       KOBS(IIND,JIND,OIND) = K

       IF(ABS(SIT%TDIST(K)) .LT. ZDIST(IIND,JIND) ) THEN
              ZDIST(IIND,JIND) = ABS(SIT%TDIST(K))
              KGOOD(IIND,JIND) = K
       ENDIF

  ENDDO CYOBS5
#ifdef SHARED_MEMORY
!$OMP END DO
!$OMP END PARALLEL
#endif

  DO JY=1,NY
    CYX : DO JX=1,NX

      IF(KCOUNTTH(JX,JY) .GT. 0 ) THEN
          IF (KGOOD(JX,JY).EQ.0 ) &
          & CALL ABOR1('SIT THINNING, PROBLEMS WITH MINIMUM DIST')

            DO K= 1, KCOUNTTH(JX,JY)
               IF( KGOOD(JX,JY) .NE. KOBS(JX,JY,K) ) THEN
                   JOBS = KOBS(JX,JY,K)
                   SIT%FLC(JOBS) = 0
                   SIT%EVE(JOBS) = KEVE_HTHN
                   KKOTH = KKOTH +1
               ELSE
                   KOKTH = KOKTH + 1
               ENDIF
            ENDDO

      ENDIF

    ENDDO CYX
  ENDDO

  DEALLOCATE( KOBS )

 ENDDO SITSAT

  WRITE(IOUNOUT,*) ' END SIT THINNING - ITERATION ',ITER
  WRITE(IOUNLOG,*)
  WRITE(IOUNLOG,*) ' *** THINNING REPORT - ITERATION ',ITER
  WRITE(IOUNLOG,*) ' THINNING GRID : ',NX,' X',NY
  WRITE(IOUNLOG,*) ' FILTERED-OUT  : ',KKOTH
  WRITE(IOUNLOG,*) ' RETAINED      : ',KOKTH
  WRITE(IOUNLOG,*) ' TOTAL         : ',KOKTH + KKOTH
  WRITE(IOUNLOG,*) ' FLAGS COUNT   : ',SUM(SIT%FLC(1:SIT%NO))

  SIT%NC = KOKTH

  DEALLOCATE(KCOUNTTH,  &
         & ZDIST,  &
         & KGOOD )


  CALL MYFRTPROF_WALL('THINOUT_SIT: SIT DATA THINNING',1)

END SUBROUTINE THINOUT_SIT
