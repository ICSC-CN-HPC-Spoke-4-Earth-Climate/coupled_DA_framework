SUBROUTINE READ_ESACRYO2_SIT_NC(CFILE,MAXOBS,NTOBS,ZDATA,&
          & ZLLON,ZLLAT,ZERR)

USE SET_KND
USE RUN
USE NETCDF
USE IOUNITS
USE OBSHANDLING

IMPLICIT NONE

CHARACTER(LEN=*), INTENT(IN) :: CFILE
INTEGER(I4), INTENT(IN) :: MAXOBS
INTEGER(I4), INTENT(OUT) :: NTOBS
REAL   (R8), DIMENSION(MAXOBS), INTENT(OUT) :: ZDATA,&
          & ZLLON,ZLLAT,ZERR
INTEGER(I4) :: NX, NY,NT
REAL(R4), ALLOCATABLE ,  DIMENSION(:,:)  :: ZOLON,ZOLAT
REAL(R4), ALLOCATABLE ,  DIMENSION(:,:,:)  :: ZOSIT,ZOERR
INTEGER , ALLOCATABLE ,  DIMENSION(:,:,:) :: ZOFLAG

INTEGER(I4) :: STAT,IDIM,IDVAR,NCID,JX,JY,JT
REAL(R8) :: SCALE_FACTORSIT,SCALE_FACTORERR,MISSVALERR,MISSVALSIT

WRITE(IOUNLOG,*) ' :: OPENING CRYOSAT2 SIT FILE '//TRIM(CFILE)



STAT=NF90_OPEN(CFILE,NF90_NOWRITE, NCID)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

!WRITE(IOUNLOG,*) 'xc '
STAT = NF90_INQ_DIMID (NCID, 'xc', IDIM)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_INQUIRE_DIMENSION (NCID, IDIM, LEN = NX)

!WRITE(IOUNLOG,*) 'yc '
STAT = NF90_INQ_DIMID (NCID, 'yc', IDIM)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_INQUIRE_DIMENSION (NCID, IDIM, LEN = NY)

!WRITE(IOUNLOG,*) 'time '
STAT = NF90_INQ_DIMID (NCID, 'time', IDIM)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_INQUIRE_DIMENSION (NCID, IDIM, LEN = NT)

ALLOCATE(ZOLON(NX,NY),ZOLAT(NX,NY))
ALLOCATE(ZOSIT(NX,NY,NT),ZOERR(NX,NY,NT),ZOFLAG(NX,NY,NT))

!WRITE(IOUNLOG,*) 'lon '
STAT = NF90_INQ_VARID (NCID, 'lon', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (NCID,IDVAR,ZOLON)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

!WRITE(IOUNLOG,*) 'lat '

STAT = NF90_INQ_VARID (NCID, 'lat', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (NCID,IDVAR,ZOLAT)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

!WRITE(IOUNLOG,*) 'ice_conc '

STAT = NF90_INQ_VARID (NCID, 'sea_ice_thickness', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (NCID,IDVAR,ZOSIT)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_GET_ATT (NCID, IDVAR, 'scale_factor',SCALE_FACTORSIT) ! after check with missval
IF (STAT /= NF90_NOERR) SCALE_FACTORSIT=1._R8
MISSVALSIT=-999.
STAT = NF90_GET_ATT(NCID, IDVAR, '_FillValue',MISSVALSIT)
IF (STAT /= NF90_NOERR) THEN
    WHERE( .NOT. isnan(ZOSIT) ) ZOSIT=ZOSIT*SCALE_FACTORSIT
ELSE
    WHERE(ZOSIT /=MISSVALSIT ) ZOSIT=ZOSIT*SCALE_FACTORSIT
ENDIF

!WRITE(IOUNLOG,*) 'total_standard_error '

STAT = NF90_INQ_VARID (NCID, 'sea_ice_thickness_uncertainty', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (NCID,IDVAR,ZOERR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_ATT (NCID, IDVAR, 'scale_factor',SCALE_FACTORERR) ! after check with missval
IF (STAT /= NF90_NOERR) SCALE_FACTORERR=1._R8
MISSVALERR=-999.
STAT = NF90_GET_ATT(NCID, IDVAR, '_FillValue',MISSVALERR)
IF (STAT /= NF90_NOERR) THEN
    WHERE( .NOT. isnan(ZOERR) ) ZOERR=ZOERR*SCALE_FACTORERR
ELSE
    WHERE(ZOERR /=MISSVALERR ) ZOERR=ZOERR*SCALE_FACTORERR
ENDIF

STAT = NF90_INQ_VARID (NCID, 'quality_flag', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (NCID,IDVAR,ZOFLAG)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
!no check on the time
!STAT = NF90_INQ_VARID (NCID, 'time', IDVAR)
!IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
!STAT = NF90_GET_VAR (NCID,IDVAR,ZOTIM)
!IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
!!seconds from 19780101
!CALL YMDS2JU(1978,01,01,0._DP,ZJUL1978)

STAT=NF90_CLOSE(NCID)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

NTOBS=0

DO JT=1,NT
 DO JY=1,NY
  DO JX=1,NX
        !WRITE(IOUNLOG,*)JX,JY,JT,ZOSIT(JX,JY,JT),ZOERR(JX,JY,JT),ZOFLAG(JX,JY,JT)
        IF (ISNAN(ZOSIT(JX,JY,JT)) .OR. ISNAN(ZOERR(JX,JY,JT) )) CYCLE
        IF(ZOSIT(JX,JY,JT).GE. 0.5_R8 .AND. ZOSIT(JX,JY,JT).LE. 9._R8 .AND. &
        & ZOSIT(JX,JY,JT) /= MISSVALSIT .AND. ZOERR(JX,JY,JT) /=MISSVALERR .AND. ( ZOFLAG(JX,JY,JT) == 1  ) ) THEN
          NTOBS=NTOBS+1
          IF(NTOBS.GT.MAXOBS) &
          & CALL ABOR1('READ_ESACRYO2_SIT_NC   : MAXOBS TO INCREASE')
          ZDATA(NTOBS) = REAL(ZOSIT(JX,JY,JT),KIND=R8)
          ZLLON(NTOBS) = REAL(ZOLON(JX,JY),KIND=R8)
          ZLLAT(NTOBS) = REAL(ZOLAT(JX,JY),KIND=R8)
          ZERR(NTOBS)  = REAL(ZOERR(JX,JY,JT),KIND=R8)*COEFF_SIT_ERR_CRYO
        ENDIF
  ENDDO
 ENDDO
ENDDO

WRITE(IOUNLOG,*) ' --> FOUND ',NTOBS,' OBSERVATIONS'
DEALLOCATE(ZOSIT,ZOLAT,ZOERR,ZOLON,ZOFLAG)

END SUBROUTINE READ_ESACRYO2_SIT_NC
