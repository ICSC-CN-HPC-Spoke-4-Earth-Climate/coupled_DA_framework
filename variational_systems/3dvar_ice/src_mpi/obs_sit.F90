#undef SHARED_MEMORY
SUBROUTINE OBS_SIT

!-----------------------------------------------------------------------
!                                                                      !
! APPLY OBSERVATIONAL OPERATOR FOR SIT DATA                            !
!                                                                      !
!-----------------------------------------------------------------------


 USE SET_KND
 USE GRD_STR
 USE OBSDEF
 USE OBS_STR
 USE READFG
 USE CTL_STR
 USE IOUNITS , ONLY : IOUNERR,IOUNLOG,IOUNOUT
 USE RUN, ONLY : RTIMES1950,NTSTEPS
 USE READFG
 USE MYFRTPROF, ONLY : MYFRTPROF_WALL

 IMPLICIT NONE

 INTEGER(I4)   ::  I, J, D, KK,JTSTEP,KSTEP,JP,FIRSTTS
 INTEGER(I4)   ::  KNUM

CALL MYFRTPROF_WALL('OBS_SIT: COMPUTE SIT BACKGROUND VALUES',0)

 WRITE(IOUNOUT,*) ' ENTERING OBS_SIT'

FIRST : DO JTSTEP=1,NTSTEPS
  IF( LL_FGASSIM(JTSTEP) ) THEN
      FIRSTTS=JTSTEP
      EXIT FIRST
  ENDIF
ENDDO FIRST

#ifdef SHARED_MEMORY
!$OMP PARALLEL DEFAULT(SHARED),PRIVATE(KK,KSTEP,JTSTEP,I,J,JP)
!$OMP DO SCHEDULE(DYNAMIC,1)
#endif
 CYOBS : DO KK = 1,SIT%NO

  IF(SIT%FLC(KK).NE.1 ) CYCLE CYOBS

  KSTEP = MINLOC( ABS(RTIMES1950(1:NTSTEPS)-SIT%TIM(KK) ), DIM=1 ) - (FIRSTTS-1)
  IF(KSTEP .LT. 1 .OR. KSTEP .GT. NFGTSTEPS) THEN
     WRITE(IOUNOUT,*) ' OBS_SIT: FOUND KSTEP EQUAL TO ',KSTEP
     WRITE(IOUNOUT,*) ' OBS_SIT: TIME EQUAL TO ',SIT%TIM(KK)
     WRITE(IOUNOUT,*) ' LL_FGASSIM : ',LL_FGASSIM
     WRITE(IOUNOUT,*) ' RTIMES1950 : ',RTIMES1950(1:NTSTEPS)
     CALL ABOR1('OBS_SIT: KSTEP OUT OF RANGE')
  ENDIF

  SIT%BAC(KK) = 0._R8
  DO JP=1,NPQ
       SIT%BAC(KK) = SIT%BAC(KK) + SIT%PQ(KK,JP)*GRD%SITB(SIT%IB(KK,JP),SIT%JB(KK,JP),KSTEP)
  ENDDO

  SIT%RES(KK) = SIT%VAL(KK) - SIT%BAC(KK)

 ENDDO CYOBS
#ifdef SHARED_MEMORY
!$OMP END DO
!$OMP END PARALLEL
#endif

 WRITE(IOUNLOG,*)
 WRITE(IOUNLOG,*) ' *** SIT OBSERVATIONS OBSOP COMPLETED'

CALL MYFRTPROF_WALL('OBS_SIT: COMPUTE SIT BACKGROUND VALUES',1)
END SUBROUTINE OBS_SIT
