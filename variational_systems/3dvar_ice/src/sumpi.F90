SUBROUTINE SUMPI

USE SET_KND, ONLY : I4
#ifndef NOMPI
USE MPI
#endif
USE MPIREL
USE IOUNITS
USE RUN
USE OMP_LIB
USE GRD_STR

IMPLICIT NONE

INTEGER(KIND=I4) :: IERR, IRET
INTEGER(KIND=I4) :: IAUX, JI, NMPIDOMS, NTHRDS
NAMELIST / MPILST / LLUSEMPI, NIOMASTER, NIOMASTERB, NMPIDOMS
#ifdef NOMPI
INTEGER(KIND=I4), PARAMETER :: MPI_COMM_WORLD=1, MPI_SUCCESS=0
#endif

CMPIDOM='    '

!... OPEN NAMELIST
OPEN(IOUNNAM,FILE='var_3d_nml',STATUS='OLD',IOSTAT=IERR)
IF(IERR.NE.0) CALL ABOR1('PROBLEMS OPENING NAMELIST')

!... SET DEFAULTS AND READ NAMELIST
LLUSEMPI=.FALSE.
NIOMASTER=1
NIOMASTERB=1

CALL POSNAM(IOUNNAM,'MPILST')
READ(IOUNNAM,MPILST)

!$OMP PARALLEL DEFAULT(SHARED)
NTHRDS = OMP_GET_NUM_THREADS()
!$OMP END PARALLEL

WRITE(IOUNOUT,*) 'NUMBER OF OMP THREADS  :', NTHRDS

IF(LLUSEMPI) THEN

!... INITIALIZE

#ifdef NOMPI
   CALL MPI_INIT(IERR)
#else
   CALL MPI_INIT(IERR)
   !CALL MPI_INIT_THREAD(MPI_THREAD_FUNNELED,IRET,IERR)
   !WRITE(IOUNLOG,*) ' MPI_INIT_THREAD RETURNED ', IRET
   !WRITE(IOUNLOG,*) ' MPI_THREAD_SINGLE :', MPI_THREAD_SINGLE
   !WRITE(IOUNLOG,*) ' MPI_THREAD_FUNNELED :', MPI_THREAD_FUNNELED
   !WRITE(IOUNLOG,*) ' MPI_THREAD_SERIALIZED :',  MPI_THREAD_SERIALIZED
   !WRITE(IOUNLOG,*) ' MPI_THREAD_MULTIPLE :', MPI_THREAD_MULTIPLE
#endif

   IF (IERR.NE.MPI_SUCCESS) CALL ABOR1('SUMPI: MPI_INIT ERROR')

!... GET THE NUMBER OF PROCESSES.

   CALL MPI_COMM_SIZE (MPI_COMM_WORLD, NPROCS, IERR)
   IF (IERR.NE.MPI_SUCCESS) CALL ABOR1('SUMPI: MPI_COMM_SIZE ERROR')

   CALL MPI_COMM_RANK (MPI_COMM_WORLD, MYPROC, IERR)
   IF (IERR.NE.MPI_SUCCESS) CALL ABOR1('SUMPI: MPI_COMM_SIZE ERROR')

   WRITE(IOUNOUT,*)
   WRITE(IOUNOUT,*) 'NUMBER OF MPI PROCESSES:', NPROCS

   IF(NPROCS .NE. NMPIDOMS ) THEN
       WRITE(IOUNOUT,*) ' NMPIDOMS = ',NMPIDOMS
       CALL ABOR1('SUMPI: MISMATCH BETWEEN PROCS AND DOMAINS')
   ENDIF

   WRITE(CMPIDOM,'(I4.4)') MYPROC
   MYPROC=MYPROC+1

   LMPION=.TRUE.

ELSE

   WRITE(IOUNOUT,*)
   WRITE(IOUNOUT,*) 'SINGLE-PROCESS RUN'

   LMPION=.FALSE.
   NPROCS=1
   MYPROC=1
   NIOMASTER=1
   NIOMASTERB=1

ENDIF

IF(LMPION) THEN
   OPEN(UNIT=99,FILE='mpp_conf.dat',STATUS='OLD')
   READ(99,*) IAUX
   IF( IAUX .NE. NMPIDOMS ) CALL ABOR1('SUMPI: MISMATCH BETWEEN LIMITS AND DOMAINS')
   ALLOCATE( MP_SD(NPROCS), MP_SDST(NPROCS), MP_XST(NPROCS), MP_XEN(NPROCS), &
   & MP_YST(NPROCS), MP_YEN(NPROCS) )
   DO JI=1,NMPIDOMS
      READ(99,*) MP_SD(JI),MP_SDST(JI),MP_XST(JI),MP_XEN(JI),MP_YST(JI),MP_YEN(JI)
   ENDDO
   CLOSE(99)
   NN_DOMSTART=MP_SDST(MYPROC)
ELSE
   ALLOCATE( MP_SD(NPROCS), MP_SDST(NPROCS), MP_XST(NPROCS), MP_XEN(NPROCS), &
   & MP_YST(NPROCS), MP_YEN(NPROCS) )
   MP_SD(1)  =1
   MP_SDST(1)=1
   MP_XST(1) = 1
   MP_XEN(1) = GRD%IM
   MP_YST(1) = 1
   MP_YEN(1) = GRD%JM
   NN_DOMSTART=1
ENDIF

!... INIT LOGFILE ACCORDINGLY

! NO I/O HAS TO BE DONE BEFORE THE CALL TO SULOGF
CALL SULOGF

!... REPORT TIME VARS

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' THE PROGRAM HAS BEEN CALLED AS FOLLOWS:'
WRITE(IOUNLOG,*) TRIM(CARGS)
WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' ANALYSIS DATE         :',IANDATE
WRITE(IOUNLOG,*) ' ANALYSIS TIME         :',IANTIME
WRITE(IOUNLOG,*) ' ASSIMILATION WINDOW   :',IANTW
WRITE(IOUNLOG,*) ' ASSIM WINDOW CENTERED :',LL_TWCENTERED
WRITE(IOUNLOG,*) ' YY MM DD              :   ',IANYY,IANMM,IANDD
WRITE(IOUNLOG,*) ' HH MIN                :   ',IANHH,IANMI
WRITE(IOUNLOG,*) ' SECONDS               :   ',ZANSS
WRITE(IOUNLOG,*) ' JULIAN DAY            :   ',ZJULIAN
WRITE(IOUNLOG,*) ' JULIAN DAY  START     :   ',ZJULSTART
WRITE(IOUNLOG,*) ' JULIAN DAY  END       :   ',ZJULEND
WRITE(IOUNLOG,*) ' JULIAN DAY SINCE  1950:   ',ZANJUL1950
WRITE(IOUNLOG,*) ' JULIAN DAY START  1950:   ',ZJULSTART-ZJUL1950
WRITE(IOUNLOG,*) ' JULIAN DAY END    1950:   ',ZJULEND-ZJUL1950
WRITE(IOUNLOG,*) ' JULIAN DAY BEG.MM     :   ',ZJULSTARTMM
WRITE(IOUNLOG,*) ' JULIAN DAY BEG.MM 1950:   ',ZJULSTARTMM-ZJUL1950

WRITE(IOUNLOG,*)

!... REPORT THE STATUS IN THE LOGFILE

WRITE(IOUNLOG,*) ' ----------------------------------------'
WRITE(IOUNLOG,*) ' :: ROUTINE SUMPI : MPI INITIALIZATION ::'
WRITE(IOUNLOG,*) ' ----------------------------------------'
WRITE(IOUNLOG,*) ' LLUSEMPI   ',LLUSEMPI
WRITE(IOUNLOG,*) ' LMPION     ',LMPION
WRITE(IOUNLOG,*) ' NPROCS     ',NPROCS
WRITE(IOUNLOG,*) ' MYPROC     ',MYPROC
WRITE(IOUNLOG,*) ' NIOMASTER  ',NIOMASTER
WRITE(IOUNLOG,*) ' NIOMASTERB ',NIOMASTERB
WRITE(IOUNLOG,*) ' ----------------------------------------'

RETURN
END SUBROUTINE SUMPI
