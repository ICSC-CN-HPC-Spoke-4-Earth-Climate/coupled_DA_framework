SUBROUTINE READSLA

USE IOUNITS
USE RUN
USE SET_KND
USE OBSDEF
USE OBS_STR, ONLY : SLA
USE GRD_STR
USE OBSHANDLING
USE ALLOCOBS

IMPLICIT NONE

INTEGER(I4) :: IOBS,IERR,JSAT,KFILE,KSAT,ISTART,K,IJ,JOBS,KKOBS
LOGICAL :: LLEXIST
INTEGER(I4),PARAMETER :: MAX_SLAOBS = 2000000
REAL(R8)          :: DTSTART,DTEND
INTEGER(I4)       :: KTRC(MAX_SLAOBS)
CHARACTER(LEN=30) :: CFILE
REAL(R8),DIMENSION(MAX_SLAOBS) :: ZLON,ZLAT,ZVAL
REAL(DP),DIMENSION(MAX_SLAOBS) :: ZTIME
LOGICAL :: L_RT

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) '... ENTERING READSLA ...'

DTSTART = ZJULSTART - ZJUL1950
DTEND   = ZJULEND   - ZJUL1950

WRITE(IOUNLOG,*) ' STARTDATE (JULIAN DAYS SINCE 1950) : ', DTSTART
WRITE(IOUNLOG,*) ' END  DATE (JULIAN DAYS SINCE 1950) : ', DTEND
WRITE(IOUNLOG,*)

SLA%ISLAOBS(:) = 0
SLA%NO=0

! ---
! LEVEL CORRESPONDING TO THE MINIMUM DEPTH
  SLA%KDP=GRD%KM
  DO K=GRD%KM, 1, -1
   IF(GRD%DEP(K).GE.SLA%DEP) SLA%KDP = K
  ENDDO

  CALL ALLOCSLA(MAX_SLAOBS,.TRUE.)

  L_RT = ( CSLA_OPT .EQ. 'RT' )
  IF ( .NOT. L_RT .AND. CSLA_OPT .NE. 'DT' ) &
  CALL ABOR1('READSLA : CSLA_OPT UNKNOWN')

SAT : DO JSAT = 1,NOSLASATS

   WRITE(IOUNLOG,*)
   WRITE(IOUNLOG,*) '================= '//TRIM(CSLASATID(JSAT))//' ===='
   IF( .NOT.SLA%USES(JSAT)  .OR. IANDATE/100 .LT. ISLASATSTART(JSAT) .OR. &
     & IANDATE/100 .GT. ISLASATEND(JSAT) ) THEN
     WRITE(IOUNLOG,*) 'NOT_FOUND'
     CYCLE SAT
   ENDIF

   LLEXIST=.TRUE.
   KFILE=0
   KSAT=JSAT

   FILES: DO WHILE(LLEXIST)

        KFILE=KFILE+1
        WRITE(CFILE,'(A,I2.2)')  TRIM(CSLASATID(KSAT)),KFILE
        INQUIRE(FILE=CFILE,EXIST=LLEXIST)
        IF(.NOT.LLEXIST) THEN
           EXIT FILES
        ENDIF

        WRITE(IOUNLOG,*) 'FOUND FILE '//TRIM(CFILE)

        IF ( CSLA_FMT .EQ. 'OLD' ) THEN
           CALL READNETCDF_SLA_ATP &
           & (TRIM(CFILE),'SLA',DTSTART,DTEND,MAX_SLAOBS,IOBS,&
           &  ZLON,ZLAT,ZTIME,ZVAL,KTRC,IOUNLOG)
	ELSEIF ( CSLA_FMT .EQ. 'NEW' ) THEN
           CALL READNETCDF_SLA_NEW &
           & (TRIM(CFILE),'SLA',DTSTART,DTEND,MAX_SLAOBS,IOBS,L_RT,&
           &  ZLON,ZLAT,ZTIME,ZVAL,KTRC,IOUNLOG)
	ELSE
	  CALL ABOR1('READSLA: UNKNOWN CSLA_FMT')
	ENDIF

        IF(IOBS.GT.0 ) THEN
          DO KKOBS=1,IOBS
             IF(ABS(ZVAL(KKOBS)).LT.1000_R8) THEN
               SLA%NO = SLA%NO + 1
               IF( SLA%NO .GT. MAX_SLAOBS ) &
               & CALL ABOR1('FATAL IN READSLA: MAX_SLAOBS MUST BE INCREASED')
               SLA%LON(SLA%NO) = ZLON(KKOBS)
               SLA%LAT(SLA%NO) = ZLAT(KKOBS)
               SLA%VAL(SLA%NO) = ZVAL(KKOBS)
               SLA%TIM(SLA%NO) = ZTIME(KKOBS)
               SLA%TRACK(SLA%NO) = KTRC(KKOBS)
               SLA%KSAT(SLA%NO) = JSAT
             ENDIF
          ENDDO
          SLA%ISLAOBS(JSAT) = SLA%NO
        ENDIF

   ENDDO FILES

   IF(KFILE.EQ.1 ) THEN
       WRITE(IOUNLOG,*)
       WRITE(IOUNLOG,*) '*** WARNING!'
       WRITE(IOUNLOG,*) 'NO FILE FOR SLA FROM '//TRIM(CSLASATID(JSAT))
       WRITE(IOUNLOG,*) 'THOUGH DISSEMINATION SHOULD BE OK'
   ENDIF

ENDDO SAT

DO JOBS=1,SLA%NO
      SLA%TDIST(JOBS)=SLA%TIM(JOBS) - ZANJUL1950
ENDDO

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' SLA REPORT : TOTAL NO OF OBS IS',SLA%NO

! CLOSE(1027)

END SUBROUTINE READSLA
