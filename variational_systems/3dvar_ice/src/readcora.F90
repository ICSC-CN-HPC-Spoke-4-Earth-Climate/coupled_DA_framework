SUBROUTINE READCORA

!.. INTERFACE FOR READING INS FILES
!.. CALLING READEN3FILE, WHICH IS INTENDED
!.. FOR A MORE GENERIC (READ: POTENTIALLY EXTERNAL)
!.. READING OF OBSERVATIONAL NETCDF FILES
!..
!.. ANDREA STORTO, *INGV* 2009-03-23

USE SET_KND
USE OBS_STR, ONLY : SST,INS,SLA,OBS,SSS
USE GRD_STR
USE IOUNITS
USE ALLOCOBS
USE OBSHANDLING , ONLY : INS_LQC_POS,INS_LQC_TIME,INS_LQC_SAL,&
                       & INS_LQC_TEMP,INS_LQC_PROF_T,&
                       & INS_LQC_PROF_S,INS_CHK_TIME
USE MYFRTPROF, ONLY : MYFRTPROF_WALL
USE RUN, ONLY : ZANJUL1950

IMPLICIT NONE

CHARACTER(LEN=20) :: CFILE
INTEGER(I4), PARAMETER :: NMAX = 1500000,MAXPROFS = 20000
INTEGER(I4) :: NOBS , IFILE, KOSUM, NLEVS
INTEGER(I4) :: STOBS,ENOBS,TOTOBS
INTEGER(I4), DIMENSION(NMAX) :: OTYPE, OPAR, OPROF
REAL   (DP), DIMENSION(NMAX) :: OTIME
REAL   (R8), DIMENSION(NMAX) :: ODEP,  OVAL
REAL   (R8), DIMENSION(NMAX) :: OLON,  OLAT
CHARACTER(LEN=8), DIMENSION(NMAX) :: OPLA
LOGICAL :: LPROCEED, LFLAGS(10), LDUPL, LDUPLA(MAXPROFS)
INTEGER(I4) :: KPRC, KDUPL, JOBS,JPROF,JPROF2, KPIND, KPIND2,UPPROF,K,JT
INTEGER(I4), PARAMETER :: NCORATYPES=8
CHARACTER(LEN=2) :: INSTYPE
CHARACTER(LEN=2) :: LIST_INSTYPE(NCORATYPES)

  WRITE(IOUNOUT,*) ' READING IN IN-SITU OBSERVATIONS'

  LIST_INSTYPE(1:NCORATYPES)(1:2) = (/ 'XB','BA','TE','OC','PF','HF','CT','MO' /)

  INS%NO = NMAX
  INS%NC = NMAX

  CALL ALLOCINS(INS%NO,.TRUE.)
  INS%FLC(:)=0
  INS%FLG(:)=1

  TOTOBS=0
  UPPROF=0

  !... SET FLAGS FOR EXTRACTION OF OBS
  LFLAGS( 1) = INS_LQC_POS
  LFLAGS( 2) = INS_LQC_TIME
  LFLAGS( 3) = INS_LQC_SAL
  LFLAGS( 4) = INS_LQC_TEMP
  LFLAGS( 5) = INS_LQC_PROF_T
  LFLAGS( 6) = INS_LQC_PROF_S
  LFLAGS( 7) = INS_CHK_TIME

  DO JT=1,NCORATYPES

   INSTYPE(1:2) = LIST_INSTYPE(JT)(1:2)

   WRITE(IOUNLOG,*) 'READING CORA INSTYPE ',INSTYPE(1:2)

   LPROCEED=.TRUE.
   IFILE=1

  !... LOOP OVER EXISTING INS FILES
   DO WHILE(LPROCEED)
      WRITE(CFILE,'(A,I2.2)' ) 'PR_'//INSTYPE(1:2)//'_',IFILE
      INQUIRE(FILE=CFILE,EXIST=LPROCEED)
      IF(LPROCEED) THEN
          WRITE(IOUNLOG,*)
          WRITE(IOUNLOG,*) ' /// READING INS FILE ',TRIM(CFILE)
          WRITE(IOUNLOG,*)

          CALL READINSLEVS(CFILE,NLEVS)

          CALL READCORFILE(CFILE,NLEVS,IOUNLOG,NMAX,&
          & LFLAGS(1),LFLAGS(2),LFLAGS(3),LFLAGS(4),LFLAGS(5),LFLAGS(6),&
          & LFLAGS(7),LL_VTHINNING, LL_VAVERAGING,&
          & NOBS,OTYPE,OPAR,ODEP,OVAL,OTIME,OLON,OLAT,OPROF,OPLA,INSTYPE(1:2))

   !... STORE OBS

          IF(NOBS.GT.0) THEN
            IF(TOTOBS+NOBS .GT. NMAX ) &
            & CALL ABOR1('READEN3ITU : NMAX MUST BE INCREASED')

            STOBS = TOTOBS +1
            ENOBS = TOTOBS + NOBS

            INS%OTYPE(STOBS:ENOBS) = OTYPE(1:NOBS)
            INS%  PAR(STOBS:ENOBS) = OPAR (1:NOBS)
            INS%  TIM(STOBS:ENOBS) = OTIME(1:NOBS)
            INS%  DPT(STOBS:ENOBS) = ODEP (1:NOBS)
            INS%  VAL(STOBS:ENOBS) = OVAL (1:NOBS)
            INS%  LON(STOBS:ENOBS) = OLON (1:NOBS)
            INS%  LAT(STOBS:ENOBS) = OLAT (1:NOBS)
            INS% PROF(STOBS:ENOBS) = OPROF(1:NOBS) + UPPROF
            INS% PLNO(STOBS:ENOBS)(1:8) = OPLA (1:NOBS)(1:8)

            TOTOBS = TOTOBS + NOBS
            UPPROF = INS% PROF(ENOBS)

          ENDIF
      ELSE

          IF(IFILE.EQ.1) &
          & WRITE(IOUNERR,*) '*** WARNING : NO INSITU FILE FOR ',INSTYPE(1:2)

      ENDIF
      IFILE=IFILE+1
  ENDDO

 ENDDO

 INS%NO=TOTOBS
 INS%FLC(1:INS%NO) = 1

 INS%DSRC(1:INS%NO) = 'COR'

 CALL PREPPROF

END SUBROUTINE READCORA
