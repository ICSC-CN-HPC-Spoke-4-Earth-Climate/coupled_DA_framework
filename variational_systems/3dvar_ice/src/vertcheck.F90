SUBROUTINE VERTCHECK

!.. REJECT MEASUREMENTS IF SOME OBSERVATIONS ABOVE
!.. WITHIN THE SAME PROFILE WERE REJECTED
!..
!.. ANDREA STORTO *CMCC* 2011-12-20

USE SET_KND
USE OBSDEF
USE OBS_STR
USE IOUNITS, ONLY : IOUNLOG
USE OBSHANDLING , ONLY : LLBGQC_INS
USE MYFRTPROF, ONLY : MYFRTPROF_WALL

IMPLICIT NONE

REAL(R8), PARAMETER :: M_DPT=99999._R8
REAL(R8) :: MINDP
INTEGER(I4) :: KSUM, JPROF, K, K2, JOBS

#include "obs_events.h"

CALL MYFRTPROF_WALL('VERTCHECK: VERTICAL CONSISTENCY CHECK',0)

KSUM=0

!... INS PART

IF( LLBGQC_INS) THEN

  EPROF : DO JPROF=1,INS%NPROFS

    K =  INS%PRIND(JPROF)

    IF(JPROF.LT.INS%NPROFS) THEN
     K2 = INS%PRIND(JPROF+1)-1
    ELSE
     K2 = INS%NO
    ENDIF

    MINDP=M_DPT
    DO JOBS=K,K2
       IF( INS%EVE(JOBS) .EQ. KEVE_BGQC .OR. INS%EVE(JOBS) .EQ. KEVE_BGCH &
       & .OR. INS%EVE(JOBS) .EQ. KEVE_CLIM ) MINDP = MIN( MINDP,INS%DPT(JOBS) )
    ENDDO

    IF( MINDP .LT. M_DPT) THEN
       DO JOBS=K,K2
         IF( INS%DPT(JOBS) .GE. MINDP .AND. INS%FLC(JOBS) .EQ. 1 ) THEN
             INS%FLC(JOBS) = 0
             INS%EVE(JOBS) = KEVE_VCHK
             KSUM=KSUM+1
         ENDIF
       ENDDO
    ENDIF

  ENDDO EPROF
ENDIF

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' *** VERTICAL CHECK - DERIVED REJECTIONS FOR INSITU'
WRITE(IOUNLOG,*) ' INS REJECTED:',KSUM
WRITE(IOUNLOG,*)


CALL MYFRTPROF_WALL('VERTCHECK: VERTICAL CONSISTENCY CHECK',1)
END SUBROUTINE VERTCHECK
