SUBROUTINE COSTF(INDIC,NDIM,X,XJ,XGJ,NITER,IZS,RZS,DZS,ISTEP)

!-----------------------------------------------------------------------
!                                                                      !
! CALCULATE THE COST FUNCTION AND ITS GRADIENT                         !
!                                                                      !
!-----------------------------------------------------------------------


 USE OBS_STR
 USE GRD_STR
 USE EOF_STR
 USE CTL_STR
 USE IOUNITS, ONLY : IOUNERR, IOUNOUT, IOUNLOG, IOUNCOST
 USE RUN
 USE MYFRTPROF
 USE STATS, ONLY : MEAN
 USE TLAD
 USE WEAKLY
 USE HYBRID
 USE EOFS3D
 USE TRACK_CORREL
 USE PROF_CORREL
 USE CERES

 IMPLICIT NONE

 INTEGER(I4) :: I,J,K, KK,NITER,NN, JLEV, ITB,JO
 REAL   (R8) :: JO_SLA, JO_INS, JO_SST, JO_SSS,JO_SIC,JO_SIT,JO_OTH, ZA1
 REAL   (R8) :: JO_TOA, ZCHC
 REAL   (R8) :: ZT(GRD%KM),ZS(GRD%KM)
 REAL   (R8) :: ZTN(GRD%KM),ZSN(GRD%KM)
 REAL   (R8) :: ZTX(GRD%KM),ZSX(GRD%KM)
 CHARACTER(LEN=10)  :: CDATEX,CTIMEX,CZONE
 CHARACTER(LEN=200) :: CTISTRING
 INTEGER(KIND=I4) :: ITVALUES(8)
 LOGICAL :: LLINCRBYITER = .TRUE.
 LOGICAL :: LL_DBG

 INTEGER(I4), INTENT(IN)    :: INDIC
 INTEGER(I4), INTENT(IN)    :: NDIM     ! NUMBER OF CONTROL VARIABLES
 INTEGER(I4), INTENT(IN), OPTIONAL :: ISTEP
 INTEGER(I4)  :: KSTEP

 REAL(R8), DIMENSION(NDIM), INTENT(IN)   :: X    ! VECTOR OF CONTROL VARIABLES
 REAL(R8), DIMENSION(NDIM), INTENT(OUT)  :: XGJ  ! GRADIENT OF COST FUNCTION
 REAL(R8), INTENT(OUT)                   :: XJ   ! COST FUNCTION
 REAL(R8) :: XS, ZCST

 INTEGER(I4)  :: IZS
 REAL(R8)     :: RZS
 REAL(R8)     :: DZS

 REAL(R8)     :: ZJ, ZK

 CALL MYFRTPROF_WALL('COSTF: CALCULATE COST FUNCTION',0)

 KSTEP=0
 IF(PRESENT(ISTEP)) THEN
   IF(ISTEP.NE.0  .AND. ISTEP.NE.1 .AND. ISTEP.NE.2) &
   CALL ABOR1('COSTF CALLED WITH INVALID ISTEP')
   KSTEP=ISTEP
 ENDIF

 LL_DBG = ( LL_WEAKLY .OR. LL_ALPHA_CTL )
 LLINCRBYITER = LL_DBG

! -------------------------------------------------------
! CALCULATE BACKGORUND COST TERM
! -------------------------------------------------------

   CTL%F_B = 0.5_R8 * DOT_PRODUCT( CTL%X_C(1:CVB), CTL%X_C(1:CVB))

   IF(LLVARMDT) THEN
       CTL%F_M = 0.5_R8 * &
     & DOT_PRODUCT(CTL%X_C(CVM_S:CVM_E), CTL%X_C(CVM_S:CVM_E))
   ELSE
       CTL%F_M = 0._R8
   ENDIF

   IF(LL_WEAKLY) THEN
       CTL%F_E = 0.5_R8 * &
     & DOT_PRODUCT(CTL%X_C(CVWC4_S:CVWC4_E), CTL%X_C(CVWC4_S:CVWC4_E))
       WRITE(IOUNLOG,*) 'CHECK WEAKLY :',CTL%F_E, CTL%F_B, CVWC4_S, CVWC4_E
       IF( LL_WCEOF ) THEN
         IF(NN_WCEOF3D_2.GT.0) THEN
           WROE  = CTL%X_C(CVWC4_S:(CVWC4_S+NN_WCEOF3D-1))
           WROE_2= CTL%X_C((CVWC4_S+NN_WCEOF3D):CVWC4_E)
         ELSE
           WROE= CTL%X_C(CVWC4_S:CVWC4_E)
         ENDIF

         WRITE(IOUNLOG,*) 'CHECK WEAKLY :',MINVAL(WROE),MAXVAL(WROE)
       ELSE
         ROW = CTL%X_C(CVWC4_S:CVWC4_E)
         WRITE(IOUNLOG,*) 'CHECK WEAKLY :',MINVAL(ROW),MAXVAL(ROW)
       ENDIF
   ELSE
       CTL%F_E = 0._R8
   ENDIF

   IF(LL_HYBRID) THEN
       CTL%F_H = 0.5_R8 * &
     & DOT_PRODUCT(CTL%X_C(CVH_S:CVH_E), CTL%X_C(CVH_S:CVH_E))
   ELSE
       CTL%F_H = 0._R8
   ENDIF

   IF(LL_HYBRID .AND. LL_ALPHA_CTL) THEN
       ALPHA(:) = CTL%X_C(CVA_S:CVA_E)
       IF(LL_PRECON_ALPHA) THEN
        CTL%F_A = 0.5_R8*ALPHA(1)*ALPHA(1)
       ELSE
        CTL%F_A = 0.5_R8*ALPHA(1)*ALPHA(1)/ALPHAV(1)
       ENDIF
   ELSE
       CTL%F_A = 0._R8
   ENDIF

   IF (LL_EOFS3D) THEN
       CTL%F_3 = 0.5_R8 * &
       & DOT_PRODUCT(CTL%X_C(CVE_S:CVE_E), CTL%X_C(CVE_S:CVE_E))
   ELSE
       CTL%F_3 = 0._R8
   ENDIF

   CTL%KITER = CTL%KITER +1
   WRITE(IOUNLOG,*) ' COSTF STARTING :: ITERATION ', CTL%KITER
   IF( LL_DBG ) WRITE(IOUNLOG,*) 'MMC0 ',MINVAL( CTL%X_C ), MAXVAL( CTL%X_C )

! -------------------------------------------------------
! CALCULATE OBSERVATIONAL COST TERM
! -------------------------------------------------------
! --------
! CONVERT THE CONTROL VECTOR TO V
   CALL CNV_CTV

  IF( LL_DBG ) WRITE(IOUNLOG,*) 'MMC1 ',MINVAL( GRD%RO ), MAXVAL( GRD%RO )
! --------
! CONTROL TO PHYSICAL SPACE (HORIZONTAL FILTER AND VERTICAL EOFS)
  IF ( NCONF .EQ. 202 ) THEN
     CALL VER_HOR202
  ELSE
     IF(LL_HYBRID_CR) CALL VER_HORF
     CALL VER_HOR
  ENDIF

  IF( LL_DBG ) WRITE(IOUNLOG,*) 'MMC2 ',MINVAL( GRD%TEM ), MAXVAL( GRD%TEM )
  IF( LL_DBG ) WRITE(IOUNLOG,*) 'MMC2 ',MINVAL( GRD%SAL ), MAXVAL( GRD%SAL )

! --------
! WEAK-CONSTRAINT 4DVAR
  IF( LL_WEAKLY ) CALL CTRL2ERR_TL(0)

  IF( LL_DBG ) WRITE(IOUNLOG,*) 'MMC2b',MINVAL( TERR    ), MAXVAL( TERR    )
  IF( LL_DBG ) WRITE(IOUNLOG,*) 'MMC2b',MINVAL( SERR    ), MAXVAL( SERR    )

! --------
! SIMPLIFIED TL MODEL
  IF ( LL_4DVAR ) CALL TLMOD(0)

  IF( LL_DBG ) WRITE(IOUNLOG,*) 'MMC3 ',MINVAL( GRD%TEM ), MAXVAL( GRD%TEM )
  IF( LL_DBG ) WRITE(IOUNLOG,*) 'MMC3 ',MINVAL( GRD%SAL ), MAXVAL( GRD%SAL )
! --------
! APPLY OBSERVATIONAL OPERATORS
  IF(.NOT. LL_4DVAR ) CALL OBSOP
! --------
! CALCULATE RESIDUALS
   CALL RESID
! --------
! CALCULATE COST

  CTL%F_O = 0._R8
  JO_SLA = 0._R8
  JO_INS = 0._R8
  JO_SST = 0._R8
  JO_SSS = 0._R8
  JO_SIC = 0._R8
  JO_SIT = 0._R8

  IF ( LL_HUBERQC .AND. CTL%KITER.GE. ITER_HUBQC .AND. .NOT. LL_INIT_HUBER ) THEN
       CALL INIT_HUBER
       LL_INIT_HUBER = .TRUE.
  ENDIF

  ! SLA PART
  IF( SLA%NO .GT. 0 ) THEN
   IF( NN_TRACK_CORREL.GT.0 ) THEN
    CALL JOSLA_CORREL(JO_SLA)
   ELSEIF( LL_HUBERQC .AND. LL_HUBERQC_SLA .AND. CTL%KITER .GT. ITER_HUBQC ) THEN
    DO JO=1,SLA%NO
      IF( OBS%AMO(JO).GT.ABS(OBS%AHUB(JO,1)) .AND. OBS%AMO(JO).LE.ABS(OBS%AHUB2(JO,1)) ) THEN
           ZCST = OBS%AMO(JO)*OBS%AHUB(JO,1)-0.5_R8*OBS%AHUB(JO,1)*OBS%AHUB(JO,1)
      ELSEIF( OBS%AMO(JO).LT.-ABS(OBS%AHUB(JO,2)) .AND.  OBS%AMO(JO).GE.-ABS(OBS%AHUB2(JO,2)) ) THEN
           ZCST = -OBS%AMO(JO)*OBS%AHUB(JO,2)-0.5_R8*OBS%AHUB(JO,2)*OBS%AHUB(JO,2)
      ELSEIF( OBS%AMO(JO).GT.ABS(OBS%AHUB2(JO,1)) ) THEN
           ZJ = 2._R8*OBS%AHUB(JO,1)*SQRT(OBS%AHUB2(JO,1))
           ZK = 0.5_R8*OBS%AHUB(JO,1)*OBS%AHUB(JO,1)-OBS%AHUB(JO,1)*OBS%AHUB2(JO,1)+ZJ*SQRT(OBS%AHUB2(JO,1))
           ZCST = ZJ*SQRT(OBS%AMO(JO))-ZK
      ELSEIF( OBS%AMO(JO).LT.-ABS(OBS%AHUB2(JO,2)) ) THEN
           ZJ = -2._R8*OBS%AHUB(JO,2)*SQRT(OBS%AHUB2(JO,2))
           ZK = 0.5_R8*OBS%AHUB(JO,2)*OBS%AHUB(JO,2)-OBS%AHUB(JO,2)*OBS%AHUB2(JO,2)-ZJ*SQRT(OBS%AHUB2(JO,2))
           ZCST = -ZJ*SQRT(ABS(OBS%AMO(JO)))-ZK
      ELSE
           ZCST = 0.5_R8 * OBS%AMO(JO) * OBS%AMO(JO)
      ENDIF
      JO_SLA = JO_SLA + ZCST
    ENDDO
   ELSE
      JO_SLA = 0.5_R8 * DOT_PRODUCT( OBS%AMO(1:SLA%NO), OBS%AMO(1:SLA%NO) )
   ENDIF
  ENDIF

  ! INS PART
  IF( INS%NO .GT. 0 ) THEN
   IF( NN_PROF_CORREL.GT.0 .AND. .NOT. LL_PROFCORR_ONLYP ) THEN
    CALL JOINS_CORREL(JO_INS)
   ELSEIF( LL_HUBERQC .AND. LL_HUBERQC_INS .AND. CTL%KITER .GT. ITER_HUBQC ) THEN
    DO JO=SLA%NO+1,SLA%NO+INS%NO
      IF( OBS%AMO(JO).GT.ABS(OBS%AHUB(JO,1)) .AND. OBS%AMO(JO).LE.ABS(OBS%AHUB2(JO,1)) ) THEN
           ZCST = OBS%AMO(JO)*OBS%AHUB(JO,1)-0.5_R8*OBS%AHUB(JO,1)*OBS%AHUB(JO,1)
      ELSEIF( OBS%AMO(JO).LT.-ABS(OBS%AHUB(JO,2)) .AND.  OBS%AMO(JO).GE.-ABS(OBS%AHUB2(JO,2)) ) THEN
           ZCST = -OBS%AMO(JO)*OBS%AHUB(JO,2)-0.5_R8*OBS%AHUB(JO,2)*OBS%AHUB(JO,2)
      ELSEIF( OBS%AMO(JO).GT.ABS(OBS%AHUB2(JO,1)) ) THEN
           ZJ = 2._R8*OBS%AHUB(JO,1)*SQRT(OBS%AHUB2(JO,1))
           ZK = 0.5_R8*OBS%AHUB(JO,1)*OBS%AHUB(JO,1)-OBS%AHUB(JO,1)*OBS%AHUB2(JO,1)+ZJ*SQRT(OBS%AHUB2(JO,1))
           ZCST = ZJ*SQRT(OBS%AMO(JO))-ZK
      ELSEIF( OBS%AMO(JO).LT.-ABS(OBS%AHUB2(JO,2)) ) THEN
           ZJ = -2._R8*OBS%AHUB(JO,2)*SQRT(OBS%AHUB2(JO,2))
           ZK = 0.5_R8*OBS%AHUB(JO,2)*OBS%AHUB(JO,2)-OBS%AHUB(JO,2)*OBS%AHUB2(JO,2)-ZJ*SQRT(OBS%AHUB2(JO,2))
           ZCST = -ZJ*SQRT(ABS(OBS%AMO(JO)))-ZK
      ELSE
           ZCST = 0.5_R8 * OBS%AMO(JO) * OBS%AMO(JO)
      ENDIF
      JO_INS = JO_INS + ZCST
    ENDDO
   ELSE
      JO_INS = 0.5_R8 * DOT_PRODUCT( OBS%AMO((SLA%NO+1):(SLA%NO+INS%NO)), &
      OBS%AMO((SLA%NO+1):(SLA%NO+INS%NO)) )
   ENDIF
  ENDIF

  ! SST PART
  IF( SST%NO .GT. 0 ) THEN
   IF( LL_HUBERQC .AND. LL_HUBERQC_SST .AND. CTL%KITER .GT. ITER_HUBQC ) THEN
    DO JO=INS%NO+SLA%NO+1,INS%NO+SLA%NO+SST%NO
      IF( OBS%AMO(JO).GT.ABS(OBS%AHUB(JO,1)) .AND. OBS%AMO(JO).LE.ABS(OBS%AHUB2(JO,1)) ) THEN
           ZCST = OBS%AMO(JO)*OBS%AHUB(JO,1)-0.5_R8*OBS%AHUB(JO,1)*OBS%AHUB(JO,1)
      ELSEIF( OBS%AMO(JO).LT.-ABS(OBS%AHUB(JO,2)) .AND.  OBS%AMO(JO).GE.-ABS(OBS%AHUB2(JO,2)) ) THEN
           ZCST = -OBS%AMO(JO)*OBS%AHUB(JO,2)-0.5_R8*OBS%AHUB(JO,2)*OBS%AHUB(JO,2)
      ELSEIF( OBS%AMO(JO).GT.ABS(OBS%AHUB2(JO,1)) ) THEN
           ZJ = 2._R8*OBS%AHUB(JO,1)*SQRT(OBS%AHUB2(JO,1))
           ZK = 0.5_R8*OBS%AHUB(JO,1)*OBS%AHUB(JO,1)-OBS%AHUB(JO,1)*OBS%AHUB2(JO,1)+ZJ*SQRT(OBS%AHUB2(JO,1))
           ZCST = ZJ*SQRT(OBS%AMO(JO))-ZK
      ELSEIF( OBS%AMO(JO).LT.-ABS(OBS%AHUB2(JO,2)) ) THEN
           ZJ = -2._R8*OBS%AHUB(JO,2)*SQRT(OBS%AHUB2(JO,2))
           ZK = 0.5_R8*OBS%AHUB(JO,2)*OBS%AHUB(JO,2)-OBS%AHUB(JO,2)*OBS%AHUB2(JO,2)-ZJ*SQRT(OBS%AHUB2(JO,2))
           ZCST = -ZJ*SQRT(ABS(OBS%AMO(JO)))-ZK
      ELSE
           ZCST = 0.5_R8 * OBS%AMO(JO) * OBS%AMO(JO)
      ENDIF
      JO_SST = JO_SST + ZCST
    ENDDO
   ELSE
      JO_SST = 0.5_R8 * DOT_PRODUCT( OBS%AMO((INS%NO+SLA%NO+1):(INS%NO+SLA%NO+SST%NO)), &
      OBS%AMO((INS%NO+SLA%NO+1):(INS%NO+SLA%NO+SST%NO)))
   ENDIF
  ENDIF

  ! SSS PART
  IF( SSS%NO .GT. 0 ) THEN
   IF( LL_HUBERQC .AND. LL_HUBERQC_SSS .AND. CTL%KITER .GT. ITER_HUBQC ) THEN
    DO JO=SST%NO+INS%NO+SLA%NO+1,SST%NO+INS%NO+SLA%NO+SSS%NO
      IF( OBS%AMO(JO).GT.ABS(OBS%AHUB(JO,1)) .AND. OBS%AMO(JO).LE.ABS(OBS%AHUB2(JO,1)) ) THEN
           ZCST = OBS%AMO(JO)*OBS%AHUB(JO,1)-0.5_R8*OBS%AHUB(JO,1)*OBS%AHUB(JO,1)
      ELSEIF( OBS%AMO(JO).LT.-ABS(OBS%AHUB(JO,2)) .AND.  OBS%AMO(JO).GE.-ABS(OBS%AHUB2(JO,2)) ) THEN
           ZCST = -OBS%AMO(JO)*OBS%AHUB(JO,2)-0.5_R8*OBS%AHUB(JO,2)*OBS%AHUB(JO,2)
      ELSEIF( OBS%AMO(JO).GT.ABS(OBS%AHUB2(JO,1)) ) THEN
           ZJ = 2._R8*OBS%AHUB(JO,1)*SQRT(OBS%AHUB2(JO,1))
           ZK = 0.5_R8*OBS%AHUB(JO,1)*OBS%AHUB(JO,1)-OBS%AHUB(JO,1)*OBS%AHUB2(JO,1)+ZJ*SQRT(OBS%AHUB2(JO,1))
           ZCST = ZJ*SQRT(OBS%AMO(JO))-ZK
      ELSEIF( OBS%AMO(JO).LT.-ABS(OBS%AHUB2(JO,2)) ) THEN
           ZJ = -2._R8*OBS%AHUB(JO,2)*SQRT(OBS%AHUB2(JO,2))
           ZK = 0.5_R8*OBS%AHUB(JO,2)*OBS%AHUB(JO,2)-OBS%AHUB(JO,2)*OBS%AHUB2(JO,2)-ZJ*SQRT(OBS%AHUB2(JO,2))
           ZCST = -ZJ*SQRT(ABS(OBS%AMO(JO)))-ZK
      ELSE
           ZCST = 0.5_R8 * OBS%AMO(JO) * OBS%AMO(JO)
      ENDIF
      JO_SSS = JO_SSS + ZCST
    ENDDO
   ELSE
      JO_SSS = 0.5_R8 * DOT_PRODUCT( OBS%AMO((SST%NO+INS%NO+SLA%NO+1):(SST%NO+INS%NO+SLA%NO+SSS%NO)), &
      OBS%AMO((SST%NO+INS%NO+SLA%NO+1):(SST%NO+INS%NO+SLA%NO+SSS%NO)) )
   ENDIF
  ENDIF

  IF( SIC%NO .GT. 0 ) THEN
      JO_SIC = 0.5_R8 * DOT_PRODUCT( OBS%AMO((SSS%NO+SST%NO+INS%NO+SLA%NO+1):(SSS%NO+SST%NO+INS%NO+SLA%NO+SIC%NO)), &
      OBS%AMO((SSS%NO+SST%NO+INS%NO+SLA%NO+1):(SSS%NO+SST%NO+INS%NO+SLA%NO+SIC%NO)) )
  ENDIF

  IF( SIT%NO .GT. 0 ) THEN
      JO_SIT = 0.5_R8 * DOT_PRODUCT( OBS%AMO((SIC%NO+SSS%NO+SST%NO+INS%NO+SLA%NO+1):(SIC%NO+SSS%NO+SST%NO+INS%NO+SLA%NO+SIT%NO)), &
      OBS%AMO((SIC%NO+SSS%NO+SST%NO+INS%NO+SLA%NO+1):(SIC%NO+SSS%NO+SST%NO+INS%NO+SLA%NO+SIT%NO)))
  ENDIF

  CTL%F_O = JO_SLA + JO_INS + JO_SST + JO_SSS + JO_SIC +JO_SIT

! INTEGRATED QUANTITIES

  IF(LL_CERES) THEN
     CALL CERES_OO_TL(GRD%IM,GRD%JM,GRD%KM,GRD%TEM,ZCHC)
     JO_TOA = 0.5_R8 * (ZCHC+ZCBAC-ZCVAL)**2/ZCERR**2
     WRITE(IOUNLOG,*) ' CERES-TOA: ',CTL%KITER,ZCHC,JO_TOA
  ELSE
     JO_TOA = 0._R8
  ENDIF
  CTL%F_T = JO_TOA

! -------------------------------------------------------
! COST FUNCTION
! -------------------------------------------------------

    CTL%F_C = CTL%F_B + CTL%F_O + CTL%F_M + CTL%F_E + &
            & CTL%F_H + CTL%F_A + CTL%F_3 + CTL%F_T
    XJ = CTL%F_C

    IF( KSTEP .NE. 1 ) THEN

! -------------------------------------------------------
! CALCULATE THE COST FUNCTION GRADIENT
! -------------------------------------------------------

! --------
! RESET THE INCREMENTS
   CALL RES_INC

! INTEGRATED QUANTITIES

  IF(LL_CERES) THEN
     JO_TOA = (ZCHC+ZCBAC-ZCVAL)/ZCERR**2
     CALL CERES_OO_AD(GRD%IM,GRD%JM,GRD%KM,GRD%TEM_AD,JO_TOA)
  ENDIF

! --------
! OBSERVATIONAL OPERATORS
  IF(.NOT. LL_4DVAR ) CALL OBSOP_AD

! --------
! SIMPLIFIED AD MODEL
  IF ( LL_4DVAR ) CALL ADMOD(0)

! --------
! WEAK-CONSTRAINT 4DVAR
  IF( LL_WEAKLY ) CALL CTRL2ERR_AD

! --------
! CONTROL TO PHYSICAL SPACE (HORIZONTAL FILTER AND VERTICAL EOFS)
  IF ( NCONF .EQ. 202 ) THEN
     CALL VER_HOR_AD202
  ELSE
     CALL VER_HOR_AD
     IF(LL_HYBRID_CR) CALL VER_HORF_AD
  ENDIF

! --------
! CONVERT THE CONTROL VECTOR
   CALL CNV_CTV_AD

! -------------------------------------------------------
! COST FUNCTION GRADIENT
! -------------------------------------------------------

   ENDIF

   CALL DATE_AND_TIME(CDATEX,CTIMEX,CZONE,ITVALUES)
   WRITE(CTISTRING,'(A)') CDATEX(1:4)//'.'//CDATEX(5:6)//'.'//&
               & CDATEX(7:8)//' ' //CTIMEX(1:2)//':'//&
               & CTIMEX(3:4)//':'//CTIMEX(5:6)

  IF( LL_WEAKLY .AND. LL_DEBUG_WEAKLY ) CALL PRINT_MODERR(CTL%KITER)

   IF(LL_ALPHA_CTL .AND. .NOT.LL_PRECON_ALPHA) THEN
     DO K=1,CTL%N
       IF(K.EQ.CVA_S) THEN
          CTL%G_C(K)=CTL%G_C(K) + CTL%X_C(K)/ALPHAV(1)
       ELSE
          CTL%G_C(K)=CTL%G_C(K) + CTL%X_C(K)
       ENDIF
     ENDDO
   ELSE
     CTL%G_C(:) = CTL%X_C(:) + CTL%G_C(:)
   ENDIF
   XGJ(:) = CTL%G_C(:)
   XS = DOT_PRODUCT(XGJ,XGJ)

   WRITE(IOUNLOG,'(A,X,I3,I3,10(X,E10.3))') &
     & '  GREPCOST :: ITER, SIMUL, JO, JB, JR, J, JM, NORM(GRAD(J))', &
     & NITER, CTL%KITER, CTL%F_O, CTL%F_B, CTL%F_C, CTL%F_M, CTL%F_E,CTL%F_H,CTL%F_A,CTL%F_3,XS
   WRITE(IOUNCOST,'(A,X,I3,13(X,E13.6))') TRIM(CTISTRING),CTL%KITER,CTL%F_O, CTL%F_B,&
   & CTL%F_C, JO_SLA,JO_INS,JO_SST,JO_SSS,JO_SIC,JO_SIT,CTL%F_M,CTL%F_E,CTL%F_H,CTL%F_A,CTL%F_3
   CALL FLUSH(IOUNLOG)
   CALL FLUSH(IOUNCOST)

   IF(.NOT.ABS(CTL%F_C) .LT. 1.E+24) CALL ABOR1('NANS ARISE IN COST FUNCTION')

   IF(LLINCRBYITER .AND. NCONF.NE.202) THEN
     DO JLEV=1,GRD%KM
        ZT(JLEV) = MEAN(GRD%TEM(:,:,JLEV),MASK=(INT(GRD%MSK(:,:,JLEV)).EQ.1))
        ZS(JLEV) = MEAN(GRD%SAL(:,:,JLEV),MASK=(INT(GRD%MSK(:,:,JLEV)).EQ.1))
        ZTN(JLEV) = MINVAL(GRD%TEM(:,:,JLEV),MASK=(INT(GRD%MSK(:,:,JLEV)).EQ.1))
        ZSN(JLEV) = MINVAL(GRD%SAL(:,:,JLEV),MASK=(INT(GRD%MSK(:,:,JLEV)).EQ.1))
        ZTX(JLEV) = MAXVAL(GRD%TEM(:,:,JLEV),MASK=(INT(GRD%MSK(:,:,JLEV)).EQ.1))
        ZSX(JLEV) = MAXVAL(GRD%SAL(:,:,JLEV),MASK=(INT(GRD%MSK(:,:,JLEV)).EQ.1))
     ENDDO

     WRITE(IOUNLOG,*) '======== ITER, SIMUL :: ',NITER, CTL%KITER
     WRITE(IOUNLOG,*) ' LEVEL     TEMP_INCR      SAL_INCR'
     DO JLEV=1,GRD%KM
         WRITE(IOUNLOG,'(I7,6F14.6)') JLEV, ZT(JLEV),&
         & ZS(JLEV),ZTN(JLEV),ZSN(JLEV),ZTX(JLEV),ZSX(JLEV)
     ENDDO
     WRITE(IOUNLOG,*)
     CALL FLUSH(IOUNLOG)
   ENDIF

   WRITE(IOUNOUT,*) 'ITERATION', CTL%KITER,' J =',CTL%F_C

 CALL MYFRTPROF_WALL('COSTF: CALCULATE COST FUNCTION',1)
END SUBROUTINE COSTF
