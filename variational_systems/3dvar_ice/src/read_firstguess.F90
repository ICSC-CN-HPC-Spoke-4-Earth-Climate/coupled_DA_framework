SUBROUTINE READ_FIRSTGUESS

USE SET_KND
USE IOUNITS, ONLY : IOUNERR,IOUNOUT,IOUNLOG
USE OBS_STR
USE GRD_STR
USE RUN
USE MYFRTPROF, ONLY : MYFRTPROF_WALL
USE CALENDAR, ONLY : TIME_DIFF, TIME_ADD
USE READFG
USE MPIREL
USE OBS_STR

IMPLICIT NONE

CHARACTER (LEN=10) :: CDATENEW
INTEGER(I4)    ::  K, I, IERR, JTSTEP,IRET,TSTEPS
INTEGER(I4)    ::  IFGYY,IFGMM,IFGDD,IFGHH
INTEGER(I4)    ::  ITTYY,ITTMM,ITTDD
REAL(KIND=DP)  ::  ZTTSS, ZMS_NUM,ZMS_DEN
INTEGER(I4), PARAMETER  ::  MS = 999
REAL(KIND=R8)  ::  SEC(MS), MINTD

CALL MYFRTPROF_WALL('READ_FIRSTGUESS: READ-IN FG FIELDS',0)

WRITE(IOUNOUT,*) ' SETTING UP FG FILENAMES...'
CALL SETFGFILES(NSUBDOMAINS)

IF( .NOT. LL_RESTART_FILE ) THEN
  WRITE(IOUNOUT,*) ' SETTING UP FG TIMESTEPS...'
  CALL FINDFGTIMES(TSTEPS, IFGYY, IFGMM, IFGDD, IFGHH, MS, SEC)
ELSE
  TSTEPS=1
ENDIF

NTSTEPS=TSTEPS
WRITE(IOUNLOG,*) ' *** FOUND ',TSTEPS,' IN FIRST GUESS FILE'

ALLOCATE(LL_FGASSIM(TSTEPS))
LL_FGASSIM = .FALSE.

IF(NTSTEPS .GT. MAXT) CALL ABOR1('MAXT IN RUN MUST BE INCREASED')

! COMPUTE TIMESTEPS IN FG ACCORDING TO SLA OBS FOR FGAT O-G

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) 'TIMESTEP IN THE FGFILE (AS DAYS SINCE 1950010100)'
WRITE(IOUNLOG,*)

IF( .NOT. LL_RESTART_FILE ) THEN

 MINTD = 1.E+12
 ANATSTEP = 0

 DO JTSTEP=1,TSTEPS

    CALL TIME_ADD(IFGYY,IFGMM,IFGDD,REAL(IFGHH*3600,KIND=DP),&
                & SEC(JTSTEP),ITTYY,ITTMM,ITTDD,ZTTSS)

    CALL TIME_DIFF(1950,1,1,0._DP,ITTYY,ITTMM,ITTDD,ZTTSS,RTIMES1950(JTSTEP))

    RTIMES1950(JTSTEP) = RTIMES1950(JTSTEP) / 86400._DP

    IF(ABS(RTIMES1950(JTSTEP)-ZANJUL1950) .LT. MINTD) THEN
       ANATSTEP=JTSTEP
       MINTD = ABS(RTIMES1950(JTSTEP)-ZANJUL1950)
    ENDIF

    IF( ( RTIMES1950(JTSTEP) .GE. ZJULSTART-ZJUL1950 .AND. &
       RTIMES1950(JTSTEP) .LE. ZJULEND-ZJUL1950 ) .OR. &
       ABS( RTIMES1950(JTSTEP) - (ZJULSTART-ZJUL1950) ) .LT. (SEC(2)-SEC(1))/86400._R8&
       .OR.ABS( RTIMES1950(JTSTEP) - (ZJULEND-ZJUL1950) ).LT. (SEC(2)-SEC(1))/86400._R8 )&
       LL_FGASSIM(JTSTEP) = .TRUE.
  
    WRITE(IOUNLOG,*) JTSTEP,RTIMES1950(JTSTEP),&
    & ITTYY,ITTMM,ITTDD,ZTTSS,LL_FGASSIM(JTSTEP)

 ENDDO

 IF(ANATSTEP.EQ.0 .OR. MINTD .GT. IANTW/24._R8 ) THEN
   WRITE(IOUNERR,*) 'TSTEP AT ANALYSIS TIME (',ZANJUL1950,') NOT FOUND'
   WRITE(IOUNERR,*) 'ANATSTEP, MINTD = ', ANATSTEP, MINTD
   CALL ABOR1('ANATSTEP NOT FOUND IN FIRST GUESS FILE')
 ELSE
   WRITE(IOUNOUT,*)' TSTEP CORRESPONDING TO ANALYSIS TIME (',ZANJUL1950,'):',&
            & ANATSTEP
   WRITE(IOUNLOG,*)' TSTEP CORRESPONDING TO ANALYSIS TIME (',ZANJUL1950,'):',&
            & ANATSTEP
 ENDIF

ELSE
 ANATSTEP = 1
 LL_FGASSIM(1) = .TRUE.
ENDIF

IF( LL_NOFGAT ) THEN
  NFGTSTEPS=1
  LL_FGASSIM = .FALSE.
  LL_FGASSIM(ANATSTEP) = .TRUE.
ELSE
  NFGTSTEPS=COUNT(LL_FGASSIM)
ENDIF

WRITE(IOUNLOG,*)' NUMBER OF BG FIELDS NEEDED FOR 3DVAR :',NFGTSTEPS
CALL FLUSH(IOUNLOG)

CALL SETFGFLAYOUT

ALLOCATE(GRD%SALB(GRD%IM,GRD%JM,GRD%KM,NFGTSTEPS),&
       & GRD%TEMB(GRD%IM,GRD%JM,GRD%KM,NFGTSTEPS),STAT=IERR)
IF(IERR.NE. 0) CALL ABOR1('CANNOT ALLOCATE BG FIELDS')

ALLOCATE(GRD%ETAB(GRD%IM,GRD%JM,NFGTSTEPS),STAT=IERR)

IF(IERR.NE. 0) CALL ABOR1('CANNOT ALLOCATE BG FIELDS')

!IF(LLONLYOBSPP .AND. OBS%SLA+OBS%ARG+OBS%XBT+OBS%GLD+OBS%SST+OBS%SSS .EQ. 0 ) THEN
IF( OBS%SLA+OBS%ARG+OBS%XBT+OBS%GLD+OBS%SST+OBS%SSS .EQ. 0 ) THEN  
 WRITE(IOUNOUT,*) ' NO NEED TO READ 3D FIELDS'
ELSE
  CALL READFGFIELDS(GRD%SALB,GRD%TEMB,GRD%ETAB)
ENDIF

IF(OBS%AIR.EQ.1) THEN
       ALLOCATE(GRD%T2MB(GRD%IM,GRD%JM,NFGTSTEPS),&
       & GRD%Q2MB(GRD%IM,GRD%JM,NFGTSTEPS),STAT=IERR)
       IF(IERR.NE. 0) CALL ABOR1('CANNOT ALLOCATE BG FIELDS')
       CALL READFGFIELDS_A(GRD%T2MB,GRD%Q2MB)
ENDIF

 IF(LL_ICE) THEN
       WRITE(IOUNLOG,*) ' READING AND TRANSFORMING SIC (SIC SHOULD BE ALREADY INITIALIZED) '
       ALLOCATE(GRD%SICB(GRD%IM,GRD%JM,NFGTSTEPS),GRD%TRA_SICB(GRD%IM,GRD%JM,NFGTSTEPS),STAT=IERR)
       IF(IERR.NE. 0) CALL ABOR1('CANNOT ALLOCATE BG FIELDS')
       CALL READFGFIELDS_ICE(GRD%SICB,GRD%TRA_SICB,1)

!IF(OBS%SIT .EQ. 1) THEN
       WRITE(IOUNLOG,*) ' READING AND TRANSFORMING SIT (SIT SHOULD BE ALREADY INITIALIZED)'
       ALLOCATE(GRD%SITB(GRD%IM,GRD%JM,NFGTSTEPS),GRD%TRA_SITB(GRD%IM,GRD%JM,NFGTSTEPS),STAT=IERR)      
       IF(IERR.NE. 0) CALL ABOR1('CANNOT ALLOCATE BG FIELDS')
       CALL READFGFIELDS_ICE(GRD%SITB,GRD%TRA_SITB,2)
ENDIF

IF( LL_ADJETAB ) THEN
  WRITE(IOUNLOG,*)
  WRITE(IOUNLOG,*) ' *** ADJUSTING FIRST GUESS SSH'
  DO JTSTEP=1,NFGTSTEPS
     !ZMS = SUM( GRD%ETAB(:,:,JTSTEP)*GRD%DX*GRD%DY*GRD%MSK(:,:,1) ) / &
     !      SUM( GRD%DX*GRD%DY*GRD%MSK(:,:,1) )
    ZMS_NUM = SUM( GRD%ETAB(:,:,JTSTEP)*GRD%DX*GRD%DY*GRD%MSK(:,:,1) )
    ZMS_DEN = SUM( GRD%DX*GRD%DY*GRD%MSK(:,:,1))

    IF( LMPION ) THEN
        CALL MPPSUM( ZMS_NUM )
        CALL MPPSUM( ZMS_DEN )
    ENDIF
    IF(ZMS_DEN<=0._R8) THEN
     CALL ABOR1('DENOMINATOR OF GLOBAL SSH MEAN IS ZERO (OR LESS)!!')
    ENDIF
    WRITE(IOUNLOG,*) ' TIMESTEP ',JTSTEP,' GLOBAL SSH = ',ZMS_NUM/ZMS_DEN
    GRD%ETAB(:,:,JTSTEP) = GRD%ETAB(:,:,JTSTEP) - ZMS_NUM/ZMS_DEN*GRD%MSK(:,:,1)

  ENDDO
  WRITE(IOUNLOG,*)
ENDIF

CALL MYFRTPROF_WALL('READ_FIRSTGUESS: READ-IN FG FIELDS',1)

END SUBROUTINE READ_FIRSTGUESS
