SUBROUTINE WRT_OUT

!-----------------------------------------------------------------------
!                                                                      !
! WRITE OUTPUTS
!                                                                      !
! VERSION 1: S.DOBRICIC 2006                                           !
! VERSION 2: S.DOBRICIC 2006                                           !
!-----------------------------------------------------------------------


 USE SET_KND
 USE DRV_STR
 USE OBS_STR
 USE GRD_STR
 USE EOF_STR
 USE CTL_STR
 USE RUN
 USE IOUNITS
 USE WRITECORR, ONLY : WRITECORRLOC, WRITECORRLOCBIN
 USE STATS, ONLY : MEAN
 USE LBCLNK
 USE MYFRTPROF

 IMPLICIT NONE

 INTEGER(I4)  :: I,J,K, KK,JVL,JLEV,JX,JY
 INTEGER(I4)  :: COORDMI(6),COORDMA(6),KC,KSTEP,FPID
 REAL(R8)     :: TMN,SMN, SPD,ONEN,ZDAS,ZMDS
 REAL(R8)     :: ZNV, ZT(GRD%KM,3), ZS(GRD%KM,3)
 INTEGER(I4)  :: ITLOC(GRD%KM,4),ISLOC(GRD%KM,4)
 REAL(R8),ALLOCATABLE  :: DETA (:,:), SSH_TMP(:,:)
 CHARACTER    :: FGRD
 LOGICAL :: LLNOICECORR
 REAL(R8)     :: SA,S,SB,TA,T,TB
 REAL(R8)     :: ZMIN1,ZMIN2,ZMAX1,ZMAX2,ZMEA(2)
 REAL(R8)     :: ZTEMP(GRD%IM,GRD%JM)
 REAL(R8)     :: ANAMDT(GRD%IM,GRD%JM)
 REAL(R8)     :: ERR(GRD%IM,GRD%JM)
 INTEGER(I4), ALLOCATABLE :: BTMFLT(:,:)
 REAL(R8), PARAMETER :: ZCM0=0.4845_R8, ZCM1=0.8013_R8

#include "wraninc.h"

 CALL MYFRTPROF_WALL('WRT_OUT: WRITE ANALYSIS OUTPUT AND DIAGNOSTICS',0)

IF( NCONF .NE. 202 ) THEN

  IF (LL_TS) THEN

    LLNOICECORR = .TRUE.

    GRD%TEM = GRD%TEM*GRD%MSK
    GRD%SAL = GRD%SAL*GRD%MSK
    GRD%TEM(:,:,GRD%KM) = 0._R8
    GRD%SAL(:,:,GRD%KM) = 0._R8

    IF( LL_BOUNDANAL ) THEN
       WHERE( GRD%TEM .LT. -ZANMAXTEM ) GRD%TEM = -ZANMAXTEM
       WHERE( GRD%TEM .GT.  ZANMAXTEM ) GRD%TEM =  ZANMAXTEM
       WHERE( GRD%SAL .LT. -ZANMAXSAL ) GRD%SAL = -ZANMAXSAL
       WHERE( GRD%SAL .GT.  ZANMAXSAL ) GRD%SAL =  ZANMAXSAL
    ENDIF

    IF( LL_BOTTOMFILTER ) THEN
     ALLOCATE( BTMFLT(GRD%IM,GRD%JM ) )
     DO J=1,GRD%JM
        DO I=1,GRD%IM
          LEV_LOOP : DO JLEV=GRD%KM,1,-1
             IF( GRD%DEP(JLEV) .LT. GRD%HGT(I,J) ) THEN
                 BTMFLT( I, J ) = JLEV
                 EXIT LEV_LOOP
             ENDIF
          ENDDO LEV_LOOP
        ENDDO
     ENDDO
    ENDIF

   DO JLEV=1,GRD%KM-1
    IF( LL_BOTTOMFILTER ) THEN
       WHERE ( BTMFLT-1 .EQ. JLEV ) 
               GRD%TEM(:,:,JLEV) = GRD%TEM(:,:,JLEV) * ZCM1
               GRD%SAL(:,:,JLEV) = GRD%SAL(:,:,JLEV) * ZCM1
       ENDWHERE
       WHERE ( BTMFLT .EQ. JLEV ) 
               GRD%TEM(:,:,JLEV) = GRD%TEM(:,:,JLEV) * ZCM0
               GRD%SAL(:,:,JLEV) = GRD%SAL(:,:,JLEV) * ZCM0
       ENDWHERE
    ENDIF
    IF( LLSHAPIROANL ) THEN
       CALL SHAPIRO_1D(GRD%IM,GRD%JM,GRD%MSK(:,:,JLEV),GRD%TEM(:,:,JLEV),NN_ITERSHAP,ZTEMP)
       GRD%TEM(:,:,JLEV) = ZTEMP
       CALL SHAPIRO_1D(GRD%IM,GRD%JM,GRD%MSK(:,:,JLEV),GRD%SAL(:,:,JLEV),NN_ITERSHAP,ZTEMP)
       GRD%SAL(:,:,JLEV) = ZTEMP
    ENDIF
    ZT(JLEV,1) = MEAN(GRD%TEM(:,:,JLEV),MASK=(INT(GRD%MSK(:,:,JLEV)).EQ.1))
    ZS(JLEV,1) = MEAN(GRD%SAL(:,:,JLEV),MASK=(INT(GRD%MSK(:,:,JLEV)).EQ.1))
    ZT(JLEV,2) = MINVAL(GRD%TEM(:,:,JLEV),MASK=(INT(GRD%MSK(:,:,JLEV)).EQ.1))
    ITLOC(JLEV,1:2) = MINLOC(GRD%TEM(:,:,JLEV),MASK=(INT(GRD%MSK(:,:,JLEV)).EQ.1))
    ZS(JLEV,2) = MINVAL(GRD%SAL(:,:,JLEV),MASK=(INT(GRD%MSK(:,:,JLEV)).EQ.1))
    ISLOC(JLEV,1:2) = MINLOC(GRD%SAL(:,:,JLEV),MASK=(INT(GRD%MSK(:,:,JLEV)).EQ.1))
    ZT(JLEV,3) = MAXVAL(GRD%TEM(:,:,JLEV),MASK=(INT(GRD%MSK(:,:,JLEV)).EQ.1))
    ITLOC(JLEV,3:4) = MAXLOC(GRD%TEM(:,:,JLEV),MASK=(INT(GRD%MSK(:,:,JLEV)).EQ.1))
    ZS(JLEV,3) = MAXVAL(GRD%SAL(:,:,JLEV),MASK=(INT(GRD%MSK(:,:,JLEV)).EQ.1))
    ISLOC(JLEV,3:4) = MAXLOC(GRD%SAL(:,:,JLEV),MASK=(INT(GRD%MSK(:,:,JLEV)).EQ.1))
   ENDDO

   IF( LL_BOTTOMFILTER ) THEN
      DEALLOCATE( BTMFLT )
    ENDIF

   CALL LBC_LNK( GRD%TEM )
   CALL LBC_LNK( GRD%SAL )

   WRITE(IOUNLOG,*)
   WRITE(IOUNLOG,*) '======== FINAL GLOBAL ANALYSIS INCREMENTS'
   WRITE(IOUNLOG,*) 'LEV            TEMPERATURE INCREMENT                      ',&
   & ' |                   SALINITY INCREMENT                  '
   WRITE(IOUNLOG,*) 'LEV    MEAN       MINVAL     MINLOC     MAXVAL     MAXLOC ',&
   & '     MEAN       MINVAL     MINLOC     MAXVAL     MAXLOC  '
   DO JLEV=1,GRD%KM-1
      WRITE(IOUNLOG,'(I4,2F12.6,2I5,F12.6,2I5,2F12.6,2I5,F12.6,2I5)') &
      & JLEV,ZT(JLEV,1),ZT(JLEV,2),ITLOC(JLEV,1:2),ZT(JLEV,3),ITLOC(JLEV,3:4),&
      &      ZS(JLEV,1),ZS(JLEV,2),ISLOC(JLEV,1:2),ZS(JLEV,3),ISLOC(JLEV,3:4)
   ENDDO
   WRITE(IOUNLOG,*)

   WRITE(IOUNLOG,*) ' WRITE ANALYSIS INCREMENTS FILE'
   WRITE(IOUNLOG,*) ' LL_SSH            = ', LL_SSH
   WRITE(IOUNLOG,*) ' LL_WRITE_SLA_BIAS = ', LL_WRITE_SLA_BIAS

 ENDIF

 IF(.NOT.LL_ICE) THEN

 IF( ( LL_SSH .OR. LL_WRITE_SLA_BIAS ) .AND. .NOT. LL_TQ2) THEN
        ALLOCATE( SSH_TMP(GRD%IM,GRD%JM) )
        SSH_TMP = 0._R8
        IF(LL_WRITE_SLA_BIAS) SSH_TMP = SSH_TMP+GRD%MSK(:,:,1)*ZZ_SLA_BIAS
        IF(LL_SSH ) SSH_TMP = SSH_TMP+GRD%ETA
        CALL WRANINC(GRD%IM,GRD%JM,GRD%KM,GRD%LON,GRD%LAT,GRD%DEP,&
        & GRD%SAL,GRD%TEM,.TRUE.,.TRUE.,.FALSE.,.FALSE.,SSH=SSH_TMP)
        DEALLOCATE( SSH_TMP )
 ELSEIF( LL_TQ2 .AND. .NOT. ( LL_SSH .OR. LL_WRITE_SLA_BIAS ) ) THEN 
  CALL WRANINC(GRD%IM,GRD%JM,GRD%KM,GRD%LON,GRD%LAT,GRD%DEP,&
  & GRD%SAL,GRD%TEM,.TRUE.,.FALSE.,.TRUE.,.FALSE.,T2M=GRD%T2M,Q2M=GRD%Q2M)
 ELSEIF( LL_TQ2 .AND. ( LL_SSH .OR. LL_WRITE_SLA_BIAS ) ) THEN
  ALLOCATE( SSH_TMP(GRD%IM,GRD%JM) )
  SSH_TMP = 0._R8
  IF(LL_WRITE_SLA_BIAS) SSH_TMP = SSH_TMP+GRD%MSK(:,:,1)*ZZ_SLA_BIAS
  IF(LL_SSH ) SSH_TMP = SSH_TMP+GRD%ETA
  CALL WRANINC(GRD%IM,GRD%JM,GRD%KM,GRD%LON,GRD%LAT,GRD%DEP,&
  & GRD%SAL,GRD%TEM,.TRUE.,.TRUE.,.TRUE.,.FALSE.,SSH=SSH_TMP,T2M=GRD%T2M,Q2M=GRD%Q2M)
 ELSE
  CALL WRANINC(GRD%IM,GRD%JM,GRD%KM,GRD%LON,GRD%LAT,GRD%DEP,&
  & GRD%SAL,GRD%TEM,.TRUE.,.FALSE.,.FALSE.,.FALSE.)
 ENDIF

 ELSE

 IF( ( LL_SSH .OR. LL_WRITE_SLA_BIAS ) .AND. .NOT. LL_TQ2) THEN
        ALLOCATE( SSH_TMP(GRD%IM,GRD%JM) )
        SSH_TMP = 0._R8
        IF(LL_WRITE_SLA_BIAS) SSH_TMP = SSH_TMP+GRD%MSK(:,:,1)*ZZ_SLA_BIAS
        IF(LL_SSH ) SSH_TMP = SSH_TMP+GRD%ETA
        CALL WRANINC(GRD%IM,GRD%JM,GRD%KM,GRD%LON,GRD%LAT,GRD%DEP,&
        & GRD%SAL,GRD%TEM,.TRUE.,.TRUE.,.FALSE.,.TRUE.,SSH=SSH_TMP,SIC=GRD%SIC,SIT=GRD%SIT,&
        & ICU=GRD%ICU, ICV=GRD%ICV)
        DEALLOCATE( SSH_TMP )
 ELSEIF( LL_TQ2 .AND. .NOT. ( LL_SSH .OR. LL_WRITE_SLA_BIAS ) ) THEN 
        CALL WRANINC(GRD%IM,GRD%JM,GRD%KM,GRD%LON,GRD%LAT,GRD%DEP,&
        & GRD%SAL,GRD%TEM,.TRUE.,.FALSE.,.TRUE.,.TRUE.,&
        & T2M=GRD%T2M,Q2M=GRD%Q2M,SIC=GRD%SIC,SIT=GRD%SIT,&
        & ICU=GRD%ICU, ICV=GRD%ICV)
 ELSEIF( LL_TQ2 .AND. ( LL_SSH .OR. LL_WRITE_SLA_BIAS ) ) THEN
        ALLOCATE( SSH_TMP(GRD%IM,GRD%JM) )
        SSH_TMP = 0._R8
        IF(LL_WRITE_SLA_BIAS) SSH_TMP = SSH_TMP+GRD%MSK(:,:,1)*ZZ_SLA_BIAS
        IF(LL_SSH ) SSH_TMP = SSH_TMP+GRD%ETA
        CALL WRANINC(GRD%IM,GRD%JM,GRD%KM,GRD%LON,GRD%LAT,GRD%DEP,&
        & GRD%SAL,GRD%TEM,.TRUE.,.TRUE.,.TRUE.,.TRUE.,SSH=SSH_TMP,&
        & T2M=GRD%T2M,Q2M=GRD%Q2M,SIC=GRD%SIC,SIT=GRD%SIT,&
        & ICU=GRD%ICU, ICV=GRD%ICV)
 ELSEIF( .NOT. LL_TS .AND. .NOT. LL_TQ2 .AND. .NOT. LL_SSH ) THEN
        CALL WRANINC(GRD%IM,GRD%JM,GRD%KM,GRD%LON,GRD%LAT,GRD%DEP,&
       & GRD%SAL,GRD%TEM,.FALSE.,.FALSE.,.FALSE.,.TRUE.,SIC=GRD%SIC,SIT=GRD%SIT,&
       & ICU=GRD%ICU, ICV=GRD%ICV) 
 ELSE
       CALL WRANINC(GRD%IM,GRD%JM,GRD%KM,GRD%LON,GRD%LAT,GRD%DEP,&
       & GRD%SAL,GRD%TEM,.TRUE.,.FALSE.,.FALSE.,.TRUE.,SIC=GRD%SIC,SIT=GRD%SIT,&
       & ICU=GRD%ICU, ICV=GRD%ICV)

 ENDIF

 ENDIF

 IF( LL_HRINTERP .OR. ABS(IDOUBLEDOM) .EQ. 1) THEN
   WRITE(IOUNLOG,*) ' HIGH RESOLUTION CONVERSION'
   CALL HRINTERP
 ENDIF

 ZNV=1.0E+20_R8

 IF( .NOT. LL_IAU ) THEN
   WRITE(IOUNLOG,*) ' WRITE CORRECTIONS'
   IF( LL_WRITECORRBIN ) THEN
      CALL WRITECORRLOCBIN(GRD%TEM,GRD%SAL)
   ELSE
      CALL WRITECORRLOC(GRD%TEM,GRD%SAL,LLNOICECORR)
   ENDIF
 ENDIF

ENDIF

IF(LLVARMDT .OR. NCONF.EQ.202) THEN

    ANAMDT = GRD%CMDT + GRD%MDT
    ERR = GRD%CMDT_STDEV * ZVARMDT_RATIOSD
    ZNV=-999._R8
    ZDAS = 0._R8
    ZMDS = 0._R8
    DO JY=1,GRD%JM
       DO JX=1,GRD%IM
          IF(ABS(ANAMDT(JX,JY)).GE. 10._R8 .OR. GRD%MSK(JX,JY,1).LT.0.9_R8) THEN
              ANAMDT(JX,JY)=ZNV
              GRD%CMDT(JX,JY)=ZNV
              GRD%CMDT_STDEV(JX,JY)=ZNV
          ELSE
              ZDAS = ZDAS + GRD%DX(JX,JY)*GRD%DY(JX,JY)
              ZMDS = ZMDS + ANAMDT(JX,JY)*GRD%DX(JX,JY)*GRD%DY(JX,JY)
          ENDIF
       ENDDO
    ENDDO

    WRITE(IOUNLOG,*) ' MDT SPATIAL AVERAGE IS ', ZMDS / ZDAS

    WHERE ( ABS(ANAMDT).LT. 10._R8 .AND. GRD%MSK(:,:,1).GT.0.9_R8) 
       ANAMDT = ANAMDT - (ZMDS/ZDAS)
    ENDWHERE

    CALL WRMDT(GRD%IM,GRD%JM,GRD%LON,GRD%LAT,&
    & ANAMDT,GRD%CMDT,ERR,ZNV,ZMDS / ZDAS,ZVARMDT_RATIOSD)

ENDIF

IF(COBSOPT.EQ.'GLO' .OR. COBSOPT.EQ.'ASC') CALL WRT_OBS

 CALL MYFRTPROF_WALL('WRT_OUT: WRITE ANALYSIS OUTPUT AND DIAGNOSTICS',1)
END SUBROUTINE WRT_OUT
