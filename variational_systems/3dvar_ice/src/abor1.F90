#ifdef NECSX
SUBROUTINE ABOR1(CDTEXT) ! A.S. DUMMY ABORT ALLOWS INLINING
USE IOUNITS
USE MYFRTPROF, ONLY : MYFRTPROF_PRINT
IMPLICIT NONE
CHARACTER(LEN=*), INTENT(IN) :: CDTEXT
LOGICAL :: LLOGOPE
WRITE(IOUNOUT,'(1X,A)') CDTEXT
WRITE(IOUNERR,'(1X,A)') CDTEXT
INQUIRE(UNIT=IOUNLOG, OPENED=LLOGOPE)
IF(LLOGOPE) THEN
    CALL MYFRTPROF_PRINT(-1,IOUNLOG)
    CLOSE(IOUNLOG)
ELSE
    CALL MYFRTPROF_PRINT(-1,IOUNOUT)
ENDIF
CALL FLUSH(IOUNLOG)
CALL FLUSH(IOUNOUT)
CALL ABORT
END SUBROUTINE ABOR1
#else
SUBROUTINE ABOR1(CDTEXT)

!... ABOR1
!    -----------------------------------------
!    PROPERLY ABORT THE JOB, CALLING TRACEBACK
!    AND MYFRTPROF
!    ACCORDING TO ARCHITECTURE/PROCESSES.
!    ADAPTED FROM IAAA CODE:
!    - INTRODUCTION OF MYFRTPROF PACKAGE
!    - REMOVAL OF MANDATORY MPI SUPPORT

USE SET_KND  ,ONLY : I4
USE IOUNITS, ONLY : IOUNOUT,IOUNERR, IOUNLOG
USE MYFRTPROF, ONLY : MYFRTPROF_PRINT
USE MPIREL,  ONLY : MYPROC, LMPION
#ifndef NOMPI
USE MPI
#endif

IMPLICIT NONE

CHARACTER(LEN=*), INTENT(IN) :: CDTEXT
INTEGER(KIND=I4) :: ILEN
INTEGER(KIND=I4) :: IRETURN_CODE,IERROR
LOGICAL :: LLOGOPE

IF (IOUNOUT >= 0) WRITE(IOUNOUT,*)
IF (IOUNERR >= 0) WRITE(IOUNERR,*)

!... BY DEFAULT, TRY TO PRINT MYFRTPROF OUTPUT ON LOGFILE,
!    IF NOT OPEN, ON STDOUT
INQUIRE(UNIT=IOUNLOG, OPENED=LLOGOPE)
IF(LLOGOPE) THEN
    CALL MYFRTPROF_PRINT(-1,IOUNLOG)
    CLOSE(IOUNLOG)
ELSE
    CALL MYFRTPROF_PRINT(-1,IOUNOUT)
ENDIF


ILEN=LEN(CDTEXT)
IF (IOUNOUT >= 0) WRITE(IOUNOUT,'(1X,A)') 'ABOR1 CALLED'

IF(ILEN <= 512) THEN
  IF (IOUNOUT >= 0) WRITE(IOUNOUT,'(1X,A)') CDTEXT
  IF (IOUNERR >= 0) WRITE(IOUNERR,'(1X,A,1X,I3,1X,A,A)') &
  & 'ABORT FROM PROC ',MYPROC,': ',CDTEXT
ELSE
  IF (IOUNERR >= 0) WRITE(IOUNERR,'(1X,A,1X,I3,1X,A)') &
  & 'ABORT FROM PROC ',MYPROC,': ABOR1 CALLED WITHOUT TEXT STRING'
ENDIF
IF (IOUNOUT >= 0) WRITE(IOUNOUT,*)
IF (IOUNERR >= 0) WRITE(IOUNERR,*)

IF (IOUNOUT >= 0) THEN
  CALL FLUSH(IOUNOUT)
  IF (IOUNOUT /= 0 .AND. IOUNOUT /= 6) CLOSE(IOUNOUT)
ENDIF

CALL FLUSH(IOUNLOG)
CALL FLUSH(IOUNOUT)

IF (LMPION) THEN
#ifndef NOMPI
  WRITE(IOUNOUT,*) 'CALLING MPI_ABORT'
  IRETURN_CODE=1
  CALL MPI_ABORT(MPI_COMM_WORLD,IRETURN_CODE,IERROR)
#else
! THIS CANNOT HAPPEN UNLESS EXTERNAL USE
  CALL FLUSH(0)
#endif
ELSE
  CALL FLUSH(0)
ENDIF

END SUBROUTINE ABOR1
#endif
