SUBROUTINE COMPSC_LV

USE SET_KND
USE RECFILTER
USE GRD_STR
USE LBCLNK
USE EXTGRID
USE MYFRTPROF

!-- COMPUTE SCALING FACTOR FOR RF

IMPLICIT  NONE

INTEGER(KIND=I4) :: NX,NY,NZ
INTEGER(KIND=I4) :: NSPL,NTB
REAL(KIND=R8) :: DSMN,DSMX,DSL,DST,E,ZCR,TMP,RCR,KMD,RDD,TCR
REAL(KIND=R8) :: B1, B2, B3, B, SIGMA
INTEGER(KIND=I4) :: JLEV,K,J,I,JCR,KD,KR,IAUX2(2),IERR,KR2,KD2
REAL(KIND=R8),ALLOCATABLE :: SFCT(:),AL(:),BT(:)
INTEGER(KIND=I4),ALLOCATABLE :: JNXX(:),IAUX(:)
REAL(KIND=R8),ALLOCATABLE :: ALNS(:,:),SCNS(:,:)
CHARACTER(LEN=99) :: CFBIN

CALL MYFRTPROF_WALL('COMPSC_LV: COMPUTE RF SCALING FACTORS',0)

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' /// COMPUTE RF SCALING FACTORS'
WRITE(IOUNLOG,*)

#ifndef USE_POINTERS
IF(.NOT.ALLOCATED(NEWDX).OR..NOT.ALLOCATED(NEWDY)) THEN
   WRITE(IOUNERR,*) 'COMPSC CALLED BEFORE GRID INITIALIZATION'
   CALL ABOR1('ERROR IN COMPSC, CANNOT PROCEED')
ENDIF
#endif

IF(.NOT.LLRFINIT) &
& CALL ABOR1('COMPSC CALLED BEFORE RF INITIALIZATION')

NX=GRD%IM
NY=GRD%JM
NZ=GRD%KM

IF( NCONF .NE. 104 .AND. NCONF .NE. 202 ) THEN

  ! Convert to meters
  CRX=CRX*RF_CORRAD_MFACT
  CRY=CRY*RF_CORRAD_MFACT

  WRITE(IOUNLOG,*)
  WRITE(IOUNLOG,*) ' *** AVERAGED CORRAD FOR LEVEL 1'
  WRITE(IOUNLOG,*) '     ',SUM(CRX(:,:,1)*GRD%DX*GRD%DY*GRD%MSK(:,:,1))/&
                           SUM(GRD%DX*GRD%DY*GRD%MSK(:,:,1)),&
                           SUM(CRY(:,:,1)*GRD%DX*GRD%DY*GRD%MSK(:,:,1))/&
                           SUM(GRD%DX*GRD%DY*GRD%MSK(:,:,1))
  WRITE(IOUNLOG,*)
  
  ! Bound
  WHERE( CRX .LT. RF_CRMN ) CRX = RF_CRMN
  WHERE( CRY .LT. RF_CRMN ) CRY = RF_CRMN
  WHERE( CRX .GT. RF_CRMX ) CRX = RF_CRMX
  WHERE( CRY .GT. RF_CRMX ) CRY = RF_CRMX

  IF( LLVARMDT ) THEN
     CRMX=CRMX*RF_CORRAD_MFACT
     CRMY=CRMY*RF_CORRAD_MFACT
     WHERE( CRMX .LT. RF_CRMN ) CRMX = RF_CRMN
     WHERE( CRMY .LT. RF_CRMN ) CRMY = RF_CRMN
     WHERE( CRMX .GT. RF_CRMX ) CRMX = RF_CRMX
     WHERE( CRMY .GT. RF_CRMX ) CRMY = RF_CRMX
  ENDIF
ENDIF

IF( NCONF .EQ. 104 ) OPEN(931,FILE='n104.out')

ALLOCATE(IAUX(1))

NSPL=MAX(NX,NY)
ALLOCATE (SFCT(NSPL),JNXX(NSPL),AL(NSPL),BT(NSPL) )
NTB = MIN(RF_NRES,MIN(NX,NY))

DSMN=MIN(MINVAL(NEWDX),MINVAL(NEWDY))
DSMX=MAX(MAXVAL(NEWDX),MAXVAL(NEWDY))

ZCR=(RF_CRMX-RF_CRMN)/(REAL(RF_NCR,KIND=R8)-1._R8)

IF( NCONF .NE. 202 ) ALLOCATE(SCXNS(NX,NY,NTOTV), SCYNS(NX,NY,NTOTV))
ALLOCATE(ALNS(NTB,RF_NCR))
ALLOCATE(SCNS(NTB,RF_NCR))

WRITE(IOUNLOG,*) ' MAX,MIN DIM.:',NSPL,NTB
WRITE(IOUNLOG,*) ' MAX,MIN RES.:',DSMX,DSMN

DSMX=DSMX+MAX(1._R8,(DSMX-DSMN)/(REAL(NTB,KIND=R8)-2._R8))
DSL=(DSMX-DSMN)/(REAL(NTB,KIND=R8)-1._R8)

IAUX(1) = NSPL

IF( .NOT. LL_READCOMPSC_LV ) THEN
  DO JCR=1,RF_NCR
    DO K=1,NTB
      DST=DSMN+(K-1._R8)*DSL
      RCR=RF_CRMN+(JCR-1._R8)*ZCR
      SFCT(:)=0._R8
      SFCT(NSPL/2 + 1)=1._R8
      IF ( RCF_TYPE .EQ. 1 ) THEN
        E=(2._R8*RF_NTR) * DST**2 / (4._R8 * RCR**2)
        ALNS(K,JCR) = 1._R8 + E - SQRT(E*(E+2._R8))
        AL(:)=ALNS(K,JCR)
        BT(:)=ALNS(K,JCR)
        DO J=1,NSPL
          JNXX(J)=J
        ENDDO
        CALL RCFL_Y_OLD(1,NSPL,1,NSPL,AL,BT,SFCT,JNXX,IAUX)
        CALL RCFL_Y_AD_OLD(1,NSPL,1,NSPL,AL,BT,SFCT,JNXX,IAUX)
      ELSEIF ( RCF_TYPE .EQ. 2 ) THEN
        SIGMA = MAX(0.5_R8,RCR/DST)
        CALL DEF_COEF(SIGMA,B1,B2,B3,B) 
        CALL RCFL_2(NSPL,SFCT,B1,B2,B3,B) 
        CALL RCFL_2_AD(NSPL,SFCT,B1,B2,B3,B) 
      ENDIF
      SCNS(K,JCR) = SFCT(NSPL/2+1)
      IF(NCONF.EQ.104) WRITE(931,*) DST, RCR, SCNS(K,JCR)
    ENDDO
  ENDDO
ENDIF

IF ( RCF_TYPE .EQ. 1 ) THEN
  CFBIN='COMPSC_LV.bin'
ELSEIF ( RCF_TYPE .EQ. 2 ) THEN
  CFBIN='COMPSC_LV_T2.bin'
ENDIF

IF( LL_WRITECOMPSC_LV ) THEN
    OPEN(601,FILE=CFBIN,STATUS='NEW',FORM='UNFORMATTED',&
    & ACCESS='SEQUENTIAL')
    WRITE(601) RF_NCR, NTB
    WRITE(601) SCNS
    CLOSE(601)
ENDIF

IF( NCONF .EQ. 104 ) THEN
    CLOSE(931)
    CALL MYFRTPROF_WALL('COMPSC_LV: COMPUTE RF SCALING FACTORS',1)
    RETURN
ENDIF

IF( LL_READCOMPSC_LV ) THEN
    OPEN(601,FILE=CFBIN,STATUS='OLD',FORM='UNFORMATTED',&
    & ACCESS='SEQUENTIAL')
    READ(601) IAUX2(1), IAUX2(2)
    IF( IAUX2(1) .NE. RF_NCR .OR. IAUX2(2) .NE. NTB ) THEN
        WRITE(IOUNERR,*) 'PROBLEM WHILE READING ',TRIM(CFBIN)
        WRITE(IOUNERR,*) 'IAUX2(1:2) = ',IAUX2(1:2)
        WRITE(IOUNERR,*) '(RF_NCR,NTB) = ',RF_NCR, NTB
        CALL ABOR1(' PROBLEM IN THE '//TRIM(CFBIN)//' HEADER')
    ENDIF
    READ(601,IOSTAT=IERR) SCNS
    IF(IERR.NE.0) CALL ABOR1(' PROBLEM IN THE '//TRIM(CFBIN)//' BODY')
    CLOSE(601)
ENDIF

IF(NCONF .NE. 202 ) THEN
#ifdef SHARED_MEMORY
!$OMP PARALLEL DEFAULT(SHARED),PRIVATE(JLEV,J,I,DST,KD,KD2,KMD,TCR,&
!$OMP KR,KR2,RDD,TMP)
!$OMP DO SCHEDULE(DYNAMIC,1)
#endif
DO JLEV=1,NTOTV
   DO J=1,NY
     DO I=1,NX

         DST = ( NEWDX(I,J) - DSMN )/DSL
         KD = INT(DST) + 1
         DST = DST - REAL(KD-1)
         KMD = DST
         KD2 = KD+1
         IF( KD2 .GT. NTB ) KD2=KD
         TCR = CRX(I,J,JLEV)
         DST = ( TCR - RF_CRMN )/ZCR
         KR = INT(DST) + 1
         DST = DST - REAL(KR-1)
         RDD = DST
         KR2 = KR+1
         IF( KR2 .GT. RF_NCR ) KR2=KR
         TMP = SCNS(KD,KR)*(1._R8-KMD)*(1._R8-RDD) + &
             & SCNS(KD2,KR)*(KMD)*(1._R8-RDD) + &
             & SCNS(KD,KR2)*(1._R8-KMD)*(RDD) + &
             & SCNS(KD2,KR2)*(KMD)*(RDD)
         SCXNS(I,J,JLEV) = &
         & SQRT( 1._R8/ TMP )

         DST = ( NEWDY(I,J) - DSMN )/DSL
         KD = INT(DST) + 1
         DST = DST - REAL(KD-1)
         KMD = DST
         KD2 = KD+1
         IF( KD  .GT. NTB ) KD =NTB
         IF( KD2 .GT. NTB ) KD2=KD
         TCR = CRY(I,J,JLEV)
         DST = ( TCR - RF_CRMN )/ZCR
         KR = INT(DST) + 1
         DST = DST - REAL(KR-1)
         RDD = DST
         KR2 = KR+1
         IF( KR .GT. RF_NCR  ) KR=RF_NCR
         IF( KR2 .GT. RF_NCR ) KR2=KR
         TMP = SCNS(KD,KR)*(1._R8-KMD)*(1._R8-RDD) + &
             & SCNS(KD2,KR)*(KMD)*(1._R8-RDD) + &
             & SCNS(KD,KR2)*(1._R8-KMD)*(RDD) + &
             & SCNS(KD2,KR2)*(KMD)*(RDD)
         SCYNS(I,J,JLEV) = &
         & SQRT( 1._R8/ TMP )

     ENDDO
   ENDDO
   WRITE(IOUNLOG,*) 'SCXNS ', JLEV, ': ',SUM(SCXNS(:,:,JLEV))/(NY*NX),&
   & MINVAL(SCXNS(:,:,JLEV)),MAXVAL(SCXNS(:,:,JLEV))
   WRITE(IOUNLOG,*) 'SCYNS ', JLEV, ': ',SUM(SCYNS(:,:,JLEV))/(NY*NX),&
   & MINVAL(SCYNS(:,:,JLEV)),MAXVAL(SCYNS(:,:,JLEV))
ENDDO
#ifdef SHARED_MEMORY
!$OMP END DO
!$OMP END PARALLEL
#endif
ENDIF

IF( LLVARMDT .OR. NCONF.EQ.202) THEN
   ALLOCATE(SCXNZ(NX,NY), SCYNZ(NX,NY))
   DO J=1,NY
     DO I=1,NX
         DST = ( NEWDX(I,J) - DSMN )/DSL
         KD = INT(DST) + 1
         DST = DST - REAL(KD-1)
         KMD = DST
         KD2 = KD+1
         IF( KD2 .GT. NTB ) KD2=KD
         TCR = CRMX(I,J)
         DST = ( TCR - RF_CRMN )/ZCR
         KR = INT(DST) + 1
         DST = DST - REAL(KR-1)
         RDD = DST
         KR2 = KR+1
         IF( KR2 .GT. RF_NCR ) KR2=KR
         TMP = SCNS(KD,KR)*(1._R8-KMD)*(1._R8-RDD) + &
             & SCNS(KD2,KR)*(KMD)*(1._R8-RDD) + &
             & SCNS(KD,KR2)*(1._R8-KMD)*(RDD) + &
             & SCNS(KD2,KR2)*(KMD)*(RDD)
         SCXNZ(I,J) = &
         & SQRT( 1._R8/ TMP )

         DST = ( NEWDY(I,J) - DSMN )/DSL
         KD = INT(DST) + 1
         DST = DST - REAL(KD-1)
         KMD = DST
         KD2 = KD+1
         IF( KD2 .GT. NTB ) KD2=KD
         TCR = CRMY(I,J)
         DST = ( TCR - RF_CRMN )/ZCR
         KR = INT(DST) + 1
         DST = DST - REAL(KR-1)
         RDD = DST
         KR2 = KR+1
         IF( KR2 .GT. RF_NCR ) KR2=KR
         TMP = SCNS(KD,KR)*(1._R8-KMD)*(1._R8-RDD) + &
             & SCNS(KD2,KR)*(KMD)*(1._R8-RDD) + &
             & SCNS(KD,KR2)*(1._R8-KMD)*(RDD) + &
             & SCNS(KD2,KR2)*(KMD)*(RDD)
         SCYNZ(I,J) = &
         & SQRT( 1._R8/ TMP )

     ENDDO
   ENDDO

ENDIF

! Temporary
!CALL WRITE_VARNCDF('scx.nc',GRD%IM,GRD%JM,SCXNS(:,:,1),'scxns','','')
!CALL WRITE_VARNCDF('scy.nc',GRD%IM,GRD%JM,SCYNS(:,:,1),'scyns','','')

DEALLOCATE(ALNS,SCNS)

CALL MYFRTPROF_WALL('COMPSC_LV: COMPUTE RF SCALING FACTORS',1)
END SUBROUTINE COMPSC_LV
