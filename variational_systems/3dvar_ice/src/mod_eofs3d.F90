MODULE EOFS3D

USE SET_KND
USE GRD_STR
USE EOF_STR
USE MYNETCDF
USE MYFRTPROF
USE IOUNITS

IMPLICIT NONE

LOGICAL :: LL_EOFS3D
INTEGER(I4) :: NN_EOFS3D, NEKMT
REAL(R8)    :: AEO

REAL(R8), ALLOCATABLE :: &
& EIVAS(:), EIVES(:,:,:,:), &
& PSE(:,:,:), PSE_AD(:,:,:), &
& ROE(:), ROE_AD(:)

INTEGER(I4) :: CVE_S, CVE_E

CONTAINS 

SUBROUTINE SUEOF3D

IMPLICIT NONE

INTEGER(I4) :: JE

CALL MYFRTPROF_WALL('SUEOF3D: SET-UP EOFS3D EOF',0)

! ---
! EOFS

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) '  /// EOFS SETUP FOR EOFS3D SCHEME'

NEKMT = ROS%KMT

WRITE(IOUNLOG,*) '  DIMENSION, EOFS : ',NN_EOFS3D
WRITE(IOUNLOG,*) '  DIMENSION, EOFS : ',NEKMT

ALLOCATE( EIVAS(NN_EOFS3D) )
ALLOCATE( EIVES(GRD%IM,GRD%JM,NEKMT,NN_EOFS3D) )

CALL GETNCVAR('EOFS3D.nc','EIVAs',NN_EOFS3D,EIVAS)
CALL GETNCVAR('EOFS3D.nc','EOFs',GRD%IM,GRD%JM,NEKMT,NN_EOFS3D,EIVES)

DO JE=1,NN_EOFS3D
  EIVES(:,:,1:GRD%KM,JE) = EIVES(:,:,1:GRD%KM,JE)*GRD%MSK
  EIVES(:,:,(GRD%KM+1):(2*GRD%KM),JE) = EIVES(:,:,(GRD%KM+1):(2*GRD%KM),JE)*GRD%MSK
ENDDO

ALLOCATE( PSE   (GRD%IM,GRD%JM,NEKMT) )
ALLOCATE( PSE_AD(GRD%IM,GRD%JM,NEKMT) )
ALLOCATE( ROE   (          NN_EOFS3D) )
ALLOCATE( ROE_AD(          NN_EOFS3D) )

CALL MYFRTPROF_WALL('SUEOF3D: SET-UP EOFS3D EOF',1)
END SUBROUTINE SUEOF3D

SUBROUTINE EOF3D
IMPLICIT NONE
INTEGER(I4) :: JE,JI,JJ,JK
CALL MYFRTPROF_WALL('EOF3D: EOF3D COVARIANCE',0)
PSE = 0._R8
DO JE=1,NN_EOFS3D
 DO JK=1,NEKMT
  DO JJ=1,GRD%JM
   DO JI=1,GRD%IM
     PSE(JI,JJ,JK) = PSE(JI,JJ,JK) + ROE(JE)*EIVAS(JE)*EIVES(JI,JJ,JK,JE)
   ENDDO
  ENDDO
 ENDDO
ENDDO
PSV = AEO*PSV + SQRT(1-AEO*AEO)*PSE
CALL MYFRTPROF_WALL('EOF3D: EOF3D COVARIANCE',1)
END SUBROUTINE EOF3D

SUBROUTINE EOF3D_AD
IMPLICIT NONE
INTEGER(I4) :: JE,JI,JJ,JK
CALL MYFRTPROF_WALL('EOF3D: EOF3D COVARIANCE',0)
PSE_AD = SQRT(1-AEO*AEO)*PSV_AD
PSV_AD = AEO*PSV_AD
ROE_AD = 0._R8
DO JE=NN_EOFS3D,1,-1
 DO JK=NEKMT,1,-1
  DO JJ=GRD%JM,1,-1
   DO JI=GRD%IM,1,-1
     ROE_AD(JE) = ROE_AD(JE) + EIVAS(JE)*EIVES(JI,JJ,JK,JE)*PSE_AD(JI,JJ,JK)
   ENDDO
  ENDDO
 ENDDO
ENDDO
CALL MYFRTPROF_WALL('EOF3D: EOF3D COVARIANCE',1)
END SUBROUTINE EOF3D_AD

END MODULE EOFS3D
