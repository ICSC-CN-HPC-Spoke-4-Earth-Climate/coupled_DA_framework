SUBROUTINE READEN3FILE(CFILENAME,NUMLEVS,IOUN,MAXOBS,LLSTRICTPOS,LLSTRICTTIME,&
         & LSTRICTPSU,LSTRICTTEM,LGLOBTQF,LGLOBSQF,LLCHECKTIME, &
         & NTOTOBS,OBSTYPE, OBSPAR,OBSDEP,OBSVAL, OBSTIME, &
         & OBSLON,OBSLAT,OBSPROF,OBSPLAT)

!
! -> READ IN OBSERVATIONS FROM ENSEMBLES INS
!    FROM NATIVE NETCDF FORMAT
!
!    ANDREA STORTO _2009-03_
!

USE SET_KND
USE OBSDEF
USE RUN
USE NETCDF
USE CALENDAR, ONLY : TIME_DIFF,YMDS2JU

IMPLICIT NONE

CHARACTER(LEN=*) , INTENT(IN) :: CFILENAME
INTEGER(I4),INTENT(IN) :: NUMLEVS,IOUN, MAXOBS
INTEGER(I4),INTENT(OUT) :: NTOTOBS
!... SWITCHES
LOGICAL, INTENT(IN) :: LLSTRICTPOS,LSTRICTPSU,LLSTRICTTIME,&
         & LSTRICTTEM,LGLOBTQF,LGLOBSQF,LLCHECKTIME

REAL   (DP)  :: DATE_START, DATE_END, ZJULREF
INTEGER(I4) :: IYY,IMM,IDD,IHH,IMI,ISS
REAL   (DP)  :: ZSS,ZANDATELOC

INTEGER(I4) :: KERR(59),KOK(5),KCO(10)
INTEGER(I4) :: STAT, NCID, IDVAR, JOBS
INTEGER(I4) :: NDTSTR,NPROFS,NPARAMS,NLEVELS,KDTSTR
INTEGER(I4) :: JLEV,JPROF,ITYPE

!... OBS STORAGE
INTEGER(I4), PARAMETER :: MAXPROFS = 99000
INTEGER(I4), DIMENSION(MAXOBS),INTENT(OUT) :: OBSTYPE, OBSPAR, OBSPROF
REAL   (DP), DIMENSION(MAXOBS),INTENT(OUT) :: OBSTIME
REAL   (R8), DIMENSION(MAXOBS),INTENT(OUT) :: OBSDEP,  OBSVAL, OBSLON,OBSLAT
CHARACTER(LEN=8 ), DIMENSION(MAXOBS),INTENT(OUT) :: OBSPLAT
CHARACTER(LEN=40) :: CREFDT

REAL   (R8)      , ALLOCATABLE, DIMENSION(:,:) :: TEMP,SALC,DEPTHC
REAL   (R8)      , ALLOCATABLE, DIMENSION(:)   :: LATS,LONS
REAL   (DP)      , ALLOCATABLE, DIMENSION(:)   :: TIME
CHARACTER(LEN=4 ), ALLOCATABLE, DIMENSION(:)   :: INST
CHARACTER(LEN=8 ), ALLOCATABLE, DIMENSION(:)   :: PLANO
CHARACTER(LEN=NUMLEVS), DIMENSION(MAXPROFS) :: TQC, SQC
CHARACTER(LEN=MAXPROFS) :: TGQC,SGQC,POSQC,TIMQC

INTEGER(4)  :: IDIFFER1, IDIFFER2,IRETC

INTEGER(4)  :: KPROF

LOGICAL :: LPROFT, LPROFS, LOBS, LLPROFOK

!  INITIALIZE
KCO=0
KERR=0
KOK =0
JOBS=0

WRITE(IOUN,*)
WRITE(IOUN,*) '*** READEN3FILE : READING NETCDF FILE '//TRIM(CFILENAME)
WRITE(IOUN,*)

    STAT = NF90_OPEN(CFILENAME, NF90_NOWRITE, NCID)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

    STAT = NF90_INQ_DIMID (NCID, 'DATE_TIME', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = KDTSTR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQ_DIMID (NCID, 'N_PROF', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = NPROFS)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQ_DIMID (NCID, 'N_PARAM', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = NPARAMS)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQ_DIMID (NCID, 'N_LEVELS', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = NLEVELS)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'NETCDF DIMENSIONS READ'

    IF(NPROFS.GT.MAXPROFS) &
    & CALL ABOR1('READEN3FILE : MAXPROFS MUST BE INCREASED!')
    IF(NLEVELS.GT.NUMLEVS) &
    & CALL ABOR1('READEN3FILE : NUMLEVS DOES NOT MATCH!')

    STAT = NF90_INQ_VARID (NCID, 'REFERENCE_DATE_TIME', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,CREFDT(1:KDTSTR))
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'REFERENCE_DATE_TIME READ'

    IF(LLCHECKTIME) THEN

      WRITE(IOUN,*) 'REF DATE IS ', CREFDT(1:KDTSTR)

      READ(CREFDT(1:4),*) IYY
      READ(CREFDT(5:6),*) IMM
      READ(CREFDT(7:8),*) IDD
      READ(CREFDT(9:10),*) IHH
      READ(CREFDT(11:12),*) IMI
      READ(CREFDT(13:14),*) ISS

      ZSS = REAL(IHH,KIND=DP)/REAL(24,KIND=DP)  + &
      & REAL(IMI,KIND=DP)/REAL(24*60,KIND=DP)   + &
      & REAL(ISS,KIND=DP)/REAL(24*60*60,KIND=DP)

      CALL YMDS2JU(IYY,IMM,IDD,ZSS,ZJULREF)

      DATE_START = ZJULSTART - ZJULREF
      DATE_END   = ZJULEND   - ZJULREF

      WRITE(IOUN,*) ' DATESTART : ',DATE_START
      WRITE(IOUN,*) ' DATEEND   : ',DATE_END
      WRITE(IOUN,*) ' ... JULIAN DAYS SINCE '//CREFDT(1:10)

    ENDIF

    ALLOCATE(LATS(NPROFS),LONS(NPROFS),TIME(NPROFS),INST(NPROFS),&
           & PLANO(NPROFS))

    STAT = NF90_INQ_VARID (NCID, 'LATITUDE', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,LATS)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'LATITUDE READ'

    STAT = NF90_INQ_VARID (NCID, 'LONGITUDE', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,LONS)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'LONGITUDE READ'

    STAT = NF90_INQ_VARID (NCID, 'JULD', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,TIME)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'JULD READ'

    STAT = NF90_INQ_VARID (NCID, 'WMO_INST_TYPE', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,INST)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'WMO_INST_TYPE READ'

    STAT = NF90_INQ_VARID (NCID, 'PLATFORM_NUMBER', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,PLANO)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'PLATFORM_NUMBER READ'

    IF(LLSTRICTPOS) THEN
          STAT = NF90_INQ_VARID (NCID, 'POSITION_QC', IDVAR)
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          STAT = NF90_GET_VAR (NCID,IDVAR,POSQC(1:NPROFS))
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          WRITE(IOUN,*) 'POSITION_QC READ'
    ENDIF

    IF(LLSTRICTTIME) THEN
          STAT = NF90_INQ_VARID (NCID, 'JULD_QC', IDVAR)
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          STAT = NF90_GET_VAR (NCID,IDVAR,TIMQC(1:NPROFS))
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          WRITE(IOUN,*) 'POSITION_QC READ'
    ENDIF

    IF(LGLOBTQF) THEN
          STAT = NF90_INQ_VARID (NCID, 'PROFILE_POTM_QC', IDVAR)
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          STAT = NF90_GET_VAR (NCID,IDVAR,TGQC(1:NPROFS))
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          WRITE(IOUN,*) 'PROFILE_POTM_QC READ'
    ENDIF

    IF(LGLOBSQF) THEN
          STAT = NF90_INQ_VARID (NCID, 'PROFILE_PSAL_QC', IDVAR)
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          STAT = NF90_GET_VAR (NCID,IDVAR,SGQC(1:NPROFS))
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          WRITE(IOUN,*) 'PROFILE_PSAL_QC READ'
    ENDIF

    ALLOCATE(TEMP(NLEVELS,NPROFS),SALC(NLEVELS,NPROFS),DEPTHC(NLEVELS,NPROFS))

    STAT = NF90_INQ_VARID (NCID, 'POTM_CORRECTED', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,TEMP)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'TEMP READ'

    STAT = NF90_INQ_VARID (NCID, 'PSAL_CORRECTED', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,SALC)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'PSAL_CORRECTED READ'

    STAT = NF90_INQ_VARID (NCID, 'DEPH_CORRECTED', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,DEPTHC)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'DEPH_CORRECTED READ'

    IF(LSTRICTPSU) THEN
          STAT = NF90_INQ_VARID (NCID, 'PSAL_CORRECTED_QC', IDVAR)
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          STAT = NF90_GET_VAR (NCID,IDVAR,SQC(1:NPROFS))
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          WRITE(IOUN,*) 'PSAL_CORRECTED_QC READ'
    ENDIF

    IF(LSTRICTTEM) THEN
          STAT = NF90_INQ_VARID (NCID, 'POTM_CORRECTED_QC', IDVAR)
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          STAT = NF90_GET_VAR (NCID,IDVAR,TQC(1:NPROFS))
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          WRITE(IOUN,*) 'POTM_CORRECTED_QC READ'
    ENDIF

    STAT = NF90_CLOSE(NCID)


!========    OBS SELECTION    ========!

    KPROF=0

    CYCLE_PROF : DO JPROF = 1, NPROFS

       LLPROFOK = .FALSE.
       PLANO(JPROF)(1:8) = ADJUSTL(PLANO(JPROF)(1:8))

       !... CHECK TIME
       IF(LLCHECKTIME) THEN
           IF( TIME(JPROF) .LT. DATE_START .OR. &
                TIME(JPROF) .GT. DATE_END   ) THEN
               KERR(15) = KERR(15) + 1
               CYCLE CYCLE_PROF
           ENDIF
       ENDIF

       !... CHECK LATLON
       IF ( LATS(JPROF) .LT. -89.999999 .OR. LATS(JPROF) .GT. 89.999999 .OR. &
          & LONS(JPROF) .LT.-179.999999 .OR. LONS(JPROF) .GT.179.999999 ) THEN
           KERR(1) = KERR(1) + 1
           CYCLE CYCLE_PROF
       ENDIF
       IF ( LLSTRICTPOS ) THEN
           IF( ISNOGOOD( POSQC(JPROF:JPROF) ) ) THEN
               KERR(4) = KERR(4) + 1
               CYCLE CYCLE_PROF
           ENDIF
       ENDIF

       !... CHECK DATE
       IF ( LLSTRICTTIME ) THEN
           IF( ISNOGOOD( TIMQC(JPROF:JPROF) ) ) THEN
               KERR(16) = KERR(16) + 1
               CYCLE CYCLE_PROF
           ENDIF
       ENDIF


!... A.S.: OBS TYPE DEFINITION IS IN ACCORDANCE WITH
!    UKMO SIMPLIFIED VERSION OF WMO 1770 TABLE

       SELECT CASE(INST(JPROF)(1:4))

            CASE(' 401')   ! XBT/MBT
               ITYPE=KKXBT
               KCO(1) = KCO(1) + 1
            CASE(' 741')   ! TESAC
               ITYPE=KKTESAC
               KCO(2) = KCO(2) + 1
            CASE(' 831')   ! ARGO
               ITYPE=KKARGO
               KCO(3) = KCO(3) + 1
            CASE(' 820')   ! BUOYS
               ITYPE=KKBUOY
               KCO(4) = KCO(4) + 1
            CASE DEFAULT
               WRITE(IOUN,*) &
               & 'READEN3FILE : FOUND OBSTYPE = ',TRIM(INST(JPROF)),&
               & ' WHICH IS UNKNOWN!'
               KERR(3) = KERR(3) + 1
               CYCLE CYCLE_PROF
       ENDSELECT


       LPROFT=.TRUE.
       IF(LGLOBTQF) THEN
            IF( ISNOGOOD( TGQC(JPROF:JPROF) ) ) THEN
               KERR(7) = KERR(7) +1
               LPROFT=.FALSE.
            ENDIF
       ENDIF

       LPROFS=.TRUE.
       IF(LGLOBSQF) THEN
            IF( ISNOGOOD( SGQC(JPROF:JPROF) ) ) THEN
               KERR(8) = KERR(8) +1
               LPROFS=.FALSE.
            ENDIF
       ENDIF

       IF(.NOT.LPROFT .AND. .NOT.LPROFS) THEN
            KERR(5) = KERR(5) + 1
            CYCLE CYCLE_PROF
       ENDIF

       KOK(4) = KOK(4) + 1

       CYCLE_LEV : DO JLEV = 1,NLEVELS

          IF(DEPTHC(JLEV,JPROF) .GT. 15000. ) THEN
                 KERR(6) = KERR(6) +1
                 CYCLE CYCLE_LEV
          ENDIF

        !... TEMPERATURE
          IF(LPROFT) THEN
            IF (ISNAN(TEMP(JLEV,JPROF))) CYCLE
            IF (TEMP(JLEV,JPROF).LE. 40._R8 .AND. TEMP(JLEV,JPROF) .GE. -3._R8 ) THEN
                  LOBS=.TRUE.
                  IF(LSTRICTTEM) THEN
                    IF( ISNOGOOD( TQC(JPROF)(JLEV:JLEV) ) ) LOBS=.FALSE.
                  ENDIF
                  IF(LOBS) THEN
                    IF( .NOT. LLPROFOK ) THEN
                       LLPROFOK=.TRUE.
                       KPROF=KPROF+1
                    ENDIF
                    JOBS=JOBS+1
                    IF(JOBS.GT.MAXOBS) &
                    & CALL ABOR1('READEN3FILE : MAXOBS MUST BE INCREASED!')
                    OBSTYPE(JOBS) = ITYPE
                    OBSDEP(JOBS) = DEPTHC(JLEV,JPROF)
                    OBSPAR(JOBS) = KKTEMP
                    OBSVAL(JOBS) = TEMP(JLEV,JPROF)
                    OBSTIME(JOBS) = TIME(JPROF)
                    OBSLON(JOBS) = LONS(JPROF)
                    OBSLAT(JOBS) = LATS(JPROF)
                    OBSPROF(JOBS) = KPROF
                    OBSPLAT(JOBS) = PLANO(JPROF)
                    KOK(1) = KOK(1) + 1
                  ELSE
                    KERR(9) = KERR(9) +1
                  ENDIF
            ELSE
                  KERR(10) = KERR(10) +1
            ENDIF
          ENDIF

        !... SALINITY
          IF(LPROFS) THEN
            IF (ISNAN(SALC(JLEV,JPROF))) CYCLE
             IF (SALC(JLEV,JPROF).LE. 60._R8 .AND. SALC(JLEV,JPROF) .GE. 0._R8 ) THEN
                  LOBS=.TRUE.
                  IF(LSTRICTPSU) THEN
                    IF( ISNOGOOD( SQC(JPROF)(JLEV:JLEV) ) ) LOBS=.FALSE.
                  ENDIF
                  IF(LOBS) THEN
                    IF( .NOT. LLPROFOK ) THEN
                       LLPROFOK=.TRUE.
                       KPROF=KPROF+1
                    ENDIF
                    JOBS=JOBS+1
                    IF(JOBS.GT.MAXOBS) &
                    & CALL ABOR1('READEN3FILE : MAXOBS MUST BE INCREASED!')
                    OBSTYPE(JOBS) = ITYPE
                    OBSDEP(JOBS) = DEPTHC(JLEV,JPROF)
                    OBSPAR(JOBS) = KKSAL
                    OBSVAL(JOBS) = SALC(JLEV,JPROF)
                    OBSTIME(JOBS) = TIME(JPROF)
                    OBSLON(JOBS) = LONS(JPROF)
                    OBSLAT(JOBS) = LATS(JPROF)
                    OBSPROF(JOBS) = KPROF
                    OBSPLAT(JOBS) = PLANO(JPROF)
                    KOK(2) = KOK(2) + 1
                  ELSE
                    KERR(11) = KERR(11) +1
                  ENDIF
            ELSE
                  KERR(12) = KERR(12) +1
            ENDIF
          ENDIF

       ENDDO CYCLE_LEV

    ENDDO CYCLE_PROF

  NTOTOBS=JOBS

  WRITE(IOUN,*)
  WRITE(IOUN,*) '*** REPORT OF READEN3FILE : ',&
  & 'READING OF ENSEMBLES INS OBSERVATIONS'
  WRITE(IOUN,*)
  WRITE(IOUN,*) ' // OPTIONS'
  WRITE(IOUN,*) ' IS TIME OF PROFILE CHECKED ?        : ',LLCHECKTIME
  WRITE(IOUN,*) ' IS QUALITY OF POSITION CHECKED ?    : ',LLSTRICTPOS
  WRITE(IOUN,*) ' IS QUALITY OF TIME     CHECKED ?    : ',LLSTRICTTIME
  WRITE(IOUN,*) ' IS QUALITY OF SALINITY CHECKED ?    : ',LSTRICTPSU
  WRITE(IOUN,*) ' IS QUALITY OF TEMPERAT. USED FOR T ?: ',LSTRICTTEM
  WRITE(IOUN,*) ' IS GLOBAL QUALITY OF T PROF. USED ? : ',LGLOBTQF
  WRITE(IOUN,*) ' IS GLOBAL QUALITY OF S PROF. USED ? : ',LGLOBSQF
  WRITE(IOUN,*)
  WRITE(IOUN,*) ' // DIMENSION'
  WRITE(IOUN,*) ' LEVELS      : ',NLEVELS
  WRITE(IOUN,*) ' PROFILES    : ',NPROFS
  WRITE(IOUN,*)
  WRITE(IOUN,*) ' // OBS PROFILES (WITHOUT UNVALID TIME/POSITION PROFILES)'
  WRITE(IOUN,*) ' XBT/MBT     : ',KCO(1)
  WRITE(IOUN,*) ' TESAC       : ',KCO(2)
  WRITE(IOUN,*) ' ARGO        : ',KCO(3)
  WRITE(IOUN,*) ' BUOYS       : ',KCO(4)
  WRITE(IOUN,*)
  WRITE(IOUN,*) ' // REPORT'
  WRITE(IOUN,*) ' TOTAL OBS STORED                :', NTOTOBS
  WRITE(IOUN,*) ' PROFILES WITH TIME OUT OF WINDOW:', KERR(15)
  WRITE(IOUN,*) ' PROFILES WITH POSITION PROBLEMS :', KERR(1)
  WRITE(IOUN,*) ' PROFILES WITH DATETIME PROBLEMS :', KERR(2)
  WRITE(IOUN,*) ' PROFILES WITH UNKNOWN INSTRUMENT:', KERR(3)
  WRITE(IOUN,*) ' POSITION QUALITY CHECK NEGATIVE :', KERR(4)
  WRITE(IOUN,*) ' TIME     QUALITY CHECK NEGATIVE :', KERR(16)
  WRITE(IOUN,*) ' PROFILE QC NEGATIVE FOR TEMPER. :', KERR(7)
  WRITE(IOUN,*) ' PROFILE QC NEGATIVE FOR SALINITY:', KERR(8)
  WRITE(IOUN,*) ' PROFILE QC NEGATIVE FOR T AND S :', KERR(5)
  WRITE(IOUN,*) ' NOT VALID DEPTHS                :', KERR(6)

  WRITE(IOUN,*) ' OBSERVATION QC NEGATIVE FOR T   :', KERR(9)
  WRITE(IOUN,*) ' NOT VALID VALUE FOR T           :', KERR(10)
  WRITE(IOUN,*) ' OBSERVATION QC NEGATIVE FOR S   :', KERR(11)
  WRITE(IOUN,*) ' NOT VALID VALUE FOR S           :', KERR(12)
  WRITE(IOUN,*) ' OBSERVATION QC NEGATIVE FOR PT  :', KERR(13)
  WRITE(IOUN,*) ' NOT VALID VALUE FOR PT          :', KERR(14)

  WRITE(IOUN,*) ' VALID PROFILES                  :', KOK (4)
  WRITE(IOUN,*) ' EXTRACTED OBS OF SALINITY       :', KOK (2)
  WRITE(IOUN,*) ' EXTRACTED OBS OF TEMPERATURE    :', KOK (1)
  WRITE(IOUN,*) ' EXTRACTED OBS OF POT. TEMPER.   :', KOK (3)
  WRITE(IOUN,*)
  WRITE(IOUN,*) '*** END OF READEN3FILE'
  WRITE(IOUN,*)

    DEALLOCATE(LATS,LONS,TIME,INST,PLANO)
    DEALLOCATE(TEMP,SALC,DEPTHC)

CONTAINS

LOGICAL FUNCTION ISNOGOOD( CHIN )
IMPLICIT NONE
CHARACTER(LEN=1) :: CHIN
ISNOGOOD = .FALSE.
IF( CHIN(1:1) .EQ. '3' .OR. CHIN(1:1) .EQ. '4' ) ISNOGOOD = .TRUE.
END FUNCTION ISNOGOOD

END SUBROUTINE READEN3FILE
