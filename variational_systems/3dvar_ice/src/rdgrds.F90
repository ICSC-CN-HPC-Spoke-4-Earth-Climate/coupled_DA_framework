SUBROUTINE RDGRDS(CGRIDNAME)

!-----------------------------------------------------------------------
!                                                                      !
! READ PARAMETERS FOR THE GRIDS DEFINITION
!                                                                      !
! VERSION 1: S.DOBRICIC 2006                                           !
! THIS ROUTINE WILL HAVE EFFECT ONLY IF COMPILED WITH NETCDF LIBRARY.  !
! VERSION 2: A.STORTO 2009                                             !
! EXTENSION TO GLOBAL GRID.
! VERSION 3: A.STORTO 2011                                             !
! CLEANING
!-----------------------------------------------------------------------

  USE SET_KND
  USE DRV_STR
  USE GRD_STR
  USE NETCDF
  USE IOUNITS, ONLY : IOUNOUT ,IOUNERR ,IOUNLOG
  USE RUN, ONLY : CGRIDPATH, LLVARMDT, NGRID_ID, NCONF, IDOUBLEDOM, LL_DENSLEVS
  USE MYFRTPROF, ONLY : MYFRTPROF_WALL
  USE MPIREL

  IMPLICIT NONE

  CHARACTER(LEN=*),INTENT(IN) :: CGRIDNAME

  INTEGER(I4) :: STAT, NCID, IDVAR,K
  INTEGER(I4) :: JGRID, I, J, JLEV,KK, KSV
  REAL(R8) :: DXMIN,DXMAX,DYMIN,DYMAX

!... INCLUDE GRID DEFINITIONS
#include "grids.h"

CALL MYFRTPROF_WALL('RDGRDS: READ-IN GRID PARAMETERS',0)
NGRID_ID=0
DO JGRID=1, NGRIDS
    IF( TRIM(CGRIDNAME) .EQ. TRIM(CNGRID(JGRID))  )  NGRID_ID=JGRID
ENDDO

IF (NGRID_ID.EQ.0) THEN
    WRITE(*,*)
    WRITE(IOUNERR,*) ' RDGRDS DOES NOT UNDERSTAND THE GRID CONFIGURATION, EXITING'
    WRITE(IOUNLOG,*) ' RDGRDS DOES NOT UNDERSTAND THE GRID CONFIGURATION, EXITING'
    CALL ABOR1('UNRECOGNIZED GRID')
ELSE
    WRITE(IOUNLOG,*)
    WRITE(IOUNLOG,*)
    WRITE(IOUNOUT,'(X,A)') &
    & ' SETTING UP PARAMETERS FOR GRID '//TRIM(CNGRID(NGRID_ID))
    WRITE(IOUNLOG,*) ' -----------------------------------------------------'
    WRITE(IOUNLOG,*) ' :: SETTING UP PARAMETERS FOR GRID '//CNGRID(NGRID_ID)
    WRITE(IOUNLOG,*) ' -----------------------------------------------------'
ENDIF

    WRITE(IOUNOUT,*) ' READING FILE ',TRIM(CGRIDPATH)//TRIM(CNFILE(NGRID_ID))
    STAT = NF90_OPEN(TRIM(CGRIDPATH)//TRIM(CNFILE(NGRID_ID)), NF90_NOWRITE, NCID)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

! GET DIMENSIONS
    STAT = NF90_INQ_DIMID (NCID, 'x', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = GRD%IM)
    GRD%TOTAL_IM=GRD%IM
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQ_DIMID (NCID, 'y', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = GRD%JM)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQ_DIMID (NCID, 'z', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = GRD%KM)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
     IF( LMPION ) THEN
         GRD%I0 = MP_XST(MYPROC)
         GRD%J0 = MP_YST(MYPROC)
         GRD%IM = MP_XEN(MYPROC) - MP_XST(MYPROC) + 1
         GRD%JM = MP_YEN(MYPROC) - MP_YST(MYPROC) + 1
     ELSE
         GRD%I0 = 1
         GRD%J0 = 1
     ENDIF

     WRITE(IOUNLOG,*)
     WRITE(IOUNLOG,'(X,X,A)') 'GRID DIMENSIONS ARE:    NX   NY   NZ'
     WRITE(IOUNLOG,'(X,X,A,3(I5))') '                     ',&
                          & GRD%IM,GRD%JM,GRD%KM
     WRITE(IOUNLOG,'(X,X,A)') 'STARTING INDEXES ARE:   I0   J0   K0'
     WRITE(IOUNLOG,'(X,X,A,3(I5))') '                     ',&
                          & GRD%I0,GRD%J0,1

!  ALLOCATE GRID ARRAYS
     ALLOCATE ( GRD%REG(GRD%IM,GRD%JM))
     ALLOCATE ( GRD%MSK(GRD%IM,GRD%JM,GRD%KM))
     ALLOCATE ( GRD%HGT(GRD%IM,GRD%JM))
     ALLOCATE ( GRD%DISTC(GRD%IM,GRD%JM))
     ALLOCATE ( GRD%F(GRD%IM,GRD%JM))

     ALLOCATE ( PSV(GRD%IM,GRD%JM,PSV3D*GRD%KM+PSV2D) )
     ALLOCATE ( PSV_AD(GRD%IM,GRD%JM,PSV3D*GRD%KM+PSV2D) )
     KSV=0
     IF( LL_TS ) THEN
       WRITE(IOUNLOG,*) ' ALLOCATING TS, FROM ',KSV+1
       GRD%TEM => PSV(1:GRD%IM,1:GRD%JM,KSV+1:KSV+GRD%KM)
       GRD%TEM_AD => PSV_AD(1:GRD%IM,1:GRD%JM,KSV+1:KSV+GRD%KM)
       KSV=KSV+GRD%KM
       GRD%SAL => PSV(1:GRD%IM,1:GRD%JM,KSV+1:KSV+GRD%KM)
       GRD%SAL_AD => PSV_AD(1:GRD%IM,1:GRD%JM,KSV+1:KSV+GRD%KM)
       KSV=KSV+GRD%KM
       WRITE(IOUNLOG,*) '                  TO ',KSV
     ENDIF
     IF( LL_UV ) THEN
       WRITE(IOUNLOG,*) ' ALLOCATING UV, FROM ',KSV+1
       GRD%UVL => PSV(1:GRD%IM,1:GRD%JM,KSV+1:KSV+GRD%KM)
       GRD%UVL_AD => PSV_AD(1:GRD%IM,1:GRD%JM,KSV+1:KSV+GRD%KM)
       KSV=KSV+GRD%KM
       GRD%VVL => PSV(1:GRD%IM,1:GRD%JM,KSV+1:KSV+GRD%KM)
       GRD%VVL_AD => PSV_AD(1:GRD%IM,1:GRD%JM,KSV+1:KSV+GRD%KM)
       KSV=KSV+GRD%KM
       WRITE(IOUNLOG,*) '                  TO ',KSV
     ENDIF
     IF( LL_SSH) THEN
       WRITE(IOUNLOG,*) ' ALLOCATING SSH, FROM ',KSV+1
       GRD%ETA => PSV(1:GRD%IM,1:GRD%JM,KSV+1)
       GRD%ETA_AD => PSV_AD(1:GRD%IM,1:GRD%JM,KSV+1)
       KSV=KSV+1
       WRITE(IOUNLOG,*) '                   TO ',KSV
     ENDIF
     IF( LL_SST) THEN
       WRITE(IOUNLOG,*) ' ALLOCATING SST, FROM ',KSV+1
       GRD%SST => PSV(1:GRD%IM,1:GRD%JM,KSV+1)
       GRD%SST_AD => PSV_AD(1:GRD%IM,1:GRD%JM,KSV+1)
       KSV=KSV+1
       WRITE(IOUNLOG,*) '                   TO ',KSV
     ENDIF
     IF( LL_SSS) THEN
       WRITE(IOUNLOG,*) ' ALLOCATING SSS, FROM ',KSV+1
       GRD%SSS => PSV(1:GRD%IM,1:GRD%JM,KSV+1)
       GRD%SSS_AD => PSV_AD(1:GRD%IM,1:GRD%JM,KSV+1)
       KSV=KSV+1
       WRITE(IOUNLOG,*) '                   TO ',KSV
     ENDIF
     IF( LL_TQ2) THEN
       WRITE(IOUNLOG,*) ' ALLOCATING TQ2, FROM ',KSV+1
       GRD%T2M => PSV(1:GRD%IM,1:GRD%JM,KSV+1)
       GRD%T2M_AD => PSV_AD(1:GRD%IM,1:GRD%JM,KSV+1)
       KSV=KSV+1
       GRD%Q2M => PSV(1:GRD%IM,1:GRD%JM,KSV+1)
       GRD%Q2M_AD => PSV_AD(1:GRD%IM,1:GRD%JM,KSV+1)
       KSV=KSV+1
       WRITE(IOUNLOG,*) '                   TO ',KSV
     ENDIF
     IF( LL_ICE) THEN
       WRITE(IOUNLOG,*) ' ALLOCATING ICE, FROM ',KSV+1
       GRD%SIC => PSV(1:GRD%IM,1:GRD%JM,KSV+1)
       GRD%SIC_AD => PSV_AD(1:GRD%IM,1:GRD%JM,KSV+1)
       KSV=KSV+1
       GRD%SIT => PSV(1:GRD%IM,1:GRD%JM,KSV+1)
       GRD%SIT_AD => PSV_AD(1:GRD%IM,1:GRD%JM,KSV+1)
       KSV=KSV+1
       !GRD%ICU => PSV(1:GRD%IM,1:GRD%JM,KSV+1)
       !GRD%ICU_AD => PSV_AD(1:GRD%IM,1:GRD%JM,KSV+1)
       !KSV=KSV+1
       !GRD%ICV => PSV(1:GRD%IM,1:GRD%JM,KSV+1)
       !GRD%ICV_AD => PSV_AD(1:GRD%IM,1:GRD%JM,KSV+1)
       !KSV=KSV+1
       WRITE(IOUNLOG,*) '                   TO ',KSV
       ALLOCATE ( GRD%SIC_PHYS_AD(GRD%IM,GRD%JM))
       ALLOCATE ( GRD%SIT_PHYS_AD(GRD%IM,GRD%JM))
    ENDIF


     WRITE(IOUNLOG,*) '*** ALLOCATION TARGET AND POINTERS:'
     WRITE(IOUNLOG,*) ' - TOT',PSV3D*GRD%KM+PSV2D

     ALLOCATE ( GRD%B_X(GRD%IM,GRD%JM,GRD%KM), GRD%B_Y(GRD%IM,GRD%JM,GRD%KM))
     ALLOCATE ( GRD%DNS(GRD%IM,GRD%JM,GRD%KM))
     ALLOCATE ( GRD%BX(GRD%IM,GRD%JM), GRD%BY(GRD%IM,GRD%JM))

     ALLOCATE ( GRD%MDT(GRD%IM,GRD%JM), GRD%SLA(GRD%IM,GRD%JM))
     IF(LLVARMDT .OR. NCONF .EQ. 202) ALLOCATE ( GRD%CMDT(GRD%IM,GRD%JM), &
     & GRD%CMDT_AD(GRD%IM,GRD%JM), GRD%CMDT_STDEV(GRD%IM,GRD%JM) )
     ALLOCATE ( GRD%LON(GRD%IM,GRD%JM), GRD%LAT(GRD%IM,GRD%JM), GRD%DEP(GRD%KM))
     ALLOCATE ( GRD%DX(GRD%IM,GRD%JM), GRD%DY(GRD%IM,GRD%JM), GRD%DZ(GRD%KM))
     ALLOCATE ( GRD%DXDY(GRD%IM,GRD%JM))

     ALLOCATE ( GRD%MSR(GRD%IM,GRD%JM,GRD%KM) )

     GRD%DLN = ZRESX(NGRID_ID)
     GRD%DLT = ZRESY(NGRID_ID)

     WRITE(IOUNLOG,*)
     WRITE(IOUNLOG,'(X,X,A)') 'RESOLUTION IS:     DX     DY'
     WRITE(IOUNLOG,'(X,X,A,X,2(F7.4,X))') '              ',GRD%DLN,GRD%DLT

     STAT = NF90_INQ_VARID (NCID, 'lon', IDVAR)
     IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
     STAT = NF90_GET_VAR (NCID,IDVAR,GRD%LON,START=(/GRD%I0,GRD%J0/),COUNT=(/GRD%IM,GRD%JM/))
     IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
     STAT = NF90_INQ_VARID (NCID, 'lat', IDVAR)
     IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
     STAT = NF90_GET_VAR (NCID,IDVAR,GRD%LAT,START=(/GRD%I0,GRD%J0/),COUNT=(/GRD%IM,GRD%JM/))
     IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
     STAT = NF90_INQ_VARID (NCID, 'dx', IDVAR)
     IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
     STAT = NF90_GET_VAR (NCID,IDVAR,GRD%DX,START=(/GRD%I0,GRD%J0/),COUNT=(/GRD%IM,GRD%JM/))
     IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
     STAT = NF90_INQ_VARID (NCID, 'dy', IDVAR)
     IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
     STAT = NF90_GET_VAR (NCID,IDVAR,GRD%DY,START=(/GRD%I0,GRD%J0/),COUNT=(/GRD%IM,GRD%JM/))
     IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
     STAT = NF90_INQ_VARID (NCID, 'dz', IDVAR)
     IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
     STAT = NF90_GET_VAR (NCID,IDVAR,GRD%DZ)
     IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

     STAT = NF90_INQ_VARID (NCID, 'dep', IDVAR)
     IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
     STAT = NF90_GET_VAR (NCID, IDVAR, GRD%DEP)
     IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

     WRITE(IOUNLOG,*)
     WRITE(IOUNLOG,'(X,X,A)') 'VERTICAL LEVELS:  DEPTH(M)'
     DO JLEV=1,GRD%KM
       WRITE(IOUNLOG,'(X,X,A,I3.2,A,X,2F12.6)') '        ',JLEV,') ',GRD%DEP(JLEV),GRD%DZ(JLEV)
     ENDDO

     STAT = NF90_INQ_VARID (NCID, 'topo', IDVAR)
     IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
     STAT = NF90_GET_VAR (NCID,IDVAR,GRD%HGT,START=(/GRD%I0,GRD%J0/),COUNT=(/GRD%IM,GRD%JM/))
     IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

     IF( LL_DENSLEVS ) THEN
        STAT = NF90_INQ_VARID (NCID, 'lsm', IDVAR)
        IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
        STAT = NF90_GET_VAR (NCID,IDVAR,GRD%MSK(:,:,1),START=(/GRD%I0,GRD%J0/),COUNT=(/GRD%IM,GRD%JM/))
        IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
        DO JLEV=2,GRD%KM
           GRD%MSK(:,:,JLEV)=GRD%MSK(:,:,1)
        ENDDO
     ELSE
        STAT = NF90_INQ_VARID (NCID, 'tmsk', IDVAR)
        IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
        STAT = NF90_GET_VAR (NCID,IDVAR,GRD%MSK,START=(/GRD%I0,GRD%J0,1/),COUNT=(/GRD%IM,GRD%JM,GRD%KM/))
        IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
     ENDIF

     STAT = NF90_INQ_VARID (NCID, 'dcoast2d', IDVAR)
     IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
     STAT = NF90_GET_VAR (NCID, IDVAR, GRD%DISTC,START=(/GRD%I0,GRD%J0/),COUNT=(/GRD%IM,GRD%JM/))
     IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

     STAT = NF90_CLOSE(NCID)
     IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

     MIMA_LL(1) = MINVAL( GRD%LON, MASK = GRD%MSK(:,:,1) .GT. 0.5 )
     MIMA_LL(2) = MAXVAL( GRD%LON, MASK = GRD%MSK(:,:,1) .GT. 0.5 )
     MIMA_LL(3) = MINVAL( GRD%LAT, MASK = GRD%MSK(:,:,1) .GT. 0.5 )
     MIMA_LL(4) = MAXVAL( GRD%LAT, MASK = GRD%MSK(:,:,1) .GT. 0.5 )

     WRITE(IOUNLOG,*)
     WRITE(IOUNLOG,*) ' MIN/MAX LONGITUDE  :', MIMA_LL(1:2)
     WRITE(IOUNLOG,*) ' MIN/MAX LATITUDE   :', MIMA_LL(3:4)
     WRITE(IOUNLOG,*)

     IF( ABS(IDOUBLEDOM) .EQ. 1 ) THEN

        WRITE(IOUNLOG,*) ' READING FILE MULTIGRID.nc'

        STAT = NF90_OPEN('MULTIGRID.nc', NF90_NOWRITE, NCID)
        IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
        STAT = NF90_INQ_VARID (NCID, 'dx', IDVAR)
        IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
        STAT = NF90_GET_VAR (NCID, IDVAR, GRD%DX,START=(/GRD%I0,GRD%J0/),COUNT=(/GRD%IM,GRD%JM/))
        IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
        STAT = NF90_INQ_VARID (NCID, 'dy', IDVAR)
        IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
        STAT = NF90_GET_VAR (NCID, IDVAR, GRD%DY,START=(/GRD%I0,GRD%J0/),COUNT=(/GRD%IM,GRD%JM/))
        IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
        STAT = NF90_INQ_VARID (NCID, 'tmsk', IDVAR)
        IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
        STAT = NF90_GET_VAR (NCID,IDVAR,GRD%MSK,START=(/GRD%I0,GRD%J0,1/),COUNT=(/GRD%IM,GRD%JM,GRD%KM/))
        IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

        STAT = NF90_CLOSE(NCID)
        IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

     ENDIF

     GRD%DXDY =  GRD%DY * GRD%DX

! DEFINE GRID FOR HORIZONTAL COVARIANCES

    WRITE(IOUNLOG,*) 'MASK FOR HORCOV IS ',DRV%MASK(DRV%KTR)
    IF( DRV%MASK(DRV%KTR).EQ.1 .OR. DRV%MASK(DRV%KTR).EQ.-1)THEN
        GRD%MSR(:,:,:) = 1.0_R8
    ELSE IF( DRV%MASK(DRV%KTR).EQ.2 .OR. DRV%MASK(DRV%KTR).EQ.-2)THEN
       DO K=1,GRD%KM
        GRD%MSR(:,:,K) = GRD%MSK(:,:,1)
       ENDDO
    ELSE IF( DRV%MASK(DRV%KTR).EQ.3 .OR. DRV%MASK(DRV%KTR).EQ.-3)THEN
        GRD%MSR(:,:,:) = GRD%MSK(:,:,:)
    ELSE
        !... FOR EXTERNAL UNINITIALISED USE...
        WRITE(IOUNERR,*) ' MASK OPTION',DRV%MASK(DRV%KTR),' NOT VALID !!!'
        WRITE(IOUNOUT,*) ' MASK OPTION',DRV%MASK(DRV%KTR),' NOT VALID !!!'
        WRITE(IOUNOUT,*)
        WRITE(IOUNOUT,*) ' *** MSR IS DISABLED '
        WRITE(IOUNOUT,*)
        GRD%MSR(:,:,:) = 1.0_R8
    ENDIF


    WRITE(IOUNLOG,*)
    WRITE(IOUNLOG,*) ' MASQUERING DX AND DY'
    WRITE(IOUNLOG,*)

    DXMIN = MINVAL(GRD%DX)
    DXMAX = MAXVAL(GRD%DX)
    DYMIN = MINVAL(GRD%DY)
    DYMAX = MAXVAL(GRD%DY)

    WRITE(IOUNLOG,*) ' DX(MIN,MAX) -- DY(MIN,MAX) :: ',&
    & DXMIN,DXMAX,DYMIN,DYMAX

    DXMIN = MINVAL(GRD%DX,MASK=(INT(GRD%MSK(:,:,1)).EQ.1))
    DXMAX = MAXVAL(GRD%DX,MASK=(INT(GRD%MSK(:,:,1)).EQ.1))
    DYMIN = MINVAL(GRD%DY,MASK=(INT(GRD%MSK(:,:,1)).EQ.1))
    DYMAX = MAXVAL(GRD%DY,MASK=(INT(GRD%MSK(:,:,1)).EQ.1))

    WRITE(IOUNLOG,*) ' BY MASQUERING'
    WRITE(IOUNLOG,*) ' DX(MIN,MAX) -- DY(MIN,MAX) :: ',&
    & DXMIN,DXMAX,DYMIN,DYMAX

    WHERE(GRD%DX.LT.DXMIN) GRD%DX=DXMIN
    WHERE(GRD%DY.LT.DYMIN) GRD%DY=DYMIN
    WHERE(GRD%DX.GT.DXMAX) GRD%DX=DXMAX
    WHERE(GRD%DY.GT.DYMAX) GRD%DY=DYMAX

    DXMIN = MINVAL(GRD%DX)
    DXMAX = MAXVAL(GRD%DX)
    DYMIN = MINVAL(GRD%DY)
    DYMAX = MAXVAL(GRD%DY)

    WRITE(IOUNLOG,*) ' AFTER MASQUERING'
    WRITE(IOUNLOG,*) ' DX(MIN,MAX) -- DY(MIN,MAX) :: ',&
    & DXMIN,DXMAX,DYMIN,DYMAX

    GRD%DXDY =  GRD%DY * GRD%DX

    GRD%ADXDY = SUM(GRD%DXDY) / (GRD%IM * GRD%JM)
    WRITE(IOUNLOG,*)
    WRITE(IOUNLOG,'(X,X,A,E14.07)') 'AVERAGE NODE DXDY (KM): ', SQRT(GRD%ADXDY)/1000.
    WRITE(IOUNLOG,'(X,X,A)') '(MEANINGLESS ON GLOBAL STRETCHED DOMAINS)'

      GRD%DXDY(:,:) = SQRT(GRD%DXDY(:,:))
      GRD%ADXDY = SQRT(GRD%ADXDY)

    GRD%NPS = GRD%IM*GRD%JM*GRD%KM
    WRITE(IOUNLOG,*)
    WRITE(IOUNLOG,'(X,X,A,I10)') 'TOTAL NO OF NODES:', GRD%NPS

! MDT ------------------------------------------------------------

    IF (LLMDT(NGRID_ID)) THEN

       STAT = NF90_OPEN(TRIM(CGRIDPATH)//TRIM(CNFILEMDT(NGRID_ID)),NF90_NOWRITE,NCID)
       IF(STAT.NE.0) THEN
         WRITE(*,*) 'PROBLEMS OPENING '//TRIM(CNFILEMDT(NGRID_ID))//' INDEX GRID=',NGRID_ID

         CALL ABOR1('NETCDF FILE NOT FOUND')
       ENDIF

       STAT = NF90_INQ_VARID (NCID, 'mdt', IDVAR)
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
       STAT = NF90_GET_VAR (NCID, IDVAR, GRD%MDT,START=(/GRD%I0,GRD%J0,1/),COUNT=(/GRD%IM,GRD%JM,GRD%KM/))
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

       STAT = NF90_CLOSE(NCID)

    ELSE

       GRD%MDT(:,:) = -999._R8

    ENDIF

     DO J= 1,GRD%JM
      DO I= 1,GRD%IM
       GRD%F(I,J) = 0.00014584_R8 *                &
        SIN((GRD%LAT(1,1)+(J-1._R8+0.5)*(GRD%LAT(2,1)-GRD%LAT(1,1)))*&
        & 3.141592654_R8/180._R8)
      ENDDO
     ENDDO

    WRITE(IOUNOUT,'(X,A)') ' DONE'

CALL MYFRTPROF_WALL('RDGRDS: READ-IN GRID PARAMETERS',1)
RETURN
END SUBROUTINE RDGRDS
