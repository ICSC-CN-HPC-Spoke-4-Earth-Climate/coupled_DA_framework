SUBROUTINE CLIMCHECK

!.. PERFORM QUALITY CHECK AGAINST CLIMATOLOGY
!.. AND REJECT OBSERVATIONS ACCORDINGLY
!..
!.. ANDREA STORTO *CMCC* 2011-12-30

USE SET_KND
USE OBSDEF
USE OBS_STR
USE GRD_STR
USE IOUNITS, ONLY : IOUNLOG
USE RUN, ONLY : IANMM, IANDD
USE OBSHANDLING 
USE MYFRTPROF, ONLY : MYFRTPROF_WALL

IMPLICIT NONE

REAL(R8) :: CLIMVAL
INTEGER (I4) :: JOBS,I,J,D,I2,KP
INTEGER (I4) :: XIND1, KREJ(4), KREJI(2)
REAL(R8), ALLOCATABLE, TARGET :: BGTOT(:,:,:,:)
REAL(R8), POINTER :: BGS(:,:,:), BGT(:,:,:)

#include "obs_events.h"

CALL MYFRTPROF_WALL('CLIMCHECK: CLIMATOLOGY QUALITY CHECK',0)

IF( COUNT( (/LLCLCK_INS,LLCLCK_SST,LLCLCK_SSS/) ) .EQ. 0 ) THEN
   CALL MYFRTPROF_WALL('CLIMCHECK: CLIMATOLOGY QUALITY CHECK',1)
   RETURN
ENDIF

ALLOCATE ( BGTOT(GRD%IM,GRD%JM,GRD%KM,2) )
BGS => BGTOT(1:GRD%IM,1:GRD%JM,1:GRD%KM,1)
BGT => BGTOT(1:GRD%IM,1:GRD%JM,1:GRD%KM,2)

CALL READ_CLIM(IANMM*100+IANDD,GRD%IM,GRD%JM,GRD%KM,BGT,BGS)

KREJ=0
KREJI=0

!... INS PART

IF( LLCLCK_INS ) THEN

IF( INS%NO .EQ. 0 ) THEN
KREJ(2) = 0
KREJI(1) = 0
KREJI(2) = 0
ELSE

#ifdef SHARED_MEMORY
!$OMP PARALLEL DEFAULT(SHARED), PRIVATE(JOBS,D,KP,CLIMVAL)
!$OMP DO SCHEDULE(DYNAMIC,1)
#endif
LOOPINS : DO JOBS=1,INS%NO

     IF(INS%FLC(JOBS) .EQ. 0 ) CYCLE LOOPINS
     IF(LL_TQ2_ASSIM .AND. &
     & (INS%PAR(JOBS).EQ.KKT2M.OR.INS%PAR(JOBS).EQ.KKQ2M)) CYCLE LOOPINS

     D=INS%KB(JOBS)
     KP=INS%PAR(JOBS)

     CLIMVAL = &
   & OSUM( INS%PQ(JOBS,1:NPQ),BGTOT(INS%IB(JOBS,1:NPQ),INS%JB(JOBS,1:NPQ),D,KP) )+ &
   & OSUM( INS%PQ(JOBS,NPQ+1:2*NPQ),BGTOT(INS%IB(JOBS,1:NPQ),INS%JB(JOBS,1:NPQ),D+1,KP) )

     IF(ABS(CLIMVAL-INS%VAL(JOBS)) .GT. CLIMLIM(KP) ) THEN
        INS%FLC(JOBS) = 0
        INS%EVE(JOBS) = KEVE_CLIM
     ENDIF

ENDDO LOOPINS
#ifdef SHARED_MEMORY
!$OMP END DO
!$OMP END PARALLEL
#endif

KREJ(2) = COUNT( INS%EVE(1:INS%NO) .EQ. KEVE_CLIM )
KREJI(1) = COUNT( INS%EVE(1:INS%NO).EQ. KEVE_CLIM .AND. INS%PAR(1:INS%NO).EQ.1)
KREJI(2) = COUNT( INS%EVE(1:INS%NO).EQ. KEVE_CLIM .AND. INS%PAR(1:INS%NO).EQ.2)

ENDIF
ENDIF

!... SST PART

IF( LLCLCK_SST ) THEN

IF( SST%NO .EQ. 0 ) THEN
KREJ(3) = 0
ELSE

#ifdef SHARED_MEMORY
!$OMP PARALLEL DEFAULT(SHARED), PRIVATE(JOBS,CLIMVAL)
!$OMP DO SCHEDULE(DYNAMIC,1)
#endif
LOOPSST : DO JOBS=1,SST%NO

     IF(SST%FLC(JOBS) .EQ. 0 ) CYCLE LOOPSST

     CLIMVAL = &
     & OSUM( SST%PQ(JOBS,1:NPQ),BGTOT(SST%IB(JOBS,1:NPQ),SST%JB(JOBS,1:NPQ),1,2) )

     IF(ABS(CLIMVAL-SST%VAL(JOBS)) .GT. CLIMLIM(2) ) THEN
        SST%FLC(JOBS) = 0
        SST%EVE(JOBS) = KEVE_CLIM
     ENDIF

ENDDO LOOPSST
#ifdef SHARED_MEMORY
!$OMP END DO
!$OMP END PARALLEL
#endif

KREJ(3) = COUNT( SST%EVE(1:SST%NO) .EQ. KEVE_CLIM )

ENDIF
ENDIF

!... SSS PART

IF( LLCLCK_SSS ) THEN

IF( SSS%NO .EQ. 0 ) THEN
KREJ(4) = 0
ELSE

#ifdef SHARED_MEMORY
!$OMP PARALLEL DEFAULT(SHARED), PRIVATE(JOBS,CLIMVAL)
!$OMP DO SCHEDULE(DYNAMIC,1)
#endif
LOOPSSS : DO JOBS=1,SSS%NO

     IF(SSS%FLC(JOBS) .EQ. 0 ) CYCLE LOOPSSS

     CLIMVAL = &
     & OSUM( SSS%PQ(JOBS,1:NPQ),BGTOT(SSS%IB(JOBS,1:NPQ),SSS%JB(JOBS,1:NPQ),1,1) )

     IF(ABS(CLIMVAL-SSS%VAL(JOBS)) .GT. CLIMLIM(1) ) THEN
        SSS%FLC(JOBS) = 0
        SSS%EVE(JOBS) = KEVE_CLIM
     ENDIF

ENDDO LOOPSSS
#ifdef SHARED_MEMORY
!$OMP END DO
!$OMP END PARALLEL
#endif

KREJ(4) = COUNT( SSS%EVE(1:SSS%NO) .EQ. KEVE_CLIM )

ENDIF
ENDIF

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' *** CLIMATOLOGY CHECK'
WRITE(IOUNLOG,*) ' SLA REJECTED:',KREJ(1)
WRITE(IOUNLOG,*) ' INS REJECTED:',KREJ(2)
WRITE(IOUNLOG,*) '               SALIN ',KREJI(1)
WRITE(IOUNLOG,*) '               TEMPE ',KREJI(2)
WRITE(IOUNLOG,*) ' SST REJECTED:',KREJ(3)
WRITE(IOUNLOG,*) ' SSS REJECTED:',KREJ(4)
WRITE(IOUNLOG,*)

DEALLOCATE ( BGTOT )

CALL MYFRTPROF_WALL('CLIMCHECK: CLIMATOLOGY QUALITY CHECK',1)
END SUBROUTINE CLIMCHECK
