SUBROUTINE VER_HOR_AD

!-----------------------------------------------------------------------
!                                                                      !
! APPLY VERTICAL EOFS AND HORIZONTAL FILTER - ADJOINT                  !
!                                                                      !
! VERSION 1: S.DOBRICIC 2006                                           !
! VERSION 2: S.DOBRICIC 2007                                           !
!     SYMMETRIC CALCULATION IN PRESENCE OF COASTAL BOUNDARIES          !
!     ETA, TEM, AND SAL ARE HERE TEMPORARY ARRAYS                      !
!-----------------------------------------------------------------------


 USE SET_KND
 USE GRD_STR
 USE EOF_STR
 USE DRV_STR
 USE CTL_STR
 USE RECFILTER, ONLY : SCXNS,SCYNS,FCT,LLTSAPART,LLRFSR,SCXNZ,SCYNZ,NTOTV,LL_NOSRF_RF
 USE RUN, ONLY : LLVARMDT
 USE MYFRTPROF, ONLY : MYFRTPROF_WALL
 USE DEBUGTMP
 USE LBCLNK
 USE BAL_AIRSEA
 USE HYBRID
 USE EOFS3D

 IMPLICIT NONE

 INTEGER(I4)    :: K, NTOTV2
 CHARACTER(LEN=20) :: CFILE

 CALL MYFRTPROF_WALL('VER_HOR_AD: ADJ OF CONTROL TO PHYSICAL SPACE',0)

    DT_CSTRIN='START VER_HOR_AD' ; IF( LL_DEBUGTMP ) CALL CALLDT

 IF( LL_NOSRF_RF ) THEN
    NTOTV2 = 2*GRD%KM*COUNT( (/LL_TS,LL_UV/) )
 ELSE
    NTOTV2 = NTOTV
 ENDIF

! ---
! DIVERGENCE DAMPING
    IF(DRV%DDA(DRV%KTR) .EQ. 1 .AND. LL_UV) &
       CALL DIV_DMP_AD

! ---
! VELOCITY OPERATOR
    IF(LL_UV .AND. (DRV%BMD(DRV%KTR)+DRV%BAL(DRV%KTR)) .GT. 0) &
       CALL GET_VEL_AD

! ---
! BAROTROPIC MODEL
    IF(DRV%BMD(DRV%KTR) .EQ. 1) &
       CALL BAR_MOD_AD

! ---
! BOUYANCY FORCE
    IF(DRV%BMD(DRV%KTR) .EQ. 1) &
       CALL GET_BYG_AD

! ---
! SIMPLIFIED BALANCE AT 2M
    IF(DRV%BA2(DRV%KTR) .EQ. 1) &
       CALL BAL2M_AD

! ---
! SIMPLIFIED BALANCE
    IF(DRV%BAL(DRV%KTR) .EQ. 1) &
       CALL BALANCE_AD

   IF ( LL_EOFS3D ) CALL EOF3D_AD

    IF(LL_HYBRID_CR) THEN
        PSV_AD  = ZHYB_A*PSV_AD
        PSVF_AD = ZHYB_B*PSV_AD + PSVF_AD
    ENDIF

! ---
! SCALE FOR BOUNDARIES
    IF(LLVARMDT) GRD%CMDT_AD = GRD%CMDT_AD * FCT(:,:,1)

   PSV_AD = PSV_AD * FCT

   DT_CSTRIN='AFTER RESCALING AD' ; IF( LL_DEBUGTMP ) CALL CALLDT

 IF(DRV%MASK(DRV%KTR) .GT. 1) THEN

! ---
! LOAD TEMPORARY ARRAYS
    IF(LLVARMDT) GRD%CMDT = GRD%CMDT_AD

    PSV = PSV_AD

! ---
! X DIRECTION
    IF(LLVARMDT) CALL RCFL_XZ( GRD%IM, GRD%JM, GRD%CMDT)

    CALL RCFL_X( GRD%IM, GRD%JM, NTOTV2, PSV )
    IF( LL_LBCLNK ) CALL LBC_LNK( PSV )

   DT_CSTRIN='AFTER RCFL_X' ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! SCALE BY THE SCALING FACTOR
    IF(LLVARMDT) GRD%CMDT = GRD%CMDT * SCXNZ

    PSV= PSV*SCXNS

    DT_CSTRIN='AFTER X SCAL' ; IF( LL_DEBUGTMP ) CALL CALLDT


! ---
! Y DIRECTION
    IF(LLVARMDT) CALL RCFL_YZ(GRD%IM, GRD%JM,GRD%CMDT)

    CALL RCFL_Y( GRD%IM,GRD%JM,NTOTV2,PSV )

    DT_CSTRIN='AFTER RCFL_Y' ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! SCALE BY THE SCALING FACTOR
    IF(LLVARMDT) GRD%CMDT = GRD%CMDT * SCYNZ

    PSV = PSV * SCYNS

    DT_CSTRIN='AFTER Y SCAL' ; IF( LL_DEBUGTMP ) CALL CALLDT

 ENDIF

! ---
! SCALE BY THE SCALING FACTOR
    IF(LLVARMDT) GRD%CMDT_AD = GRD%CMDT_AD * SCYNZ

    PSV_AD = PSV_AD * SCYNS

    DT_CSTRIN='AFTER Y SCALE' ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! Y DIRECTION
    IF(LLVARMDT) CALL RCFL_YZ_AD( GRD%IM, GRD%JM,GRD%CMDT_AD)

    CALL RCFL_Y_AD( GRD%IM,GRD%JM,NTOTV2,PSV_AD )
    DT_CSTRIN='AFTER RCFL_Y_AD';IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! SCALE BY THE SCALING FACTOR
    IF(LLVARMDT) GRD%CMDT_AD = GRD%CMDT_AD * SCXNZ

    PSV_AD = PSV_AD*SCXNS

    DT_CSTRIN='AFTER X SCALE';IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! X DIRECTION
    IF(LLVARMDT) CALL RCFL_XZ_AD( GRD%IM, GRD%JM,GRD%CMDT_AD)

    IF( LL_LBCLNK ) CALL LBC_LNK( PSV_AD )
    CALL RCFL_X_AD(GRD%IM,GRD%JM,NTOTV2 ,PSV_AD )
    DT_CSTRIN='AFTER RCFL_Y_AD';IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! AVERAGE
 IF(DRV%MASK(DRV%KTR) .GT. 1) THEN
    IF(LLVARMDT) GRD%CMDT_AD = (GRD%CMDT_AD + GRD%CMDT) * 0.5_R8
    PSV_AD = (PSV_AD + PSV) * 0.5_R8
 ENDIF

    DT_CSTRIN='AFTER AVERAGE';IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! VERTICAL EOFS
    CALL VEOF_AD

    DT_CSTRIN='AFTER VEOF_AD'; IF( LL_DEBUGTMP ) CALL CALLDT

 CALL MYFRTPROF_WALL('VER_HOR_AD: ADJ OF CONTROL TO PHYSICAL SPACE',1)
END SUBROUTINE VER_HOR_AD
