SUBROUTINE READOBS103

!.. READ OBS IN BINARY OR NETCDF FORMAT

USE SET_KND
USE RUN , ONLY : NSUBDOMAINS, IO_OBSFORMAT, LL_ONEPROC103
USE MYFRTPROF, ONLY : MYFRTPROF_WALL
USE OBSDEF
USE OBS_STR
USE GRD_STR
USE ALLOCOBS
USE EXTGRID
USE NETCDF
USE STATS

IMPLICIT NONE

INTEGER(I4) :: NPROCS_TMP, JP
INTEGER(I4),ALLOCATABLE :: KKCNT(:,:)
INTEGER(I4) :: IAUX,IST,IEN,NMAXOBS,JO,NOINT,KOBS,JSAT,K
INTEGER(I4) :: NCID, DIMID, VARID, JOBS, MAXINS, MAXSLA
CHARACTER(LEN=30) :: CFILE
REAL(R8) :: ZICO
TYPE (INS_T) :: INSB

#include "io_obs.h"

CALL MYFRTPROF_WALL('READOBS103: READ BINARY OBS FILES',0)

ZICO = 0._R8

WRITE(IOUNLOG,*) ' MULTIPLICATIVE COEFFICIENT FOR SAFE EXTENSION'
WRITE(IOUNLOG,*) ' OF OBSERVATIONAL ARRAYS IS ',ZICO

NPROCS_TMP = NSUBDOMAINS
IF ( LL_ONEPROC103 ) NPROCS_TMP = 1

CALL SETFGFILES(NPROCS_TMP)
CALL SETFGFLAYOUT

!.. GET OBS VECT INDEX

ALLOCATE(KKCNT(NPROCS_TMP,NOFAMS))

OPEN(913,FILE='OBS_TAB.DAT',STATUS='OLD')
DO JP=1,NPROCS_TMP
    READ(913,*) IAUX,KKCNT(JP,NNINS),KKCNT(JP,NNSLA),&
                   & KKCNT(JP,NNSST),KKCNT(JP,NNSSS)
ENDDO
CLOSE(913)

INS%NO = SUM(KKCNT(:,NNINS))
IF( OBS%ARG + OBS%XBT + OBS%GLD .EQ. 0 ) THEN
    INS%NO = 0
    KKCNT(:,NNINS) = 0
ENDIF
INS%NC = INS%NO

IF(INS%NO.GT.0) THEN

  CALL ALLOCINS(INS%NO+INT(INS%NO*ZICO),.TRUE.)
  MAXINS = MAXVAL(KKCNT(:,NNINS))

  WRITE(IOUNLOG,*) ' ** ALLOCATING AUXILIARY INSITU OBS VECTOR **'
  WRITE(IOUNLOG,*) '    NUMBER OF OBS IS  ',MAXINS

  ALLOCATE(INSB%INO(MAXINS),INSB%FLG(MAXINS),INSB%FLC(MAXINS),INSB%PAR(MAXINS))
  ALLOCATE(INSB%LON(MAXINS),INSB%LAT(MAXINS),INSB%DPT(MAXINS),INSB%TIM(MAXINS))
  ALLOCATE(INSB%VAL(MAXINS),INSB%BAC(MAXINS),INSB%INC(MAXINS),INSB%TDIST(MAXINS))
  ALLOCATE(INSB%BIA(MAXINS),INSB%ERR(MAXINS),INSB%EVE(MAXINS),INSB%KTY(MAXINS))
  ALLOCATE(INSB%RES(MAXINS),INSB%B_A(MAXINS),INSB%OTYPE(MAXINS),INSB%PROF(MAXINS))
  ALLOCATE(INSB%IB(MAXINS,NPQ),INSB%JB(MAXINS,NPQ),INSB%KB(MAXINS),INSB%PLNO(MAXINS))
  ALLOCATE(INSB%RB(MAXINS))
  ALLOCATE(INSB%PQ(MAXINS,2*NPQ))
  ALLOCATE(INSB%PRIND(MAXINS),INSB%INST(MAXINS))
  ALLOCATE(INSB%NIND(MAXINS))

  INS%FLC = 0
  INS%FLG = 0

ENDIF

SLA%NO=SUM(KKCNT(:,NNSLA))
IF( OBS%SLA .EQ. 0 ) THEN
    SLA%NO = 0
    KKCNT(:,NNSLA) = 0
ENDIF
SLA%NC=SLA%NO

IF(SLA%NO.GT.0) THEN
   
  CALL ALLOCSLA(SLA%NO+INT(SLA%NO*ZICO),.TRUE.)

  SLA%FLC = 0
  SLA%FLG = 0

ENDIF

SST%NO=SUM(KKCNT(:,NNSST))
IF( OBS%SST .EQ. 0 ) THEN
    SST%NO = 0
    KKCNT(:,NNSST) = 0
ENDIF
SST%NC=SST%NO

IF(SST%NO.GT.0) &
CALL ALLOCSST(SST%NO+INT(SST%NO*ZICO),.TRUE.)

SSS%NO=SUM(KKCNT(:,NNSSS))
IF( OBS%SSS .EQ. 0 ) THEN
    SSS%NO = 0
    KKCNT(:,NNSSS) = 0
ENDIF
SSS%NC=SSS%NO

IF(SSS%NO.GT.0) &
CALL ALLOCSSS(SSS%NO+INT(SSS%NO*ZICO),.TRUE.)

CALL FLUSH(IOUNLOG)

DO JP=1,NPROCS_TMP

   IST=1
   IEN=KKCNT(JP,NNINS)

   WRITE(IOUNLOG,*) ' JP, KKCNT : ',JP,IEN
   CALL FLUSH(IOUNLOG)

   IF(KKCNT(JP,NNINS) .GT. 0 ) THEN

      WRITE(CFILE,'(A,I4.4,A)') 'INSMIS_',JP-1,'.NC'

      WRITE(IOUNLOG,*) ' FETCHING OBS IN FILE ',TRIM(CFILE)
      CALL FLUSH(IOUNLOG)

      CALL IO_OBS(CFILE=CFILE,IOPT=2,NOBS=KKCNT(JP,NNINS),&
      NLEVS=GRD%KM, NPREDS=1,NPQ=NPQ,NPQ2=2*NPQ,&
      FLC=INSB%FLC(IST:IEN),&
      NIND=INSB%NIND(IST:IEN),&
      INO=INSB%INO(IST:IEN),&
      OTYPE=INSB%OTYPE(IST:IEN),&
      PAR=INSB%PAR(IST:IEN),&
      PLNO=INSB%PLNO(IST:IEN),&
      INST=INSB%INST(IST:IEN),&
      EVE=INSB%EVE(IST:IEN),&
      KTY=INSB%KTY(IST:IEN),&
      PRIND=INSB%PRIND(IST:IEN),&
      PROF=INSB%PROF(IST:IEN),&
      TDIST=INS%TDIST(IST:IEN),&
      LON=INSB%LON(IST:IEN),&
      LAT=INSB%LAT(IST:IEN),&
      DPT=INSB%DPT(IST:IEN),&
      TIM=INSB%TIM(IST:IEN),&
      VAL=INSB%VAL(IST:IEN),&
      BIA=INSB%BIA(IST:IEN),&
      IB=INSB%IB(IST:IEN,:),&
      JB=INSB%JB(IST:IEN,:),&
      KB=INSB%KB(IST:IEN),&
      RB=INSB%RB(IST:IEN),&
      PQ=INSB%PQ(IST:IEN,:),&
      BAC=INSB%BAC(IST:IEN),&
      RES=INSB%RES(IST:IEN) )

     DO JOBS=1,KKCNT(JP,NNINS)
        KOBS = INSB%NIND(JOBS)
        INS%FLG( KOBS )= 1
        INS%FLC( KOBS )=INSB%FLC(JOBS)
        INS%INO( KOBS )=INSB%INO(JOBS)
        INS%OTYPE( KOBS )=INSB%OTYPE(JOBS)
        INS%PAR( KOBS )=INSB%PAR(JOBS)
        INS%PLNO( KOBS )=INSB%PLNO(JOBS)
        INS%INST( KOBS )=INSB%INST(JOBS)
        INS%EVE( KOBS )=INSB%EVE(JOBS)
        INS%KTY( KOBS )=INSB%KTY(JOBS)
        INS%PROF( KOBS )=INSB%PROF(JOBS)
        INS%TDIST( KOBS )=INS%TDIST(JOBS)
        INS%LON( KOBS )=INSB%LON(JOBS)
        INS%LAT( KOBS )=INSB%LAT(JOBS)
        INS%DPT( KOBS )=INSB%DPT(JOBS)
        INS%TIM( KOBS )=INSB%TIM(JOBS)
        INS%VAL( KOBS )=INSB%VAL(JOBS)
        INS%BIA( KOBS )=INSB%BIA(JOBS)
        INS%IB( KOBS,: )=INSB%IB(JOBS,:)
        INS%JB( KOBS,: )=INSB%JB(JOBS,:)
        INS%KB( KOBS )=INSB%KB(JOBS)
        INS%RB( KOBS )=INSB%RB(JOBS)
        INS%PQ( KOBS, : )=INSB%PQ(JOBS,:)
        INS%BAC( KOBS )=INSB%BAC(JOBS)
        INS%RES( KOBS )=INSB%RES(JOBS)
     ENDDO

   ENDIF

   WRITE(IOUNLOG,*) ' INS READ'
   CALL FLUSH(IOUNLOG)

   IF(JP==1) THEN
      IST=1
   ELSE
      IST=SUM(KKCNT(1:JP-1,NNSLA))+1
   ENDIF
   IEN=SUM(KKCNT(1:JP,NNSLA))

   IF(KKCNT(JP,NNSLA) .GT. 0 ) THEN

      WRITE(CFILE,'(A,I4.4,A)') 'SLAMIS_',JP-1,'.NC'

      WRITE(IOUNLOG,*) ' SLA CHECK FOR PROCESSOR ',JP
      WRITE(IOUNLOG,*) ' ',ALLOCATED(SLA%TB)
      IF( ALLOCATED(SLA%TB) ) WRITE(IOUNLOG,*) ' DIMS:',SIZE(SLA%TB,1),SIZE(SLA%TB,2)
      WRITE(IOUNLOG,*) ' IST, IEN = ',IST,IEN,IEN-IST+1
      CALL FLUSH(IOUNLOG)

      CALL IO_OBS(CFILE=CFILE,IOPT=2,&
      & NOBS=KKCNT(JP,NNSLA),NLEVS=GRD%KM,NPREDS=NPRD_SLA,NPQ=NPQ,NPQ2=NPQ,&
      FLC=SLA%FLC(IST:IEN),&
      NIND=SLA%NIND(IST:IEN),&
      INO=SLA%INO(IST:IEN),&
      TRACK=SLA%TRACK(IST:IEN),&
      EVE=SLA%EVE(IST:IEN),&
      KSAT=SLA%KSAT(IST:IEN),&
      BOT=SLA%BOT(IST:IEN),&
      TDIST=SLA%TDIST(IST:IEN),&
      LON=SLA%LON(IST:IEN),&
      LAT=SLA%LAT(IST:IEN),&
      TIM=SLA%TIM(IST:IEN),&
      VAL=SLA%VAL(IST:IEN),&
      BIA=SLA%BIA(IST:IEN),&
      IB=SLA%IB(IST:IEN,:),&
      JB=SLA%JB(IST:IEN,:),&
      PQ=SLA%PQ(IST:IEN,:),&
      DPT=SLA%DPT(IST:IEN),&
      BAC=SLA%BAC(IST:IEN),&
      TB=SLA%TB(IST:IEN,1:GRD%KM),&
      SB=SLA%SB(IST:IEN,1:GRD%KM),&
      BCP=SLA%BCP(IST:IEN,1:NPRD_SLA),&
      RES=SLA%RES(IST:IEN) )

   ENDIF

   IF(JP==1) THEN
      IST=1
   ELSE
      IST=SUM(KKCNT(1:JP-1,NNSST))+1
   ENDIF
   IEN=SUM(KKCNT(1:JP,NNSST))

   IF(KKCNT(JP,NNSST) .GT. 0 ) THEN

      WRITE(CFILE,'(A,I4.4,A)') 'SSTMIS_',JP-1,'.NC'

      CALL IO_OBS(CFILE=CFILE,IOPT=2,NOBS=KKCNT(JP,NNSST),&
      NLEVS=GRD%KM, NPREDS=NPRD_SST,NPQ=NPQ,NPQ2=NPQ,&
      FLC=SST%FLC(IST:IEN),&
      TRACK=SST%TRACK(IST:IEN),&
      EVE=SST%EVE(IST:IEN),&
      KSAT=SST%KSAT(IST:IEN),&
      TDIST=SST%TDIST(IST:IEN),&
      LON=SST%LON(IST:IEN),&
      LAT=SST%LAT(IST:IEN),&
      TIM=SST%TIM(IST:IEN),&
      VAL=SST%VAL(IST:IEN),&
      BIA=SST%BIA(IST:IEN),&
      IB=SST%IB(IST:IEN,:),&
      JB=SST%JB(IST:IEN,:),&
      PQ=SST%PQ(IST:IEN,:),&
      BCP=SST%BCP(IST:IEN,1:NPRD_SST),&
      ERR=SST%ERR(IST:IEN),&
      BAC=SST%BAC(IST:IEN),&
      RES=SST%RES(IST:IEN) )

   ENDIF


   IF(JP==1) THEN
      IST=1
   ELSE
      IST=SUM(KKCNT(1:JP-1,NNSSS))+1
   ENDIF
   IEN=SUM(KKCNT(1:JP,NNSSS))

   IF(KKCNT(JP,NNSSS) .GT. 0 ) THEN

      WRITE(CFILE,'(A,I4.4,A)') 'SSSMIS_',JP-1,'.NC'

      CALL IO_OBS(CFILE=CFILE,IOPT=2,NOBS=KKCNT(JP,NNSSS),&
      NLEVS=GRD%KM, NPREDS=1,NPQ=NPQ,NPQ2=NPQ,&
      FLC=SSS%FLC(IST:IEN),&
      TRACK=SSS%TRACK(IST:IEN),&
      EVE=SSS%EVE(IST:IEN),&
      KSAT=SSS%KSAT(IST:IEN),&
      TDIST=SSS%TDIST(IST:IEN),&
      LON=SSS%LON(IST:IEN),&
      LAT=SSS%LAT(IST:IEN),&
      TIM=SSS%TIM(IST:IEN),&
      VAL=SSS%VAL(IST:IEN),&
      BIA=SSS%BIA(IST:IEN),&
      IB=SSS%IB(IST:IEN,:),&
      JB=SSS%JB(IST:IEN,:),&
      PQ=SSS%PQ(IST:IEN,:),&
      BAC=SSS%BAC(IST:IEN),&
      RES=SSS%RES(IST:IEN) )

   ENDIF

ENDDO

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' COUNTS FOR SLA SATELLITES'
DO JSAT=1,NOSLASATS
      SLA%ISLAOBS(JSAT) = COUNT( SLA%KSAT(1:SLA%NO) .EQ. JSAT)
      WRITE(IOUNLOG,*) ' SAT:',JSAT,' ',TRIM(CSLASATID(JSAT)),&
      & '   NO OF OBS:',SLA%ISLAOBS(JSAT)
ENDDO

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' COUNTS FOR SST SATELLITES'
DO JSAT=1,NOSSTSATS
      SST%ISSTOBS(JSAT) = COUNT( SST%KSAT(1:SST%NO) .EQ. JSAT)
      WRITE(IOUNLOG,*) ' SAT:',JSAT,' ',TRIM(CSSTSATID(JSAT)),&
      & '   NO OF OBS:',SST%ISSTOBS(JSAT)
ENDDO

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' COUNTS FOR SSS SATELLITES'
DO JSAT=1,NOSSSSATS
      SSS%ISSSOBS(JSAT) = COUNT( SSS%KSAT(1:SSS%NO) .EQ. JSAT)
      WRITE(IOUNLOG,*) ' SAT:',JSAT,' ',TRIM(CSSSSATID(JSAT)),&
      & '   NO OF OBS:',SSS%ISSSOBS(JSAT)
ENDDO

IF( INS%NO .GT. 0 ) THEN
   WHERE( INS%FLG .NE. 1 ) 
       INS%FLG = 0
       INS%FLC = 0
   END WHERE
   IF( SIZE(INS%FLC) .GT. INS%NO ) &
   INS%FLC( (INS%NO+1):SIZE(INS%FLC) ) = 0
ENDIF

CALL FLUSH(IOUNLOG)
CALL FLUSH(IOUNOUT)

IF ( INS%NO .GT. 0 ) THEN
  DEALLOCATE(INSB%INO,INSB%FLG,INSB%FLC,INSB%PAR)
  DEALLOCATE(INSB%LON,INSB%LAT,INSB%DPT,INSB%TIM)
  DEALLOCATE(INSB%VAL,INSB%BAC,INSB%INC,INSB%TDIST)
  DEALLOCATE(INSB%BIA,INSB%ERR,INSB%EVE,INSB%KTY)
  DEALLOCATE(INSB%RES,INSB%B_A,INSB%OTYPE,INSB%PROF)
  DEALLOCATE(INSB%IB,INSB%JB,INSB%KB,INSB%PLNO)
  DEALLOCATE(INSB%RB)
  DEALLOCATE(INSB%PQ)
  DEALLOCATE(INSB%PRIND,INSB%INST)
  DEALLOCATE(INSB%NIND)
ENDIF

CALL MYFRTPROF_WALL('READOBS103: READ BINARY OBS FILES',1)
END SUBROUTINE READOBS103
