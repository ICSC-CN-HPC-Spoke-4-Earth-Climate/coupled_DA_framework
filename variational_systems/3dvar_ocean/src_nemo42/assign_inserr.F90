SUBROUTINE ASSIGN_INSERR

!.. OBS ERRORS ASSIGNEMENT FOR IN-SITU OBSERVATIONS
!..
!.. ANDREA STORTO, 2009


USE SET_KND
USE OBS_STR
USE GRD_STR
USE OBSDEF
USE NETCDF
USE IOUNITS
USE OBSHANDLING, ONLY : ZTIME_WEIGTH, LL_NEWINSERR, LL_INSREPERR_IRREG
USE MYFRTPROF

IMPLICIT NONE

REAL (R8) :: OERR,RDEP,OEREP
INTEGER(I4) :: JK,ITYP,IPAR,IDEP,FPAR
INTEGER(I4) :: NDEPTHS
INTEGER(I4) :: JJX,JJY
INTEGER(I4) :: NCID, IDVAR, IDDIM,STAT
REAL (R8) :: ZDEP, ZLON, ZLAT
REAL (DP) :: OETIM, TD, TD2, ZT2
REAL (R8), ALLOCATABLE :: INSERRS(:,:), INSDEP(:)
REAL (R8) :: INSFAC(NOPARAMS,NOINSITU)
INTEGER(I4), PARAMETER :: NXR=1440, NYR=720
REAL (R8), ALLOCATABLE :: REPERR(:,:)
REAL(R8), PARAMETER :: MIN_ERR = 0.3_R8
REAL(R8), PARAMETER :: DEF_ERR = 0.7_R8
LOGICAL :: LL_DESROS
REAL (R8), ALLOCATABLE :: ERRDES(:,:,:)

#include "obs_events.h"

CALL MYFRTPROF_WALL('ASSIGN_INSERR: ASSIGN INSITU OBS ERROR',0)

!... READ FROM FILE

CALL CHECK( NF90_OPEN('insitu_errors.nc',NF90_NOWRITE,NCID) )
CALL CHECK( NF90_INQ_DIMID(NCID,'depth',IDDIM) )
CALL CHECK( NF90_INQUIRE_DIMENSION(NCID,IDDIM,LEN=NDEPTHS) )

LL_DESROS=.FALSE.
STAT= NF90_INQ_VARID (NCID, 'sal_des', IDVAR)
IF(STAT .EQ. NF90_NOERR) LL_DESROS=.TRUE.

WRITE(IOUNLOG,*) 'CHECK WHETHER DESROZIER CORRECTION ARE PRESENT OR NOT ',LL_DESROS," ",STAT," ",NF90_NOERR

IF ( LL_DESROS) THEN
    WRITE(IOUNLOG,*) 'DESROZIER CORRECTION FOUND!!!! '
    WRITE(IOUNLOG,*) ' '
    WRITE(IOUNLOG,*) 'APPLIED TO INSITU ERROR'

    ALLOCATE( ERRDES(NDEPTHS,NOINSITU,2),INSDEP(NDEPTHS) )
    CALL CHECK(  NF90_INQ_VARID (NCID, 'sal_des', IDVAR))
    CALL CHECK( NF90_GET_VAR (NCID,IDVAR,ERRDES(:,:,1)) )
    CALL CHECK( NF90_INQ_VARID (NCID, 'tem_des', IDVAR) )
    CALL CHECK( NF90_GET_VAR (NCID,IDVAR,ERRDES(:,:,2)) )
    CALL CHECK( NF90_INQ_VARID (NCID, 'depth', IDVAR) )
    CALL CHECK( NF90_GET_VAR (NCID,IDVAR,INSDEP(:)) )
    INSFAC(:,:)=1.
ELSE
    ALLOCATE( INSERRS(NDEPTHS,2), INSDEP(NDEPTHS) )
    CALL CHECK( NF90_INQ_VARID (NCID, 'sal', IDVAR) )
    CALL CHECK( NF90_GET_VAR (NCID,IDVAR,INSERRS(:,1)) )
    CALL CHECK( NF90_INQ_VARID (NCID, 'tem', IDVAR) )
    CALL CHECK( NF90_GET_VAR (NCID,IDVAR,INSERRS(:,2)) )
    CALL CHECK( NF90_INQ_VARID (NCID, 'depth', IDVAR) )
    CALL CHECK( NF90_GET_VAR (NCID,IDVAR,INSDEP(:)) )
    CALL CHECK( NF90_INQ_VARID (NCID, 'fact', IDVAR) )
    IF( LL_NEWINSERR ) THEN
       CALL CHECK( NF90_GET_VAR (NCID,IDVAR,INSFAC) )
    ELSE
       CALL CHECK( NF90_GET_VAR (NCID,IDVAR,INSFAC(1,:)) )
    ENDIF
ENDIF


IF( LL_INSREPERR_IRREG ) THEN
    ALLOCATE ( REPERR(GRD%IM,GRD%JM) )
ELSE
    ALLOCATE ( REPERR(NXR,NYR) )
ENDIF

CALL CHECK( NF90_INQ_VARID (NCID, 'reperr', IDVAR) )
CALL CHECK( NF90_GET_VAR (NCID,IDVAR,REPERR) )

CALL CHECK( NF90_CLOSE (NCID) )

RDEP=INSDEP(2)-INSDEP(1)

!... FOR INS, CYCLE THE OBS

CYOBS : DO JK=1,INS%NO

     IF(INS%FLC(JK) .NE. 1 ) CYCLE CYOBS

       ! REVISION OF OBS ERRORS

       ITYP=INS%KTY(JK)
       IPAR=INS%PAR(JK)
       ZDEP=INS%DPT(JK)
       FPAR=INS%PAR(JK)
       IF( .NOT. LL_NEWINSERR ) FPAR=1

       ! HANDLE SEPARATELY THE SST CASE
       IF( INS%DSRC(JK) .EQ. 'ICO' .OR. INS%DSRC(JK) .EQ. 'DRF') THEN
         IF( INS%ERR(JK) .LT. 0._R8   ) INS%ERR(JK) = DEF_ERR
         IF( INS%ERR(JK) .LT. MIN_ERR ) INS%ERR(JK) = MIN_ERR
         OERR = INS%ERR(JK)
       ELSEIF( INS%DSRC(JK) .EQ. 'BUF' ) THEN

         IF(.NOT.LL_TQ2_ASSIM) THEN
            INS%FLC(JK) = 0
            INS%EVE(JK) = KEVE_UNSP
            CYCLE CYOBS
         ENDIF

         IF(IPAR.EQ.KKT2M) THEN
            OERR = 0.75_R8
         ELSEIF (IPAR.EQ.KKQ2M) THEN
            OERR = 0.00055_R8
         ELSE
            INS%FLC(JK) = 0
            INS%EVE(JK) = KEVE_AOER
            CYCLE CYOBS
         ENDIF

       ELSE
         IDEP=NINT(ZDEP/RDEP)+1
         IF(IDEP .GT. NDEPTHS) IDEP=NDEPTHS
         IF( ITYP.GT.4 .OR. ITYP .LT.1 .OR. &
           IPAR.GT.2 .OR. IPAR .LT.1 .OR. &
           IDEP.GT.NDEPTHS .OR. IDEP .LT.1 ) THEN
            INS%FLC(JK) = 0
            INS%EVE(JK) = KEVE_AOER
            CYCLE CYOBS
         ENDIF
             IF ( LL_DESROS) THEN 
                OERR = ERRDES(IDEP,ITYP,IPAR)
             ELSE
                OERR = INSERRS(IDEP,IPAR)*INSFAC(FPAR,ITYP)
            ENDIF
       ENDIF

       TD=MIN(ZTIME_WEIGTH,ABS(INS%TDIST(JK)))
       TD2=TD*TD
       ZT2=ZTIME_WEIGTH*ZTIME_WEIGTH

       ZLON=INS%LON(JK)
       ZLAT=INS%LAT(JK)
       IF( LL_INSREPERR_IRREG ) THEN
         JJX=INS%IB(JK,1)
         JJY=INS%JB(JK,1)
       ELSE
         IF( ZLON.LT.0._R8) ZLON=ZLON+360._R8
         JJX=INT( ZLON/0.25_R8 )+1
         JJY=INT( (ZLAT+90._R8)/0.25_R8 )+1
         IF( JJX.GT.NXR .OR. JJX.LT.1 .OR. &
            JJY.GT.NYR .OR. JJY.LT.1 ) THEN
            INS%FLC(JK) = 0
            INS%EVE(JK) = KEVE_AOER
            CYCLE CYOBS
         ENDIF
       ENDIF

       OETIM = 1._DP / EXP( -TD2 / ZT2 )
       OEREP=MIN(MAX(REPERR(JJX,JJY),1._R8),2._R8)
       INS%ERR(JK) = OERR*REAL(OETIM,R8)*OEREP

ENDDO CYOBS

 DEALLOCATE ( REPERR)
 IF (ALLOCATED(ERRDES)) DEALLOCATE(ERRDES)
 iF (ALLOCATED(INSERRS)) DEALLOCATE(INSERRS)
 iF (ALLOCATED(INSDEP)) DEALLOCATE(INSDEP)

 CALL MYFRTPROF_WALL('ASSIGN_INSERR: ASSIGN INSITU OBS ERROR',1)
END SUBROUTINE ASSIGN_INSERR
