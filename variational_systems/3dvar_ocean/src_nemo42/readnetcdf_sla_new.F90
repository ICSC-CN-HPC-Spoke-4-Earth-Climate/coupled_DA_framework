SUBROUTINE READNETCDF_SLA_NEW(CINIFILE,VARNAME,DATEST,DATEEN, &
                              & IMAXO,NTOTOBS,LL_RT,&
                              & PLON,PLAT,PTIM,PVAL,TRACK,&
                              & IONUMB)

!... READ ALONGTRACK SLA PRODUCT IN NEW AVISO NETCDF FORMAT
!
!    ADAPTED FROM AVISO PUBLICREAD SOFTWARE
!    ANDREASTORTO

USE SET_KND
USE NETCDF
USE MYFRTPROF
USE OBSHANDLING, ONLY : LLFILTERSLA,NLANC_KN,ZLANC_CL

IMPLICIT NONE

CHARACTER (LEN=*),  INTENT(IN) :: CINIFILE        ! INPUT IFILE
CHARACTER (LEN=*),  INTENT(IN) :: VARNAME         ! VAR TO EXTRACT
INTEGER(I4),INTENT(IN)         :: IONUMB,IMAXO    ! UNIT FOR DIGNOSTIC OUTPUT
INTEGER(I4),INTENT(OUT)        :: NTOTOBS         ! VALID OBS
REAL(R8)         ,INTENT(IN)   :: DATEST, DATEEN  ! ASSIM TIME WINDOW
REAL(R8),DIMENSION(IMAXO),INTENT(OUT) :: PLON,PLAT,PTIM,PVAL
INTEGER(I4)      ,INTENT(OUT) :: TRACK(IMAXO)

LOGICAL, INTENT(IN) :: LL_RT
REAL(R8), ALLOCATABLE, DIMENSION(:) :: LON, LAT, SLA, TIM
INTEGER(I4), ALLOCATABLE, DIMENSION(:) :: TRK, FLAG
INTEGER(I4) :: NOTR, NOR(2)
INTEGER(I4)  :: JOBS,JT,NT
INTEGER(I4)  :: STAT, NCID, IDVAR
REAL(R8) :: SCALE_FACTOR,ADD_OFFSET,FILLVAL
!.............................................................................

    STAT = NF90_OPEN(CINIFILE, NF90_NOWRITE, NCID)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

    STAT = NF90_INQ_DIMID (NCID, 'time', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = NT)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

    ALLOCATE( SLA(NT), LON(NT), LAT(NT), TRK(NT), TIM(NT),FLAG(NT) )

     IF ( LL_RT ) THEN
      !ALLOCATE( FLAG(NT) )
      STAT = NF90_INQ_VARID (NCID, 'flag', IDVAR)
      IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
      STAT = NF90_GET_VAR (NCID,IDVAR,FLAG)
      IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    ELSE
     FLAG(1:NT)=1
    ENDIF

    STAT = NF90_INQ_VARID (NCID, VARNAME, IDVAR)
    IF (STAT /= NF90_NOERR) THEN
       STAT = NF90_INQ_VARID (NCID, 'sla_filtered', IDVAR)
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    ENDIF
    STAT = NF90_GET_ATT (NCID,IDVAR,'_FillValue',FILLVAL)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

    STAT = NF90_GET_VAR (NCID,IDVAR,SLA)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    DO JT=1,NT
        IF( SLA(JT) .EQ. FILLVAL) FLAG(JT)=0
    ENDDO
    STAT = NF90_GET_ATT (NCID, IDVAR, 'scale_factor',SCALE_FACTOR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_ATT (NCID, IDVAR, 'add_offset',ADD_OFFSET)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    SLA=SLA*SCALE_FACTOR+ADD_OFFSET
    WRITE(IONUMB,*) ' READ SCALE FACTOR ', SCALE_FACTOR
     
    STAT = NF90_INQ_VARID (NCID, 'longitude', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,LON)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
  
    STAT = NF90_GET_ATT (NCID, IDVAR, 'scale_factor',SCALE_FACTOR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_ATT (NCID, IDVAR, 'add_offset',ADD_OFFSET)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    LON=LON*SCALE_FACTOR+ADD_OFFSET
    WRITE(IONUMB,*) ' READ SCALE FACTOR for LON', SCALE_FACTOR
    STAT = NF90_INQ_VARID (NCID, 'latitude', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,LAT)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_ATT (NCID, IDVAR, 'scale_factor',SCALE_FACTOR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_ATT (NCID, IDVAR, 'add_offset',ADD_OFFSET)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    LAT=LAT*SCALE_FACTOR+ADD_OFFSET    
    WRITE(IONUMB,*) ' READ SCALE FACTOR for LAT', SCALE_FACTOR
    STAT = NF90_INQ_VARID (NCID, 'track', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,TRK)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQ_VARID (NCID, 'time', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,TIM)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

    !IF ( LL_RT ) THEN
    !  !ALLOCATE( FLAG(NT) )
    !  STAT = NF90_INQ_VARID (NCID, 'flag', IDVAR)
    !  IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    !  STAT = NF90_GET_VAR (NCID,IDVAR,FLAG)
    !  IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    !ELSE
    ! FLAG(1:NT)=1
    !ENDIF
    !  DO JT=1,NT
    !     IF( SLA(JT) .EQ. FILLVAL) FLAG(JT)=0
    !  ENDDO



    STAT = NF90_CLOSE (NCID)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

    JOBS=0
    NOTR=1
    NOR=0
    !LON=LON*1.E-6_R8
    !LAT=LAT*1.E-6_R8
    !SLA=SLA*1.E-3_R8
    CYOBS : DO JT=1,NT
          IF( FLAG(JT) .NE. 1 ) THEN
	     NOR(1)=NOR(1)+1
	     CYCLE CYOBS
	  ENDIF
          IF( TIM(JT) .LT. DATEST .OR. &
          & TIM(JT) .GT. DATEEN ) THEN
	     NOR(2)=NOR(2)+1
	     CYCLE CYOBS
	  ENDIF
          JOBS=JOBS+1
          IF(JOBS.GT.IMAXO) &
          & CALL ABOR1('READNETCDF_SLA_ATP : MAXTOTALDATA TO INCREASE')
          IF(LON(JT).GT. 180._R8) THEN
             PLON(JOBS) = LON(JT)-360._R8
          ELSE
             PLON(JOBS) = LON(JT)
          ENDIF
          PLAT(JOBS) = LAT(JT)
          PVAL(JOBS) = SLA(JT)
          PTIM(JOBS) = TIM(JT)
          TRACK(JOBS) = TRK(JT)
	  IF(JOBS.GT.1) THEN
	      IF( TRACK(JOBS).NE.TRACK(JOBS-1) ) NOTR = NOTR+1
	  ENDIF
    ENDDO CYOBS

NTOTOBS=JOBS
DEALLOCATE( SLA, LON, LAT, TRK, TIM,FLAG )
!IF ( LL_RT ) DEALLOCATE( FLAG )

WRITE(IONUMB,*)
WRITE(IONUMB,*) ' -----'
WRITE(IONUMB,*) ' AVISO/SLA FILE ',TRIM(CINIFILE)
WRITE(IONUMB,*) ' NUMBER OF TRACKS', NOTR
WRITE(IONUMB,*) ' TOTAL OBS IN FILE : ',NT
WRITE(IONUMB,*) ' UNVALID FLAG (RT) : ',NOR(1)
WRITE(IONUMB,*) ' OUT OF TIME       : ',NOR(2)
WRITE(IONUMB,*) ' VALID OBS         : ',NTOTOBS
WRITE(IONUMB,*)

END SUBROUTINE READNETCDF_SLA_NEW
