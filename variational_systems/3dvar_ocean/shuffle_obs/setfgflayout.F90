SUBROUTINE SETFGFLAYOUT

USE SET_KND
USE NETCDF
USE READFG

IMPLICIT NONE

INTEGER(I4) :: NCID, IDVAR, JF, IAUX(2), IAUX2(2), IERR
INTEGER :: STAT
WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' /// SETUP FIRST-GUESS LAYOUT'

LLMPF = ( NSUBDOMAINS .GT. 1 )

IF(.NOT.LLMPF) THEN
    KDPF(1,1)=1
    KDPF(1,2)=1
    KDPL(1,1)=IM
    KDPL(1,2)=JM
    KDHSS(1,1)=0
    KDHSS(1,2)=0
    KDHSE(1,1)=0
    KDHSE(1,2)=0
    KDSL(1,1)=IM
    KDSL(1,2)=JM
ELSE
  IF(LLAYOUTFILE) THEN
     OPEN(712,FILE='domains.dat',STATUS='OLD',IOSTAT=IERR)
     IF(IERR.NE.0) CALL ABOR1('SETFGFLAYOUT: CANNOT OPEN DOMAINS.DAT')
     DO JF=1,NSUBDOMAINS
        !READ(712,*,IOSTAT=IERR) IAUX(1:2),KDPF(JF,1:2),KDPL(JF,1:2),KDHSS(JF,1:2),&
        !& KDHSE(JF,1:2),KDSL(JF,1:2),IAUX2(1:2),KDNS(JF,1:2),KDNE(JF,1:2),&
        !& KDLS(JF,1:2),KDLE(JF,1:2)
        READ(712,*,IOSTAT=IERR) IAUX(1),KDPF(JF,1:2),KDPL(JF,1:2),KDHSS(JF,1:2),&
        & KDHSE(JF,1:2),KDSL(JF,1:2),IAUX2(1:2)

        KDNS=KDPF+KDHSS
        KDNE=KDPL-KDHSE
        KDLS=1+KDHSS
        KDLE=KDSL-KDHSE

        IF(IERR.NE.0) CALL ABOR1('SETFGFLAYOUT: CANNOT READ DOMAINS.DAT')
        ! ROUGHLY CHECK
        WRITE(IOUNLOG,*) IM,JM
        IF( (IAUX(1).NE.(JF-1) .OR.IAUX2(1).NE.IM.OR.&
        &  IAUX2(2).NE.JM ) .AND. .NOT. LLPREP ) THEN
           WRITE(IOUNERR,*) 'WRONG DOMAINS.DAT FILE',IAUX(:),IAUX2(:),JM
           CALL ABOR1('SETFGFLAYOUT: WRONG DOMAINS.DAT')
        ENDIF
     ENDDO
     CLOSE(712)
  ELSE
! ...BEWARE OF POSSIBLE SPURIOUS VALUES WHEN NF90_GET_ATT READ ARRAYS...
    DO JF=1,NSUBDOMAINS
       WRITE(IOUNLOG,*) ' ABOUT TO OPEN FILE '//TRIM(CFGFILES(JF))
       STAT = NF90_OPEN(CFGFILES(JF), NF90_NOWRITE, NCIDFG(JF))
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

       STAT=NF90_GET_ATT(NCIDFG(JF),NF90_GLOBAL,'DOMAIN_NUMBER_TOTAL',IAUX(1))
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

       IF(IAUX(1).NE.NSUBDOMAINS) THEN
          WRITE(IOUNERR,*) &
          & 'FILE ',TRIM(CFGFILES(JF)),' CONTAINS WRONG DOMAIN_NUMBER_TOTAL'
          CALL ABOR1('SETFGFLAYOUT: WRONG DOMAIN_NUMBER_TOTAL')
       ENDIF

       STAT=NF90_GET_ATT(NCIDFG(JF),NF90_GLOBAL,'DOMAIN_POSITION_FIRST',KDPF(JF,1:2))
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
       STAT=NF90_GET_ATT(NCIDFG(JF),NF90_GLOBAL,'DOMAIN_POSITION_LAST',KDPL(JF,1:2))
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
       STAT=NF90_GET_ATT(NCIDFG(JF),NF90_GLOBAL,'DOMAIN_HALO_SIZE_START',KDHSS(JF,1:2))
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
       STAT=NF90_GET_ATT(NCIDFG(JF),NF90_GLOBAL,'DOMAIN_HALO_SIZE_END',KDHSE(JF,1:2))

       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

       STAT=NF90_GET_ATT(NCIDFG(JF),NF90_GLOBAL,'DOMAIN_SIZE_LOCAL',KDSL(JF,1:2))
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
       STAT=NF90_GET_ATT(NCIDFG(JF),NF90_GLOBAL,'DOMAIN_SIZE_GLOBAL',IAUX)
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

       WRITE(IOUNLOG,*) 'DOMAIN',JF,NCIDFG(JF),':',&
       & KDPF(JF,:),KDPL(JF,:),KDHSS(JF,:),KDHSE(JF,:),KDSL(JF,:),IAUX(:)
       CALL FLUSH(IOUNLOG)

       IF(IAUX(1).NE.IM .OR.IAUX(2).NE.JM) THEN
          WRITE(IOUNERR,*) '/// DOMAIN',JF,' FILE',JF-1
          WRITE(IOUNERR,*) 'SETFGFLAYOUT: GLOBAL DIMS MISMATCH'
          WRITE(IOUNERR,*) 'FOUND    :',IAUX(1),IAUX(2)
          WRITE(IOUNERR,*) 'EXPECTED :',IM,JM
          CALL ABOR1('SETFGFLAYOUT: WRONG DOMAIN_SIZE_GLOBAL')
       ENDIF
       IF( KDPL(JF,1)-KDPF(JF,1)+1.NE.KDSL(JF,1) .OR. &
         & KDPL(JF,2)-KDPF(JF,2)+1.NE.KDSL(JF,2) ) THEN
          WRITE(IOUNERR,*) '/// DOMAIN',JF,' FILE',JF-1
          WRITE(IOUNERR,*) 'SETFGFLAYOUT: LOCAL DIMS MISMATCH'
          WRITE(IOUNERR,*) 'FOUND    :',KDSL(JF,1:2)
          WRITE(IOUNERR,*) 'EXPECTED :',KDPL(JF,1)-KDPF(JF,1)+1,&
          & KDPL(JF,2)-KDPF(JF,2)+1
          CALL ABOR1('SETFGFLAYOUT: WRONG DOMAIN_SIZE_LOCAL')
       ENDIF

       STAT = NF90_CLOSE(NCIDFG(JF))
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    ENDDO

    KDNS=KDPF+KDHSS
    KDNE=KDPL-KDHSE
    KDLS=1+KDHSS
    KDLE=KDSL-KDHSE

  ENDIF
ENDIF

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' LAYOUT FILE READING'
DO JF=1,NSUBDOMAINS
  WRITE(IOUNLOG,*) ' DOM ',JF, ' :: ',KDPF(JF,1:2),KDPL(JF,1:2),KDHSS(JF,1:2),&
        & KDHSE(JF,1:2),KDSL(JF,1:2),KDNS(JF,1:2),KDNE(JF,1:2),&
        & KDLS(JF,1:2),KDLE(JF,1:2)
ENDDO

CALL FLUSH(IOUNLOG)
END SUBROUTINE SETFGFLAYOUT
